'
'                      ------------------- 
'                     |    Twin Baston    |
'                      ------------------- 
'
'
'               * *   ©1992  PowerMob Prod.   * *  
'                *                             * 
'               * *       Ronan Amicel        * *  
'                *       David Thévenot        * 
'               * *    et Eric Hilphripre     * *
'
'
'************************************************************
'
'  NOM           :  TWIN BASTON  
'  VERSION       :  0.80 
'  AUTEUR        :  RONAN AMICEL alias MR REED 
'  DERNIERE MOD. :  25.04.92   
'  DESCRIPTION   :  Jeu de Plates-formes à 2 Joueurs 
'
'************************************************************
'
'
'
Default 
Dir$="Df0:"
Dim C(14,14)
Dim HISCORE(10)
Dim HISCORE$(10)
Global C()
V$="0.80"
Dim ENNEMI(5),TRESOR(3)
Dim DANSLESPOMMES(16)
JOUEUR1=8
JOUEUR2=9
TIR1=1
TIR2=2
ENNEMI(1)=12
ENNEMI(2)=13
ENNEMI(3)=14
ENNEMI(4)=15
ENNEMI(5)=16
TRESOR(1)=3
TRESOR(2)=4
TRESOR(3)=5
BONUS=6
Restore SCORES
For I=1 To 10
   Read HISCORE$(I),HISCORE(I)
Next I
SCORES:
Data "POWERMOB PROD.",100000
Data "POWERMOB PROD.",80000
Data "POWERMOB PROD.",70000
Data "POWERMOB PROD.",60000
Data "POWERMOB PROD.",50000
Data "POWERMOB PROD.",40000
Data "POWERMOB PROD.",30000
Data "POWERMOB PROD.",20000
Data "POWERMOB PROD.",10000
Data "POWERMOB PROD.",5000
MUSIQUEVOLUME=63
BRUITAGESVOLUME=63
ACTIVEMUSIQUE=True
ACTIVEPUB=True
NAME1$="Player 1"
NAME2$="Player 2"
Proc INIT
Auto View Off : Wait Vbl 
Screen Open 0,320,256,32,Lowres
Screen Display 0,128,40,320,256
Curs Off : Flash Off : Hide On 
Cls 0
Double Buffer 
Auto View On : Wait Vbl 
Make Mask 
'
'************************************* 
'
PRES:
Proc PRESENTATION
Proc OPTIONS
LEVEL=1
SCORE1=0
SCORE2=0
If ACTIVEMUSIQUE
   Proc MUSIQUE[2,0]
End If 
For I=ENNEMI(1) To ENNEMI(5)
   DANSLESPOMMES(I)=0
Next I
Proc _GETREADY
Proc CHARGER1[LEVEL]
Timer=0
Repeat 
Until Fire(1) or Fire(0) or Timer>=400
Fade 1
Wait 15
Sprite Off 
Cls 0
'
'****************************************
'
Repeat 
   '
   MORT1=False
   MORT2=False
   PARALIZED1=False
   PARALIZED2=False
   Proc INIT_TABLEAU
   Wait Vbl 
   Amal On 
   Update Every 2
   Timer=0
   '
   Repeat 
      '- - - - - JOUEUR 1 - - - - - -
      If MORT1=False
         If PARALIZED1=False
            Y1=Y Sprite(8)-40
            X1=X Sprite(8)-128
            If Y1<208
               Amreg(1,0)=C(X1/16,(Y1+31)/16)
               Amreg(1,1)=C((X1+15)/16,(Y1+31)/16)
            Else 
               Amreg(1,0)=1
            End If 
            If X1>15 and(X1 mod 16=0)
               Amreg(1,2)=C((X1-16)/16,(Y1+15)/16)
            Else 
               Amreg(1,2)=0
            End If 
            If X1<240
               Amreg(1,3)=C((X1+15)/16,(Y1+15)/16)
            Else 
               Amreg(1,3)=1
            End If 
            '
            If Spritebob Col(JOUEUR1,BONUS To BONUS)
               Proc EFFET_BONUS1
               Proc BONUS
            End If 
            '
            If Spritebob Col(JOUEUR1,TRESOR(1) To TRESOR(3))
               If CARRY1=0
                  If Col(TRESOR(1))
                     CARRY1=1
                  End If 
                  If Col(TRESOR(2))
                     CARRY1=2
                  End If 
                  If Col(TRESOR(3))
                     CARRY1=3
                  End If 
                  Bob Off TRESOR(CARRY1)
                  Proc UPDSCORE[100,0]
                  If ACTIVEMUSIQUE : Sam Play %1,2 : End If 
               End If 
            End If 
            '
            If X1=16 and Y1=223
               If CARRY1>0
                  CARRY1=0
                  Inc RAMENE1
                  Proc UPDSCORE[2000,0]
                  If RAMENE1+RAMENE2=3
                     PASSE=True
                  End If 
                  If ACTIVEMUSIQUE : Sam Play %1,10 : End If 
               End If 
               If Jdown(1)
                  Inc TEMPS1
                  If TEMPS1>189
                     TEMPS1=189
                  End If 
                  Ink 20
                  TP=206-TEMPS1
                  Draw 1,TP+1 To 14,TP+1
                  Draw 1,TP To 14,TP
               End If 
            End If 
         Else 
            Inc PARALIZED1
            If PARALIZED1=200
               PARALIZED1=0
               Amal On 1
               UPDSCORE[0,0]
            End If 
         End If 
         '  à exécuter toujours 
         If Bobsprite Col(TIR1,ENNEMI(1) To ENNEMI(NBMONSTRES))
            For I=ENNEMI(1) To ENNEMI(5)
               Exit If Col(I)
            Next I
            If DANSLESPOMMES(I)=False
               DANSLESPOMMES(I)=1
               Amal Freeze I-7
               Bob TIR1,,320,
               If ACTIVEMUSIQUE : Shoot : End If 
               Proc UPDSCORE[10,0]
            End If 
         End If 
         If Bobsprite Col(TIR1,JOUEUR2 To JOUEUR2) and PARALIZED2=False
            PARALIZED2=1
            Amal Freeze 2
            Bob TIR1,,320,
            If ACTIVEMUSIQUE : Shoot : End If 
            Proc UPDSCORE[50,0]
         End If 
         If Sprite Col(JOUEUR1,ENNEMI(1) To ENNEMI(NBMONSTRES))
            MORT1=True
            If ACTIVEMUSIQUE : Boom : End If 
            Amal 1,"Let A=40; P; P; P; P; P; Let A=0; End"
            Amal On 
            Bob Off TIR1
            If CARRY1<>0
               Bob CARRY1,X1,Y1-15,37
            End If 
            UPDSCORE[0,0]
         End If 
         ' si à 0 
         If TEMPS1=0
            MORT1=True
            If ACTIVEMUSIQUE
               Sam Play %1,4
               Wait 5
               Sam Play %1,4
            Else 
               Wait 5
            End If 
            Amal Off 1
            Wait Vbl 
            Sprite JOUEUR1,,,40
            Wait 5
            Sprite Off JOUEUR1
            If CARRY1<>0
               Bob CARRY1,X1,Y1-15,37
            End If 
         End If 
      End If 
      ' - - - - - - JOUEUR 2 - - - - - - - - - 
      If MORT2=False
         If PARALIZED2=False
            Y2=Y Sprite(9)-40
            X2=X Sprite(9)-128
            If Y2<208
               Amreg(2,0)=C(X2/16,(Y2+31)/16)
               Amreg(2,1)=C((X2+15)/16,(Y2+31)/16)
            Else 
               Amreg(2,0)=1
            End If 
            If X2>15 and(X2 mod 16=0)
               Amreg(2,2)=C((X2-16)/16,(Y2+15)/16)
            Else 
               Amreg(2,2)=0
            End If 
            If X2<240
               Amreg(2,3)=C((X2+15)/16,(Y2+15)/16)
            Else 
               Amreg(2,3)=1
            End If 
            If Spritebob Col(JOUEUR2,BONUS To BONUS)
               Proc EFFET_BONUS1
               Proc BONUS
            End If 
            If Spritebob Col(JOUEUR2,TRESOR(1) To TRESOR(3))
               If CARRY2=0
                  If Col(TRESOR(1))
                     CARRY2=1
                  End If 
                  If Col(TRESOR(2))
                     CARRY2=2
                  End If 
                  If Col(TRESOR(3))
                     CARRY2=3
                  End If 
                  Bob Off TRESOR(CARRY2)
                  Proc UPDSCORE[0,100]
                  If ACTIVEMUSIQUE : Sam Play %10,2 : End If 
               End If 
            End If 
            If X2=224 and Y2=223
               If CARRY2>0
                  CARRY2=0
                  Inc RAMENE2
                  If RAMENE1+RAMENE2=3
                     PASSE=True
                  End If 
                  Proc UPDSCORE[0,2000]
                  If ACTIVEMUSIQUE : Sam Play %10,10 : End If 
               End If 
               If Jdown(0) and(PARALIZED2=False)
                  Inc TEMPS2
                  If TEMPS2>189
                     TEMPS2=189
                  End If 
                  Ink 20
                  TP=206-TEMPS2
                  Draw 241,TP+1 To 254,TP+1
                  Draw 241,TP To 254,TP
               End If 
            End If 
         Else 
            Inc PARALIZED2
            If PARALIZED2=200
               PARALIZED2=0
               Amal On 2
               UPDSCORE[0,0]
            End If 
         End If 
         If Bobsprite Col(TIR2,ENNEMI(1) To ENNEMI(NBMONSTRES))
            For I=ENNEMI(1) To ENNEMI(5)
               Exit If Col(I)
            Next I
            If DANSLESPOMMES(I)=False
               DANSLESPOMMES(I)=1
               Amal Freeze I-7
               Bob TIR2,,320,
               If ACTIVEMUSIQUE
                  Shoot 
               End If 
               Proc UPDSCORE[0,10]
            End If 
         End If 
         If Bobsprite Col(TIR2,JOUEUR1 To JOUEUR1) and PARALIZED1=False
            PARALIZED1=1
            Amal Freeze 1
            Bob TIR2,,310,
            If ACTIVEMUSIQUE
               Shoot 
            End If 
            Proc UPDSCORE[0,50]
         End If 
         If Sprite Col(JOUEUR2,ENNEMI(1) To ENNEMI(NBMONSTRES))
            MORT2=True
            Boom 
            Amal Off 2
            Sprite JOUEUR2,,,40
            Wait 5
            Sprite Off JOUEUR2
            If CARRY2<>0
               Bob CARRY2,X2,Y2-15,37
            End If 
            UPDSCORE[0,0]
         End If 
         ' gestion du temps 
         If TEMPS2<1
            MORT2=True
            UPDSCORE[0,0]
            If ACTIVEMUSIQUE
               Sam Play %10,4
               Wait 5
               Sam Play %10,4
            Else 
               Wait 5
            End If 
            Amal Off 2
            Wait Vbl 
            Sprite JOUEUR2,,,40
            Wait 5
            Sprite Off JOUEUR2
            If CARRY2<>0
               Bob CARRY2,X2,Y2-15,37
            End If 
         End If 
      End If 
      ' - - - - - - - AUTRES GESTIONS - - - - - - - - - - - -
      If Timer>12
         Ink 0
         If MORT1=False
            Dec TEMPS1
            Draw 1,205-TEMPS1 To 14,205-TEMPS1
            Draw 1,206-TEMPS1 To 14,206-TEMPS1
         End If 
         If MORT2=False
            Dec TEMPS2
            Draw 241,205-TEMPS2 To 254,205-TEMPS2
            Draw 241,206-TEMPS2 To 254,206-TEMPS2
         End If 
         Timer=0
      End If 
      For I=ENNEMI(1) To ENNEMI(NBMONSTRES)
         If DANSLESPOMMES(I)=200 Then DANSLESPOMMES(I)=0 : Amal On I-7
         If DANSLESPOMMES(I)<>0 Then Inc DANSLESPOMMES(I)
      Next I
      '
      K$=Inkey$
      If K$<>""
         K$=Upper$(K$)
         If Asc(K$)=27
            Erase 2
            Erase 3
            Erase 5
            Screen Close 1
            Direct 
         End If 
         If K$="P"
            Every Off 
            Amal Freeze 
            Repeat 
            Until Upper$(Inkey$)="P"
            For I=1 To ENNEMI(NBMONSTRES)-7
               If DANSLESPOMMES(I+7)=0
                  Amal On I
               End If 
            Next I
            Every On 
         End If 
         If K$="M"
            Print At(3,1);"Fast:";Fast Free;" / Chip:";Chip Free;"  "
            Print At(3,2);"R0 =";Amreg(1,0);" / R1 =";Amreg(1,1);"  "
         End If 
         If K$="+"
            PASSE=True
         End If 
      End If 
      '
      Wait Vbl 
      '
   Until(MORT1 and MORT2=True) or PASSE
   '
   '**********************************************
   '
   Every Off 
   Amal Off 
   MUSIQUE_OFF
   Wait 2
   Rainbow 0,0,300,32
   Rainbow Del 
   Wait 2
   Fade 2
   Wait 30
   Wait Vbl 
   Bob Off 
   Sprite Off 
   Wait Vbl 
   If PASSE
      PASSE=False
      If ACTIVEMUSIQUE
         Sam Play %1111,5
      End If 
      If RAMENE1>=RAMENE2
         Proc WINNER[1]
      Else 
         Proc WINNER[2]
      End If 
      If ACTIVEPUB
         Proc PUB[LEVEL]
      End If 
      Inc LEVEL
      _GETREADY
      CHARGER1[LEVEL]
      Timer=0
      Repeat 
      Until Fire(1) or Fire(0) or Timer>=400
      Fade 1
      Wait 15
      Sprite Off 
      Cls 0
   End If 
Until MORT1 and MORT2
' - - - - - - - - - - - - - - -
FINBOUCLE:
Proc _GAME_OVER
For I=1 To 10
   If SCORE1>HISCORE(I)
      HISCORE(I)=SCORE1
      HISCORE$(I)=NAME1$
      Exit 
   End If 
Next I
For I=1 To 10
   If SCORE2>HISCORE(I)
      HISCORE(I)=SCORE2
      HISCORE$(I)=NAME2$
   End If 
Next I
HALL_OF_FAME
Repeat 
Until Fire(1)=0 and Fire(0)=0
Repeat 
Until Fire(1) or Fire(0)
Fade 1
Goto PRES
'
'***************** PROCEDURES **************** 
'
'
Procedure INIT
   ' Banques:  1=spr. 2=icons  3=music  5=spl  8=fond  9-10=imgs à charger  
   Shared MUSIQUEON,QUELLEMUSIQUE
   Close Editor 
   Close Workbench 
   Default 
   Screen Close 0
   Colour Back 0
   Screen Open 2,320,16,2,Lowres
   Screen Hide 2
   Screen Open 1,320,16,2,Lowres
   Screen Hide 1
   Curs Off : Flash Off : Hide On 
   Cls 0
   Proc _COPYRIGHT
   Load "Intro.Abk",3
   Load "TwinPres.Abk",9
   Unpack 9 To 0
   Wait Vbl 
   Erase 9
   Music 1
   MUSIQUEON=True
   QUELLEMUSIQUE=1
   Load "Twin2.Abk"
   Load "TwinIcons.Abk",2
   Load "TwinBack2.Abk",8
   Unpack 8 To 1
   Screen Hide 1
   Erase 8
End Proc
Procedure PRESENTATION
   Shared MUSIQUEVOLUME,MUSIQUEON,V$
   Screen 0 : Screen To Front 0 : Cls 0
   Load "Twinpres2.abk",9
   Load "Twinpres3.abk",10
   If MUSIQUEON=False
      Proc MUSIQUE[1,MUSIQUEVOLUME]
   End If 
   Wait Vbl 
   Screen Show 0
   Do 
      Auto View Off : Wait Vbl 
      Unpack 9 To 2
      Screen 2
      Screen To Front 2
      Screen Display 2,128,40,,
      Palette 0,0
      Auto View On : Wait Vbl 
      Fade 1,$0,$FFF
      For I=1 To 250
         If Fire(1) or Fire(0)
            Exit 2
         End If 
         Wait Vbl 
      Next I
      Fade 1 : Wait 15
      Auto View Off : Wait Vbl 
      Unpack 10 To 2
      Palette 0,0 : Auto View On 
      Wait Vbl : Fade 1,$0,$FFF
      Screen Display 2,128,40,,
      For I=1 To 250
         If Fire(1) or Fire(0)
            Exit 2
         End If 
         Wait Vbl 
      Next I
      Fade 1 : Wait 15
      Screen 0 : Screen To Front 0
      Proc HALL_OF_FAME
      For I=1 To 250
         If Fire(1) or Fire(0)
            Exit 2
         End If 
         Wait Vbl 
      Next I
      Fade 1 : Wait 15
   Loop 
   '
   Repeat 
   Until Fire(1)=0 and Fire(0)=0
   Fade 1
   Wait 5
   Screen Hide 0
   Screen Hide 2
   Screen Close 2
   Screen 0
   Screen To Front 0
   Flash Off 
   Cls 0
   Screen Show 0
   Proc MUSIQUE_OFF
   Erase 9
   Erase 10
End Proc
Procedure INIT_TABLEAU
   Shared LEVEL,TEMPS1,TEMPS2,ACTIVEMUSIQUE,MUSIQUEVOLUME,V$,MORT1,MORT2
   Shared ENNEMI(),NBMONSTRES,JOUEUR1,JOUEUR2,TIR1,TIR2
   '
   Auto View Off 
   Screen Open 0,320,256,32,Lowres
   Screen Hide 0
   Curs Off : Flash Off : Hide On 
   View 
   Auto View On 
   Wait Vbl 
   Restore _TEMPS
   For I=1 To LEVEL
      Read TEMPS1,TEMPS2
   Next I
   _TEMPS:
   Data 190,190,190,190,190,190,190,190,190,190,190,190,190,190,190,190,190,190,190,190
   Data 190,190,190,190,190,190,190,190,190,190,190,190,190,190,190,190,190,190,190,190
   '
   '
   Cls 0
   Paper 0
   Pen 1
   Get Palette 1 : Wait Vbl 
   Screen Copy 1 To 0 : Wait Vbl 
   Screen Display 0,128,40,320,256
   Proc REFOUS_PALETTE
   Ink 20
   Paint 1,16
   Paint 241,16
   Ink 3
   Paint 20,2
   Colour 3,$6
   Set Rainbow 0,3,226,"","","(14,1,16)"
   Rainbow 0,0,40,226
   Ink 11,0
   Cls 0,1,225 To 126,254
   Cls 0,129,225 To 254,254
   Proc UPDSCORE[0,0]
   If MORT1
      Text 12,242,"    OH NO !   "
   End If 
   If MORT2
      Text 140,242,"   OH NO !   "
   End If 
   '
   For CX=1 To 14
      For CY=1 To 14
         If C(CX,CY)=1
            Paste Bob CX*16,CY*16-16,1
         End If 
      Next CY
   Next CX
   Double Buffer 
   Autoback 1
   '
   '
   Sprite JOUEUR1,144,263,3
   Bob TIR1,10,320,39
   Sprite JOUEUR2,352,263,7
   Bob TIR2,30,320,39
   Limit Bob 16,0 To 240,223
   '
   ' Programmes AMAL
   '
   ' Joueur 1 
   '
   A$="      r zero case en dessous gauche ;"
   A$=A$+"   r un case en dessous droite ;"
   A$=A$+"   r deux case a gauche ;"
   A$=A$+"   r trois case a droite ;"
   A$=A$+"   r quatre empjump ;"
   A$=A$+"   r a  emp tirer"
   A$=A$+"   Let X=144 ;"
   A$=A$+"   Let Y=263 ;"
   A$=A$+"   Let R0=1 ; Let RA=0;"
   A$=A$+"AUtotest("
   A$=A$+"   If R4=0 then Jump Gauche ;"
   A$=A$+"   If J1&1<>0 then Jump Gauche ;"
   A$=A$+"   Let R4=0 ;"
   A$=A$+"Gauche:"
   A$=A$+"   If J1&4=0 then Jump Droite ;"
   A$=A$+"   If R2=1 then Jump Droite ;"
   A$=A$+"   If X=144 then Jump Droite ;"
   A$=A$+"   Let X=X-1 ; Let A=4 ;"
   A$=A$+"Droite:"
   A$=A$+"   If J1&8=0 then Jump Fire ;"
   A$=A$+"   If R3=1 then Jump Fire ;"
   A$=A$+"   If X=352 then Jump Fire ;"
   A$=A$+"   Let X=X+1 ; Let A=3 ;"
   A$=A$+"Fire:"
   A$=A$+"   If J1&16=0 then eXit;"
   A$=A$+"   If RA=1 then eXit;"
   A$=A$+"   Let RA=1; Let RB=1;"
   A$=A$+"   Let RC=X; Let RD=Y;"
   A$=A$+"   Let RE=A;"
   A$=A$+"   eXit; )"
   A$=A$+"Sauter:"
   A$=A$+"   If R4=1 then Jump Tomber ;"
   A$=A$+"   If J1&1=0 then Jump Tomber ;"
   A$=A$+"   If Y<70 then Jump Tomber ;"
   A$=A$+"   Let R4=1 ;"
   A$=A$+"   Move 0,-32,16 ; Pause; Pause;"
   A$=A$+"Tomber:"
   A$=A$+"   If R0=1 then Jump Sauter ;"
   A$=A$+"   If R1=1 then Jump Sauter ;"
   A$=A$+"   Move 0,16,8 ;"
   A$=A$+"   Jump Tomber;"
   '
   ' Tir 1
   '
   C$="   Loop:"
   C$=C$+"   If RB=0 then Jump Loop;"
   C$=C$+"   Let RB=0; Let A=39;"
   C$=C$+"   Let X=XS(0,RC); Let Y=YS(0,RD);"
   C$=C$+"   If RE=3 then Jump M;"
   C$=C$+"   Move -48,0,12;"
   C$=C$+"   Jump N;"
   C$=C$+"M: Move 48,0,12;"
   C$=C$+"N: Let Y=310;"
   C$=C$+"   Let RA=0"
   C$=C$+"   Jump Loop;"
   '
   ' Joueur 2 
   '
   B$="      r zero case en dessous gauche ;"
   B$=B$+"   r un case en dessous droite ;"
   B$=B$+"   r deux case a gauche ;"
   B$=B$+"   r trois case a droite ;"
   B$=B$+"   r quatre et r cinq empjump ;"
   B$=B$+"   Let X=352 ;"
   B$=B$+"   Let Y=263 ;"
   B$=B$+"   Let R0=1 ;"
   B$=B$+"AUtotest("
   B$=B$+"   If R4=0 then Jump Gauche ;"
   B$=B$+"   If J0&1<>0 then Jump Gauche ;"
   B$=B$+"   Let R4=0 ;"
   B$=B$+"Gauche:"
   B$=B$+"   If J0&4=0 then Jump Droite ;"
   B$=B$+"   If R2=1 then Jump Droite ;"
   B$=B$+"   If X=144 then Jump Droite ;"
   B$=B$+"   Let X=X-1 ; Let A=7 ;"
   B$=B$+"Droite:"
   B$=B$+"   If J0&8=0 then Jump Fire ;"
   B$=B$+"   If R3=1 then Jump Fire ;"
   B$=B$+"   If X=352 then Jump Fire ;"
   B$=B$+"   Let X=X+1 ; Let A=6 ;"
   B$=B$+"Fire:"
   B$=B$+"   If J0&16=0 then eXit;"
   B$=B$+"   If RA=1 then eXit;"
   B$=B$+"   Let RF=1; Let RG=1;"
   B$=B$+"   Let RH=X; Let RI=Y;"
   B$=B$+"   Let RJ=A;"
   B$=B$+"   eXit; )"
   B$=B$+"Sauter:"
   B$=B$+"   If R4=1 then Jump Tomber ;"
   B$=B$+"   If J0&1=0 then Jump Tomber ;"
   B$=B$+"   If Y<70 then Jump Tomber ;"
   B$=B$+"   Let R4=1 ;"
   B$=B$+"   Move 0,-32,16 ; Pause; Pause;"
   B$=B$+"Tomber:"
   B$=B$+"   If R0=1 then Jump Sauter ;"
   B$=B$+"   If R1=1 then Jump Sauter ;"
   B$=B$+"   Move 0,16,8 ;"
   B$=B$+"   Jump Tomber;"
   '
   ' Tir 2
   '
   D$="   Loop:"
   D$=D$+"   If RG=0 then Jump Loop;"
   D$=D$+"   Let RG=0; Let A=39;"
   D$=D$+"   Let X=XS(0,RH); Let Y=YS(0,RI);"
   D$=D$+"   If RJ=6 then Jump M;"
   D$=D$+"   Move -48,0,12;"
   D$=D$+"   Jump N;"
   D$=D$+"M: Move 48,0,12;"
   D$=D$+"N: Let Y=320;"
   D$=D$+"   Let RF=0"
   D$=D$+"   Jump Loop;"
   '
   Channel 1 To Sprite JOUEUR1
   Channel 2 To Sprite JOUEUR2
   Channel 3 To Bob TIR1
   Channel 4 To Bob TIR2
   Amal 1,A$
   Amal 2,B$
   Amal 3,C$
   Amal 4,D$
   '
   '
   Restore "L"+Str$(LEVEL)-" "
   Read NBMONSTRES
   For I=1 To NBMONSTRES
      Read NMI,NMX,NMY,NMD,NMV
      Sprite ENNEMI(I),NMX,NMY,NMI
      Channel I+4 To Sprite ENNEMI(I)
      A$="L: Move "+Str$(NMD)+",0,"+Str$(NMV)+";"
      A$=A$+"Let A="+Str$(NMI+1)+";"
      A$=A$+"Move -"+Str$(NMD)+",0,"+Str$(NMV)+";"
      A$=A$+"Let A="+Str$(NMI)+";"
      A$=A$+"Jump L;"
      Amal I+4,A$
   Next I
   Rem Nombre , Image , X début , Y début , Déplacement , Nb étapes 
   L1: Data 2,11,192,135,113,113,13,208,71,79,79
   L2: Data 2,19,240,87,79,79,21,224,183,48,48
   L3: Data 2,11,176,167,144,144,13,224,103,48,48
   L4: Data 2,13,208,167,80,80,15,208,103,80,80
   L5: Data 3,17,144,231,160,160,19,224,167,32,32,21,224,87,64,64
   L6: Data 2,21,144,199,64,64,21,272,135,64,64
   L7: Data 2,9,288,199,64,64,11,144,135,64,64
   L8: Data 0
   L9: Data 0
   L10: Data 0
   L11: Data 0
   L12: Data 0
   L13: Data 0
   L14: Data 0
   L15: Data 0
   L16: Data 0
   L17: Data 0
   L18: Data 0
   L19: Data 0
   L20: Data 0
   '
   Timer=0
   Every 500 Proc BONUS
   Clear Key 
   '
   Proc TRESORS
   '
   Screen Show 0
   '
   If ACTIVEMUSIQUE
      MUSIQUE[2,MUSIQUEVOLUME]
   End If 
   '
   Wait Vbl 
   '
End Proc
Procedure CHARGER1[N]
   Shared C()
   LEVEL=N
   Open Random 1,"Twindirect.Level"
   Field 1,1 As CASE$
   INDEX=(N-1)*196
   For X=1 To 14
      For Y=1 To 14
         Inc INDEX
         Get 1,INDEX
         C(X,Y)=Val(CASE$)
         If C(X,Y)=2 Then C(X,Y)=37
      Next Y
   Next X
   Close 1
End Proc
Procedure BONUS
   Shared BONUS,LEVEL,CURBONUS
   Every Off 
   Repeat 
      X=Rnd(13)+1
      Y=Rnd(13)+1
   Until C(X,Y)=False
   CURBONUS=Rnd(11)+26
   Bob BONUS,X*16,Y*16-16,CURBONUS
   Every On 
End Proc
Procedure EFFET_BONUS1
   Shared CURBONUS,SCORE1,SCORE2,ACTIVEMUSIQUE
   A=CURBONUS-25
   DEBEFF:
   If A=1
      If ACTIVEMUSIQUE : Sam Play %1,1 : End If 
      UPDSCORE[200,0]
      Pop Proc
   End If 
   If A=2
      If ACTIVEMUSIQUE : Sam Play %1,1 : End If 
      UPDSCORE[300,0]
      Pop Proc
   End If 
   If A=3
      If ACTIVEMUSIQUE : Sam Play %1,1 : End If 
      UPDSCORE[100,0]
      Pop Proc
   End If 
   If A=4
      If ACTIVEMUSIQUE : Sam Play %1,1 : End If 
      UPDSCORE[500,0]
      Pop Proc
   End If 
   If A=8
      A=Rnd(12)+1
      Goto DEBEFF
   End If 
   If A=10
      If ACTIVEMUSIQUE : Sam Play %1,12 : End If 
      If MORT1=False and MORT2=False
         UPDSCORE[1000,-1000]
      End If 
      Pop Proc
   End If 
   If A=11
      If ACTIVEMUSIQUE : Sam Play %1,12 : End If 
      If MORT1=False and MORT2=False
         UPDSCORE[500,-500]
      End If 
      Pop Proc
   End If 
End Proc
Procedure EFFET_BONUS2
   Shared CURBONUS,SCORE1,SCORE2,ACTIVEMUSIQUE
   A=CURBONUS-25
   DEBEFF:
   If A=1
      If ACTIVEMUSIQUE : Sam Play %10,1 : End If 
      UPDSCORE[0,200]
      Pop Proc
   End If 
   If A=2
      If ACTIVEMUSIQUE : Sam Play %10,1 : End If 
      UPDSCORE[0,300]
      Pop Proc
   End If 
   If A=3
      If ACTIVEMUSIQUE : Sam Play %10,1 : End If 
      UPDSCORE[0,100]
      Pop Proc
   End If 
   If A=4
      If ACTIVEMUSIQUE : Sam Play %10,1 : End If 
      UPDSCORE[0,500]
      Pop Proc
   End If 
   If A=10
      If ACTIVEMUSIQUE : Sam Play %10,12 : End If 
      If MORT1=False and MORT2=False
         UPDSCORE[-1000,1000]
      End If 
      Pop Proc
   End If 
   If A=11
      If ACTIVEMUSIQUE : Sam Play %10,12 : End If 
      If MORT1=False and MORT2=False
         UPDSCORE[-500,500]
      End If 
      Pop Proc
   End If 
End Proc
Procedure TRESORS
   Shared TRESOR(),C(),CARRY1,CARRY2,RAMENE1,RAMENE2
   N=1
   For X=1 To 14
      For Y=1 To 14
         If C(X,Y)=37
            Bob TRESOR(N),X*16,Y*16-16,37
            Inc N
            If N=4
               Exit 2
            End If 
         End If 
      Next Y
   Next X
   CARRY1=0
   CARRY2=0
   RAMENE1=0
   RAMENE2=0
End Proc
Procedure WINNER[N]
   Clear Key 
   Cls 0
   Colour 1,0
   Paste Icon 40,30,1
   Paste Icon 72,80,2
   Paste Icon 112,120,N+2
   Fade 1,0,$FFF
   Wait 15
   Repeat 
   Until Fire(1)=0 and Fire(0)=0
   Timer=0
   Repeat 
   Until Fire(1) or Fire(0) or Timer>=500 or Len(Inkey$)
   Fade 1
   Wait 15
   Sprite Off 
   Cls 0
End Proc
Procedure _GETREADY
   Shared LEVEL
   Palette 0,0
   Cls 0
   If LEVEL<10
      Paste Icon 68,60,17
      Paste Icon 220,62,17+LEVEL
   Else 
      Paste Icon 60,60,17
      Paste Icon 212,62,17+(LEVEL/10)
      Paste Icon 234,62,17+(LEVEL mod 10)
   End If 
   Paste Icon 30,110,16
   Wait 15
   Fade 1,0,$FFF
End Proc
Procedure _GAME_OVER
   Cls 0
   Palette 0,0
   Paste Icon 30,80,29
   Wait 15
   Fade 1,0,$FFF
   While Fire(1) or Fire(0)
   Wend 
   Repeat 
   Until Fire(1) or Fire(0)
End Proc
Procedure MUSIQUE[N,V]
   Shared MUSIQUEON,QUELLEMUSIQUE
   Volume 0
   If QUELLEMUSIQUE<>N
      Erase 3
      Erase 5
      If N=1
         Load "Intro.Abk",3
      Else 
         Load "Mf.Abk",3
         Load "Samples.Abk",5
      End If 
   End If 
   QUELLEMUSIQUE=N
   Led Off 
   Music 1
   For I=0 To V
      Volume I
      Wait Vbl 
   Next I
   MUSIQUEON=True
End Proc
Procedure MUSIQUE_OFF
   Shared MUSIQUEVOLUME,MUSIQUEON
   For I=MUSIQUEVOLUME To 0 Step -1
      Mvolume I
      Wait Vbl 
   Next I
   Music Off 
   Led On 
   MUSIQUEON=False
End Proc
Procedure REFOUS_PALETTE
   Palette ,,,0,,,,,,,,,,,,,$0,$FFF,$A,$1F,$19F,$8EF,$500,$900,$F00,$F91,$FF0,$777,$708,$60,$A0,$E0
End Proc
Procedure UPDSCORE[J1PLUS,J2PLUS]
   Shared SCORE1,SCORE2,MORT1,MORT2,PARALIZED1,PARALIZED2
   '
   Add SCORE1,J1PLUS
   Add SCORE2,J2PLUS
   If SCORE1<0 Then SCORE1=0
   If SCORE2<0 Then SCORE2=0
   '
   Ink 11,0
   If MORT1=False
      If PARALIZED1=False
         Text 8,242,"SCORE"+Space$(8-Len(Str$(SCORE1)))+Str$(SCORE1)
      Else 
         Text 8,242," Oh my head ! "
      End If 
   Else 
      Text 8,242,"    OH NO !   "
   End If 
   If MORT2=False
      If PARALIZED2=False
         Text 136,242,"SCORE"+Space$(8-Len(Str$(SCORE2)))+Str$(SCORE2)
      Else 
         Text 136,242," Oh my head ! "
      End If 
   Else 
      Text 136,242,"    OH NO !   "
   End If 
End Proc
Procedure PUB[N]
   S=Screen
   Screen Hide S
   Load Iff "Publogo.Iff",4
   Wait 60
   F$=("Pub"+Str$(N)+".Iff")-" "
   If Exist(F$)
      Load Iff F$,4
   Else 
      Load Iff "Pub1.iff",4
   End If 
   Repeat 
   Until Fire(1) or Fire(0)
   Fade 1
   Screen Close 4
   Screen S
   Screen Show S
   Wait 15
End Proc
Procedure HALL_OF_FAME
   Shared HISCORE(),HISCORE$()
   Flash Off 
   Palette 0,0,0,0,0
   Paper 0
   Pen 2
   Cls 0
   Paste Icon 65,20,5
   Cmove 0,10
   Pen 1
   Print "  RANG  POINTS   NOM"
   Cdown 
   For I=1 To 10
      H$=Str$(HISCORE(I))-" "
      Print "   ";Right$(Str$(I),2);"   ";String$("0",6-Len(H$));H$;"   ";HISCORE$(I)
   Next I
   Cmove ,7
   Centre "- PRESS FIRE -"
   Fade 1,0,$FFF
End Proc
Procedure OPTIONS
   Shared ACTIVEMUSIQUE,ACTIVEPUB,NAME1$,NAME2$
   While Fire(0) or Fire(1)
   Wend 
   Screen 0
   Screen To Front 0
   Palette 0,0,0
   Load "Pres-Samples.Abk",5
   OPTAFF
   Get Sprite Palette 
   Palette $0,$AAA,$555,$FFF,$808,$F4A,$E00,$620,$FDB,$B96,$FE0,$80,$D0,$CC,$6F,$A
   '
   Reserve Zone 6
   Set Zone 1,24,65 To 110,125
   Set Zone 2,120,65 To 206,125
   Set Zone 3,216,65 To 300,125
   Set Zone 4,24,150 To 110,210
   Set Zone 5,120,150 To 206,210
   Set Zone 6,216,150 To 300,210
   '
   Repeat 
      If Jleft(1) Then Add X,-2,0 To 319
      If Jright(1) Then Add X,2,0 To 319
      If Jup(1) Then Add Y,-2,0 To 255
      If Jdown(1) Then Add Y,2,0 To 255
      Sprite 8,X Hard(X),Y Hard(Y),31
      If Fire(1)<>0
         Z=Zone(X,Y)
         While Fire(1)
         Wend 
         If Z=1
            If ACTIVEPUB=True
               ACTIVEPUB=False
               Cls 0,24,65 To 110,125
               Paste Icon 24,65,7
               Sam Play %1111,6,14000
            Else 
               ACTIVEPUB=True
               Cls 0,24,65 To 110,125
               Paste Icon 24,65,6
               Sam Play %1111,7,8000
            End If 
         End If 
         If Z=2
            If ACTIVEMUSIQUE=True
               ACTIVEMUSIQUE=False
               Cls 0,120,65 To 206,125
               Paste Icon 120,65,9
               Sam Play %1111,1,8000
            Else 
               ACTIVEMUSIQUE=True
               Cls 0,120,65 To 206,125
               Paste Icon 120,65,8
               Sam Play %1111,2,8000
            End If 
         End If 
         If Z=3
            Fade 1
            Sam Play %1111,8,8000
            Wait 15
            Sprite Off 
            SCR0LLING
            Sam Play %1111,1,8000
            OPTAFF
            Get Sprite Palette 
            Palette $0,$AAA,$555,$FFF,$808,$F4A,$E00,$620,$FDB,$B96,$FE0,$80,$D0,$CC,$6F,$A
         End If 
         If Z=4
            Sam Play %1111,4,10000
            Screen Open 5,320,32,2,Lowres
            Screen Display 5,128,260,320,32
            Palette $0,$FFF
            Print "  Please enter Player-one's new name...   "
            Clear Key : Put Key NAME1$
            Input "  ";NAME1$
            Screen Close 5
            Sam Play %1111,1,8000
         End If 
         If Z=6
            Sam Play %1111,5,10000
            Screen Open 5,320,32,2,Lowres
            Screen Display 5,128,260,320,32
            Palette $0,$FFF
            Print "  Please enter Player-two's new name...   "
            Clear Key : Put Key NAME2$
            Input "  ";NAME2$
            Screen Close 5
            Sam Play %1111,1,8000
         End If 
         If Z=5
            Sam Play %1111,3,15000
            Paste Icon 120,150,14
            While Fire(1)
            Wend 
            Paste Icon 120,150,13
            Wait 5
            _ONYVA=True
         End If 
      End If 
      Wait Vbl 
   Until _ONYVA
   '
   Fade 1
   Wait 15
   Reserve Zone 
End Proc
Procedure OPTAFF
   Shared ACTIVEMUSIQUE,ACTIVEPUB
   Paper 0
   Pen 3
   Curs Off : Cls 
   Paste Icon 54,2,15
   If ACTIVEPUB
      Paste Icon 24,65,6
   Else 
      Paste Icon 24,65,7
   End If 
   If ACTIVEMUSIQUE
      Paste Icon 120,65,8
   Else 
      Paste Icon 120,65,9
   End If 
   Paste Icon 216,65,12
   Paste Icon 24,150,10
   Paste Icon 120,150,13
   Paste Icon 216,150,11
End Proc
Procedure SCR0LLING
   Shared V$
   Dim X(46),Y(46),X2(46),Y2(46),SIZE(46)
   SC$=Space$(15)
   SC$=SC$+"Salut . . . voici TWIN Baston version demo "+V$+" . . .    "
   SC$=SC$+"   cree par powermob :   conception par ronan amicel , "
   SC$=SC$+"avec la collaboration des autres membres - "
   SC$=SC$+"programmation par Ronan Amicel - Graphiques par David "
   SC$=SC$+"thevenot - musique par eric hilpipre                  "
   SC$=SC$+"           "
   SC$=Upper$(SC$)
   A$="L: Move 0,200,100; Move 0,-200,100; Jump L;"
   Load "Lettres.Abk",6
   Unpack 6 To 2
   Erase 6
   Screen Hide 2
   Screen Open 3,368,25,8,Lowres
   Curs Off 
   Flash Off 
   Cls 0
   Get Palette 2
   Hide On 
   Double Buffer 
   Autoback 0
   Screen Display 3,128,50,320,
   Channel 1 To Screen Display 3
   Amal 1,A$
   SCRL$="ABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789(!):-., "
   Restore SCR0LLY_DATA
   For F=1 To 44
      Read TEMP$,X(F),Y(F),X2(F),Y2(F),SIZE(F)
   Next F
   ' find letter
   SP=3
   TEMP=Varptr(SC$)
   TEMP2=Len(SC$)
   Wait Vbl 
   View 
   Amal On 1
   For LOP=1 To TEMP2-1
      CNUM=Instr(SCRL$,Chr$(Peek(TEMP+LOP)))
      Screen Copy 2,X(CNUM),Y(CNUM),X2(CNUM),Y2(CNUM)+1 To 3,320,0
      For LOP1=1 To SIZE(CNUM) Step SP
         Screen Swap 
         Wait Vbl 
         Screen Copy Physic(3),0,0,368,40 To Logic(3),-SP,0
      Next LOP1
      Exit If Fire(1)
   Next LOP
   Wait 10
   Shift Off 
   Wait Vbl 
   Amal Off 1
   Fade 1
   Wait 50
   Y=77
   X=320
   Screen Close 2
   Screen Close 3
   SCR0LLY_DATA:
   Data "A",1,1,31,25,32
   Data "B",34,1,64,25,32
   Data "C",67,1,97,25,32
   Data "D",100,1,130,25,32
   Data "E",133,1,163,25,32
   Data "F",166,1,196,25,32
   Data "G",199,1,229,25,32
   Data "H",232,1,262,25,32
   Data "I",265,1,276,25,12
   Data "J",1,27,31,51,32
   Data "K",34,27,64,51,32
   Data "L",67,27,97,51,32
   Data "M",100,27,130,51,32
   Data "N",133,27,163,51,32
   Data "O",166,27,196,51,32
   Data "P",199,27,229,51,32
   Data "Q",232,27,262,51,32
   Data "R",265,27,295,51,32
   Data "S",1,53,31,77,32
   Data "T",34,53,64,77,32
   Data "U",67,53,97,77,32
   Data "V",100,53,130,77,32
   Data "W",133,53,163,77,32
   Data "X",166,53,196,77,32
   Data "Y",199,53,229,77,32
   Data "Z",232,53,262,77,32
   Data "0",1,79,29,103,32
   Data "1",32,79,46,103,32
   Data "2",49,79,77,103,32
   Data "3",80,79,108,103,32
   Data "4",111,79,139,103,32
   Data "5",142,79,171,103,32
   Data "6",174,79,202,103,32
   Data "7",205,79,233,103,32
   Data "8",236,79,264,103,32
   Data "9",267,79,295,103,32
   Data "(",1,105,16,129,14
   Data "!",19,105,29,129,12
   Data ")",32,105,47,129,14
   Data ":",50,105,60,129,12
   Data "-",63,105,93,129,32
   Data ".",96,105,106,129,12
   Data ",",109,105,119,129,12
   Data " ",1,170,20,199,19
End Proc
Procedure _COPYRIGHT
   Load "Copyright.Abk",10
   YDISPLAY=75
   Auto View Off 
   Unpack 10 To 7
   For Y=2 To Screen Height/2 Step 8
      Screen Display 7,,YDISPLAY+Screen Height/2-Y,,Y*2
      Screen Offset 7,,Screen Height/2-Y
      View 
      Wait Vbl 
   Next 
   Wait 200
   For Y=Screen Height/2 To 0 Step -8
      Screen Display 7,,YDISPLAY+Screen Height/2-Y,,Y*2
      Screen Offset 7,,Screen Height/2-Y
      View 
      Wait Vbl 
   Next 
   Screen Close 7
   Auto View On 
   Erase 10
End Proc
