'
'                      ------------------- 
'                     |    Twin Baston    |
'                      ------------------- 
'
'
'               * *   ©1992  PowerMob Prod.   * *
'                *                             * 
'               * *       Ronan Amicel        * *
'                *       David Thévenot        * 
'               * *     et Eric Hilpipre      * *
'
'
'************************************************************
'
'  NOM           :  TWIN BASTON
'  VERSION       :  0.87 
'  AUTEUR        :  RONAN AMICEL alias MR REED 
'  DERNIERE MOD. :  23.07.92 
'  DESCRIPTION   :  Jeu de Plates-formes à 2 Joueurs 
'
'************************************************************
'
'
'
Dim C(14,14)
Dim HISCORE(10)
Dim HISCORE$(10)
Dim MN(5),MX(5),MY(5),MV(5)
Global C()
V$="0.87"
Dim ENNEMI(5),TRESOR(3)
Dim DANSLESPOMMES(5)
'
JOUEUR1=1
JOUEUR2=2
TIR1=3
TIR2=4
ENNEMI(1)=5
ENNEMI(2)=6
ENNEMI(3)=7
ENNEMI(4)=8
ENNEMI(5)=9
TRESOR(1)=10
TRESOR(2)=11
TRESOR(3)=12
BONUS=13
'
Restore SCORES
For I=1 To 10
   Read HISCORE$(I),HISCORE(I)
Next I
SCORES:
Data "POWERMOB PRODUCTIONS",100000
Data "POWERMOB PRODUCTIONS",80000
Data "POWERMOB PRODUCTIONS",70000
Data "POWERMOB PRODUCTIONS",60000
Data "POWERMOB PRODUCTIONS",50000
Data "POWERMOB PRODUCTIONS",40000
Data "POWERMOB PRODUCTIONS",30000
Data "POWERMOB PRODUCTIONS",20000
Data "POWERMOB PRODUCTIONS",10000
Data "POWERMOB PRODUCTIONS",5000
'
MUSIQUEVOLUME=63
BRUITAGESVOLUME=63
ACTIVEMUSIQUE=True
ACTIVEPUB=True
NAME1$="Mr Reed - The Very Best"
NAME2$="Crappy opponent"
'
Proc INIT
Auto View Off 
Wait Vbl 
Screen Open 0,320,256,32,Lowres
Screen Display 0,128,40,320,256
Curs Off 
Flash Off 
Hide On 
Cls 0
Double Buffer 
Auto View On 
Wait Vbl 
Make Mask 
'
'************************************* 
'
Do 
   Autoback 2
   Proc PRESENTATION
   Proc OPTIONS
   '
   Load "Twin:Img-Back.Abk",9
   CREDITS=2
   LEVEL=1
   SCORE1=0
   SCORE2=0
   For I=1 To 5
      DANSLESPOMMES(I)=0
   Next I
   '
   If ACTIVEMUSIQUE
      Proc MUSIQUE[2,0]
   End If 
   Proc _GETREADY
   Proc CHARGER[LEVEL]
   Timer=0
   Repeat 
   Until Fire(1) or Fire(0) or Timer>=400
   Fade 1
   Wait 15
   Bob Off 
   Cls 0
   '
   '****************************************
   '
   Repeat 
      '
      PASSE=False
      MORT1=False
      MORT2=False
      RAMENE1=0
      RAMENE2=0
      CARRY1=0
      CARRY2=0
      PARALIZED1=False
      PARALIZED2=False
      Proc INIT_TABLEAU
      Wait Vbl 
      For I=1 To NBMONSTRES+4
         Amal On I
      Next I
      Synchro Off 
      Timer=0
      '
      Repeat 
         '- - - - - JOUEUR 1 - - - - - -
         If MORT1=False
            If PARALIZED1=False
               Y1=Y Bob(JOUEUR1)
               X1=X Bob(JOUEUR1)
               If Y1>=208
                  Amreg(1,0)=1
                  Amreg(1,1)=1
               Else 
                  X=X1/16
                  Y=Y1/16
                  Add Y,2
                  Amreg(1,0)=C(X,Y)
                  If X<14
                     X=(X1+13)/16
                     Amreg(1,1)=C(X,Y)
                  Else 
                     Amreg(1,1)=0
                  End If 
               End If 
               X=X1/16
               Y=Y1/16
               If X=1
                  Amreg(1,2)=0
               Else 
                  Dec X
                  Inc Y
                  Amreg(1,2)=C(X,Y)
                  Inc X
               End If 
               If X<14
                  Inc X
                  Amreg(1,3)=C(X,Y)
               End If 
               '
               If Bob Col(JOUEUR1,BONUS To BONUS)
                  Proc EFFET_BONUS1
                  Proc BONUS
               End If 
               '
               If Bob Col(JOUEUR1,TRESOR(1) To TRESOR(3))
                  If CARRY1=0
                     If Col(TRESOR(1))
                        CARRY1=1
                     End If 
                     If Col(TRESOR(2))
                        CARRY1=2
                     End If 
                     If Col(TRESOR(3))
                        CARRY1=3
                     End If 
                     Bob Off TRESOR(CARRY1)
                     Proc UPDSCORE[100,0]
                     If ACTIVEMUSIQUE
                        Sam Play %1,2
                     End If 
                  End If 
               End If 
               '
               If X1=16 and Y1=223
                  If CARRY1>0
                     CARRY1=0
                     Inc RAMENE1
                     Proc UPDSCORE[2000,0]
                     If RAMENE1+RAMENE2=3
                        PASSE=True
                     End If 
                     If ACTIVEMUSIQUE
                        Sam Play %1,10
                     End If 
                  End If 
                  If Jdown(1)
                     Inc TEMPS1
                     If TEMPS1>189
                        TEMPS1=189
                     End If 
                     Ink 20
                     TP=206-TEMPS1
                     Draw 1,TP+1 To 14,TP+1
                     Draw 1,TP To 14,TP
                  End If 
               End If 
            Else 
               Inc PARALIZED1
               If PARALIZED1=200
                  PARALIZED1=0
                  Amal Freeze 10
                  Bob JOUEUR1,,,PARIMG1
                  Amal On 1
                  UPDSCORE[0,0]
               End If 
            End If 
            '  à exécuter toujours 
            If Bob Col(TIR1,ENNEMI(1) To ENNEMI(NBMONSTRES))
               For I=ENNEMI(1) To ENNEMI(5)
                  Exit If Col(I)
               Next I
               If DANSLESPOMMES(I-4)=False
                  DANSLESPOMMES(I-4)=1
                  Amal Freeze I
                  Bob TIR1,,320,
                  If ACTIVEMUSIQUE
                     Shoot 
                  End If 
                  Proc UPDSCORE[10,0]
               End If 
            End If 
            If Bob Col(TIR1,JOUEUR2 To JOUEUR2) and PARALIZED2=False
               PARALIZED2=1
               PARIMG2=I Bob(JOUEUR2)
               Amal Freeze 2
               Amal On 11
               Bob TIR1,,320,
               If ACTIVEMUSIQUE
                  Sam Play %10,1,12000
               End If 
               Proc UPDSCORE[50,0]
            End If 
            If Bob Col(JOUEUR1,ENNEMI(1) To ENNEMI(NBMONSTRES))
               MORT1=True
               If ACTIVEMUSIQUE
                  Boom 
               End If 
               Amal 1,"Let A=59; P; P; P; P; P; Let Y=300; P; End"
               Amal On 1
               Bob Off TIR1
               If CARRY1<>0
                  Bob TRESOR(CARRY1),X1,Y1,14
               End If 
               UPDSCORE[0,0]
            End If 
            ' si à 0 
            If TEMPS1=0
               MORT1=True
               If ACTIVEMUSIQUE
                  Sam Play %1,4
               End If 
               Amal 1,"Let A=59; P; P; P; P; P; Let Y=300; P; End"
               Amal On 1
               Bob Off TIR1
               If CARRY1<>0
                  Bob TRESOR(CARRY1),X1,Y1,14
               End If 
               UPDSCORE[0,0]
            End If 
         End If 
         ' - - - - - - JOUEUR 2 - - - - - - - - - 
         If MORT2=False
            If PARALIZED2=False
               Y2=Y Bob(JOUEUR2)
               X2=X Bob(JOUEUR2)
               If Y2>=208
                  Amreg(2,0)=1
                  Amreg(2,1)=1
               Else 
                  X=X2/16
                  Y=Y2/16
                  Add Y,2
                  Amreg(2,0)=C(X,Y)
                  If X<14
                     X=(X2+13)/16
                     Amreg(2,1)=C(X,Y)
                  Else 
                     Amreg(2,1)=0
                  End If 
               End If 
               X=X2/16
               Y=Y2/16
               If X=1
                  Amreg(2,2)=0
               Else 
                  Dec X
                  Inc Y
                  Amreg(2,2)=C(X,Y)
                  Inc X
               End If 
               If X<14
                  Inc X
                  Amreg(2,3)=C(X,Y)
               End If 
               '
               If Bob Col(JOUEUR2,BONUS To BONUS)
                  Proc EFFET_BONUS2
                  Proc BONUS
               End If 
               If Bob Col(JOUEUR2,TRESOR(1) To TRESOR(3))
                  If CARRY2=0
                     If Col(TRESOR(1))
                        CARRY2=1
                     End If 
                     If Col(TRESOR(2))
                        CARRY2=2
                     End If 
                     If Col(TRESOR(3))
                        CARRY2=3
                     End If 
                     Bob Off TRESOR(CARRY2)
                     Proc UPDSCORE[0,100]
                     If ACTIVEMUSIQUE
                        Sam Play %10,2
                     End If 
                  End If 
               End If 
               If(X2=224) and(Y2=223)
                  If CARRY2>0
                     CARRY2=0
                     Inc RAMENE2
                     If RAMENE1+RAMENE2=3
                        PASSE=True
                     End If 
                     Proc UPDSCORE[0,2000]
                     If ACTIVEMUSIQUE
                        Sam Play %10,10
                     End If 
                  End If 
                  If Jdown(0)
                     Inc TEMPS2
                     If TEMPS2>189
                        TEMPS2=189
                     End If 
                     Ink 20
                     TP=206-TEMPS2
                     Draw 241,TP+1 To 254,TP+1
                     Draw 241,TP To 254,TP
                  End If 
               End If 
            Else 
               Inc PARALIZED2
               If PARALIZED2=200
                  PARALIZED2=0
                  Amal Freeze 11
                  Bob JOUEUR2,,,PARIMG2
                  Amal On 2
                  UPDSCORE[0,0]
               End If 
            End If 
            If Bob Col(TIR2,ENNEMI(1) To ENNEMI(NBMONSTRES))
               For I=ENNEMI(1) To ENNEMI(NBMONSTRES)
                  Exit If Col(I)
               Next I
               If DANSLESPOMMES(I-4)=False
                  DANSLESPOMMES(I-4)=1
                  Amal Freeze I
                  Bob TIR2,,320,
                  If ACTIVEMUSIQUE
                     Shoot 
                  End If 
                  Proc UPDSCORE[0,10]
               End If 
            End If 
            If Bob Col(TIR2,JOUEUR1 To JOUEUR1) and(PARALIZED1=False)
               PARALIZED1=1
               Amal Freeze 1
               PARIMG1=I Bob(JOUEUR1)
               Amal On 10
               Bob TIR2,,310,
               If ACTIVEMUSIQUE
                  Sam Play %1,1,6000
               End If 
               Proc UPDSCORE[0,50]
            End If 
            If Bob Col(JOUEUR2,ENNEMI(1) To ENNEMI(NBMONSTRES))
               MORT2=True
               If ACTIVEMUSIQUE
                  Boom 
               End If 
               Amal 2,"Let A=59; P; P; P; P; P; Let Y=300; P; End"
               Amal On 2
               Bob Off TIR2
               If CARRY2>0
                  Bob TRESOR(CARRY2),X2,Y2,14
               End If 
               UPDSCORE[0,0]
            End If 
            ' gestion du temps 
            If TEMPS2<1
               MORT2=True
               If ACTIVEMUSIQUE
                  Sam Play %10,4
               End If 
               Amal 2,"Let A=59; P; P; P; P; P; Let Y=300; P; End"
               Amal On 2
               Bob Off TIR2
               If CARRY2<>0
                  Bob TRESOR(CARRY2),X2,Y2,14
               End If 
               UPDSCORE[0,0]
            End If 
         End If 
         ' - - - - - - - AUTRES GESTIONS - - - - - - - - - - - -
         If Timer>12
            Ink 0
            If MORT1=False
               Dec TEMPS1
               Draw 1,205-TEMPS1 To 14,205-TEMPS1
               Draw 1,206-TEMPS1 To 14,206-TEMPS1
            End If 
            If MORT2=False
               Dec TEMPS2
               Draw 241,205-TEMPS2 To 254,205-TEMPS2
               Draw 241,206-TEMPS2 To 254,206-TEMPS2
            End If 
            Timer=0
         End If 
         For I=ENNEMI(1) To ENNEMI(NBMONSTRES)
            If DANSLESPOMMES(I-4)=200
               DANSLESPOMMES(I-4)=0
               Amal On I
            End If 
            If DANSLESPOMMES(I-4)<>0
               Inc DANSLESPOMMES(I-4)
            End If 
         Next I
         '
         K$=Inkey$
         If K$<>""
            K$=Upper$(K$)
            If Asc(K$)=27
               If Key Shift<>0
                  Erase 2
                  Music Off 
                  Erase 3
                  Erase 5
                  Erase 9
                  Amal Off 
                  Bob Off 
                  Erase 1
                  End 
               Else 
                  MORT1=True
                  MORT2=True
                  CREDITS=1
               End If 
            End If 
            If K$="P"
               Every Off 
               Amal Freeze 
               Repeat 
               Until Upper$(Inkey$)="P"
               For I=1 To 4
                  Amal On I
               Next I
               For I=5 To NBMONSTRES+4
                  If DANSLESPOMMES(I-4)=0
                     Amal On I
                  End If 
               Next I
               Every On 
            End If 
            If K$="M"
               Print At(3,1);"Fast:";Fast Free;" / Chip:";Chip Free;"  "
               Print "R0 =";Amreg(1,0);"  /  R1 =";Amreg(1,1)
            End If 
            If K$="+"
               PASSE=True
            End If 
         End If 
         '
         Synchro 
         Synchro 
         Wait Vbl 
         '
      Until((MORT1=True) and(MORT2=True)) or(PASSE=True)
      '
      '**********************************************
      '
      Every Off 
      Wait 6
      Amal Off 
      MUSIQUE_OFF
      Wait 4
      Fade 2
      Wait 30
      Wait Vbl 
      Bob Off 
      Wait Vbl 
      If PASSE
         If ACTIVEMUSIQUE
            Sam Play %1111,5
         End If 
         If RAMENE1>=RAMENE2
            Proc WINNER[1]
         Else 
            Proc WINNER[2]
         End If 
         If ACTIVEPUB
            Proc PUB[LEVEL]
         End If 
         Inc LEVEL
         _GETREADY
         CHARGER[LEVEL]
         Timer=0
         Repeat 
         Until Fire(1) or Fire(0) or Timer>=400
         Fade 1
         Wait 15
         Cls 0
      Else 
         Dec CREDITS
         If CREDITS>0
            _OH_NO
         End If 
      End If 
   Until CREDITS=0
   ' - - - - - - - - - - - - - - -
   FINBOUCLE:
   Proc _GAME_OVER
   For I=1 To 10
      If SCORE1>HISCORE(I)
         HISCORE(I)=SCORE1
         HISCORE$(I)=NAME1$
         Exit 
      End If 
   Next I
   For I=1 To 10
      If SCORE2>HISCORE(I)
         HISCORE(I)=SCORE2
         HISCORE$(I)=NAME2$
      End If 
   Next I
   Cls 0
   Erase 3
   Erase 5
   HALL_OF_FAME
   Fade 1,0,$FFF
   Wait 15
   Repeat 
   Until Fire(1)=0 and Fire(0)=0
   Repeat 
   Until Fire(1) or Fire(0)
   Fade 1
Loop 
'
'***************** PROCEDURES **************** 
'
'
Procedure INIT
   ' Banques:  1=spr. 2=icons  3=music  5=spl  8=fond  9-10=imgs à charger  
   Shared MUSIQUEON,QUELLEMUSIQUE
   Close Editor 
   Close Workbench 
   While Screen>=0
      Screen Close Screen
   Wend 
   For I=1 To 16
      Erase I
   Next I
   Colour Back 0
   Auto View Off 
   Screen Open 2,320,16,2,Lowres
   Screen Hide 2
   Hide On 
   Proc _COPYRIGHT
   Auto View On 
   Load "Twin:Zik-Intro.Abk",3
   Load "Twin:Img-Intro.Abk",9
   Load "Twin:Samples-First.Abk",5
   Sam Play 1
   Wait 166
   Sam Play 2
   Wait 166
   Sam Play 3
   Wait 114
   Sam Play 4
   Unpack 9 To 0
   Wait Vbl 
   Erase 9
   Wait 80
   Erase 5
   Voice 0
   Mvolume 63
   Voice 15
   Music 1
   MUSIQUEON=True
   QUELLEMUSIQUE=1
   Load "Twin:Sprites.Abk"
   Load "Twin:Icons.Abk",2
End Proc
Procedure PRESENTATION
   Shared MUSIQUEVOLUME,MUSIQUEON,V$
   Screen 0
   Screen To Front 0
   Cls 0
   Load "Twin:Img-Intro2.abk",9
   Load "Twin:Img-Intro3.abk",10
   If MUSIQUEON=False
      Proc MUSIQUE[1,MUSIQUEVOLUME]
   End If 
   Wait Vbl 
   Screen Show 0
   HALL=False
   Do 
      ' Twin Baston
      Auto View Off 
      Wait Vbl 
      Unpack 9 To 2
      Screen 2
      Screen To Front 2
      Screen Display 2,128,40,,
      Palette 0,0
      Auto View On 
      Wait Vbl 
      Fade 1,$0,$FFF
      For I=1 To 250
         If Fire(1) or Fire(0)
            Exit 2
         End If 
         Wait Vbl 
      Next I
      Fade 1
      Wait 15
      ' PowerMob 
      Auto View Off 
      Wait Vbl 
      Unpack 10 To 2
      Palette 0,0
      Auto View On 
      Wait Vbl 
      Fade 1,$0,$FFF
      Screen Display 2,128,40,,
      For I=1 To 250
         If Fire(1) or Fire(0)
            Exit 2
         End If 
         Wait Vbl 
      Next I
      Fade 1
      Wait 15
      ' Scores 
      Screen 0
      Screen To Front 0
      If HALL=False
         Proc HALL_OF_FAME
         HALL=True
      End If 
      Fade 1,0,$FFF
      For I=1 To 250
         If Fire(1) or Fire(0)
            Exit 2
         End If 
         Wait Vbl 
      Next I
      Fade 1
      Wait 15
   Loop 
   '
   Repeat 
   Until Fire(1)=0 and Fire(0)=0
   Fade 1
   Wait 5
   Screen Hide 0
   Screen Hide 2
   Screen Close 2
   Screen 0
   Screen To Front 0
   Flash Off 
   Cls 0
   Screen Show 0
   Proc MUSIQUE_OFF
   Erase 9
   Erase 10
   Erase 3
End Proc
Procedure INIT_TABLEAU
   Shared LEVEL,TEMPS1,TEMPS2,ACTIVEMUSIQUE,MUSIQUEVOLUME,V$,MORT1,MORT2
   Shared ENNEMI(),NBMONSTRES,JOUEUR1,JOUEUR2,TIR1,TIR2,TRESOR()
   Shared MX(),MY(),MN(),MV()
   '
   Auto View Off 
   Screen Open 0,320,256,32,Lowres
   Screen Hide 0
   Curs Off 
   Flash Off 
   Hide On 
   Wait Vbl 
   Restore _TEMPS
   For I=1 To LEVEL
      Read TEMPS1,TEMPS2
   Next I
   _TEMPS:
   Data 190,190,190,190,190,190,190,190,190,190,190,190,190,190,190,190,190,190,190,190
   Data 190,190,190,190,190,190,190,190,190,190,190,190,190,190,190,190,190,190,190,190
   '
   '
   Cls 0
   Paper 0
   Pen 1
   Unpack 9 To 1
   Screen 0
   Get Palette 1
   Wait Vbl 
   Screen Copy 1 To 0
   Wait Vbl 
   Screen Close 1
   Screen Display 0,128,40,320,256
   Proc REFOUS_PALETTE
   Ink 20
   Paint 1,16
   Paint 241,16
   Proc UPDSCORE[0,0]
   '
   For CX=1 To 14
      For CY=1 To 14
         If C(CX,CY)=1
            Paste Bob CX*16,CY*16-16,15
         End If 
      Next CY
   Next CX
   Double Buffer 
   Autoback 1
   '
   '
   Bob JOUEUR1,16,223,2
   Bob TIR1,10,320,18
   Bob JOUEUR2,224,223,9
   Bob TIR2,30,320,17
   Limit Bob 16,0 To 240,224
   '
   ' Programmes AMAL
   '
   ' Joueur 1 
   '
   A$="      r zero case en dessous gauche ;"
   A$=A$+"   r un case en dessous droite ;"
   A$=A$+"   r deux case a gauche ;"
   A$=A$+"   r trois case a droite ;"
   A$=A$+"   r quatre empjump ;"
   A$=A$+"   r a  emp tirer"
   A$=A$+"   Let R0=1 ; Let RA=0;"
   A$=A$+"AUtotest("
   A$=A$+"   If R4=0 then Jump Gauche ;"
   A$=A$+"   If J1&1<>0 then Jump Gauche ;"
   A$=A$+"   Let R4=0 ;"
   A$=A$+"Gauche:"
   A$=A$+"   If J1&4=0 then Jump Droite ;"
   A$=A$+"   If X=16 then Jump Droite ;"
   A$=A$+"   Let R5=X/16*16=X ;"
   A$=A$+"   If R2=1&R5 then Jump Droite ;"
   A$=A$+"   Let X=X-1 ; Let A=3 ;"
   A$=A$+"Droite:"
   A$=A$+"   If J1&8=0 then Jump Fire ;"
   A$=A$+"   If R3=1 then Jump Fire ;"
   A$=A$+"   If X=224 then Jump Fire ;"
   A$=A$+"   Let X=X+1 ; Let A=2 ;"
   A$=A$+"Fire:"
   A$=A$+"   If J1&16=0 then eXit;"
   A$=A$+"   If RA=1 then eXit;"
   A$=A$+"   Let RA=1; Let RB=1;"
   A$=A$+"   Let RC=X; Let RD=Y;"
   A$=A$+"   Let RE=A;"
   A$=A$+"   eXit; )"
   A$=A$+"Sauter:"
   A$=A$+"   If R4=1 then Jump Tomber ;"
   A$=A$+"   If J1&1=0 then Jump Tomber ;"
   A$=A$+"   If Y<30 then Jump Tomber ;"
   A$=A$+"   Let R4=1 ;"
   A$=A$+"   Move 0,-32,16 ; Pause; Pause;"
   A$=A$+"Tomber:"
   A$=A$+"   If R0=1 then Jump Sauter ;"
   A$=A$+"   If R1=1 then Jump Sauter ;"
   A$=A$+"   Move 0,16,8 ;"
   A$=A$+"   Jump Tomber ;"
   '
   ' Tir 1
   '
   C$="   Loop:"
   C$=C$+"   If RB=0 then Jump Loop;"
   C$=C$+"   Let RB=0;"
   C$=C$+"   Let X=RC; Let Y=RD;"
   C$=C$+"   If RE=2 then Jump M;"
   C$=C$+"   Let A=17;"
   C$=C$+"   Move -48,0,12;"
   C$=C$+"   Jump N;"
   C$=C$+"M: Let A=18"
   C$=C$+"   Move 48,0,12;"
   C$=C$+"N: Let Y=310;"
   C$=C$+"   Let RA=0"
   C$=C$+"   Jump Loop;"
   '
   ' Joueur 2 
   '
   B$="      r zero case en dessous gauche ;"
   B$=B$+"   r un case en dessous droite ;"
   B$=B$+"   r deux case a gauche ;"
   B$=B$+"   r trois case a droite ;"
   B$=B$+"   r quatre et r cinq empjump ;"
   B$=B$+"   Let R0=1 ;"
   B$=B$+"AUtotest("
   B$=B$+"   If R4=0 then Jump Gauche ;"
   B$=B$+"   If J0&1<>0 then Jump Gauche ;"
   B$=B$+"   Let R4=0 ;"
   B$=B$+"Gauche:"
   B$=B$+"   If J0&4=0 then Jump Droite ;"
   B$=B$+"   If X=16 then Jump Droite ;"
   B$=B$+"   Let R5=X/16*16=X ;"
   B$=B$+"   If R2=1&R5 then Jump Droite ;"
   B$=B$+"   Let X=X-1 ; Let A=9 ;"
   B$=B$+"Droite:"
   B$=B$+"   If J0&8=0 then Jump Fire ;"
   B$=B$+"   If R3=1 then Jump Fire ;"
   B$=B$+"   If X=224 then Jump Fire ;"
   B$=B$+"   Let X=X+1 ; Let A=8 ;"
   B$=B$+"Fire:"
   B$=B$+"   If J0&16=0 then eXit;"
   B$=B$+"   If RA=1 then eXit;"
   B$=B$+"   Let RF=1; Let RG=1;"
   B$=B$+"   Let RH=X; Let RI=Y;"
   B$=B$+"   Let RJ=A;"
   B$=B$+"   eXit; )"
   B$=B$+"Sauter:"
   B$=B$+"   If R4=1 then Jump Tomber ;"
   B$=B$+"   If J0&1=0 then Jump Tomber ;"
   B$=B$+"   If Y<30 then Jump Tomber ;"
   B$=B$+"   Let R4=1 ;"
   B$=B$+"   Move 0,-32,16 ; Pause; Pause;"
   B$=B$+"Tomber:"
   B$=B$+"   If R0=1 then Jump Sauter ;"
   B$=B$+"   If R1=1 then Jump Sauter ;"
   B$=B$+"   Move 0,16,8 ;"
   B$=B$+"   Jump Tomber;"
   '
   ' Tir 2
   '
   D$="   Loop:"
   D$=D$+"   If RG=0 then Jump Loop;"
   D$=D$+"   Let RG=0;"
   D$=D$+"   Let X=RH; Let Y=RI;"
   D$=D$+"   If RJ=8 then Jump M;"
   D$=D$+"   Let A=17;"
   D$=D$+"   Move -48,0,12;"
   D$=D$+"   Jump N;"
   D$=D$+"M: Let A=18;"
   D$=D$+"   Move 48,0,12;"
   D$=D$+"N: Let Y=320;"
   D$=D$+"   Let RF=0"
   D$=D$+"   Jump Loop;"
   '
   Channel 1 To Bob JOUEUR1
   Channel 2 To Bob JOUEUR2
   Channel 3 To Bob TIR1
   Channel 4 To Bob TIR2
   Amal 1,A$
   Amal 2,B$
   Amal 3,C$
   Amal 4,D$
   '
   Channel 10 To Bob JOUEUR1
   Channel 11 To Bob JOUEUR2
   Amal 10,"Anim 0,(5,8)(6,8)(7,8);"
   Amal 11,"Anim 0,(11,8)(12,8)(13,8);"
   '
   For I=1 To 5
      Exit If MN(I)=0
      Bob ENNEMI(I),MX(I)*16,MY(I)*16-1,MN(I)
      Channel ENNEMI(I) To Bob ENNEMI(I)
      A$="L: Move "+Str$(MV(I)*16)+",0,"+Str$(MV(I)*16)+";"
      A$=A$+"Let A="+Str$(MN(I)+1)+";"
      A$=A$+"Move -"+Str$(MV(I)*16)+",0,"+Str$(MV(I)*16)+";"
      A$=A$+"Let A="+Str$(MN(I))+";"
      A$=A$+"Jump L;"
      Amal ENNEMI(I),A$
   Next I
   NBMONSTRES=I
   '
   Timer=0
   Every 500 Proc BONUS
   Clear Key 
   '
   N=1
   For X=1 To 14
      For Y=1 To 14
         If C(X,Y)=2
            Bob TRESOR(N),X*16,Y*16-1,14
            Exit If N=3,2
            Inc N
         End If 
      Next Y
   Next X
   '
   Auto View On 
   Screen Show 0
   '
   If ACTIVEMUSIQUE
      MUSIQUE[2,MUSIQUEVOLUME]
   End If 
   '
   Wait Vbl 
   '
End Proc
Procedure CHARGER[N]
   Shared C(),MN(),MX(),MY(),MV()
   Open Random 1,"Twin:NewLevels.Def"
   Field 1,1 As CASE$
   INDEX=(N-1)*196+1
   For X=1 To 14
      For Y=1 To 14
         Get 1,INDEX
         C(X,Y)=Val(CASE$)
         Inc INDEX
      Next Y
   Next X
   Close 1
   Open Random 1,"Twin:NewLevels.Mst"
   Field 1,2 As NUM$,2 As XD$,2 As YD$,2 As MV$
   INDEX=(N-1)*5
   For I=1 To 5
      Inc INDEX
      Get 1,INDEX
      MN(I)=Val(NUM$)
      MX(I)=Val(XD$)
      MY(I)=Val(YD$)
      MV(I)=Val(MV$)
   Next I
   Close 1
End Proc
Procedure BONUS
   Shared BONUS,LEVEL,CURBONUS
   Every Off 
   Do 
      X=Rnd(13)+1
      Y=Rnd(13)+1
      If C(X,Y)=0
         If Y=14
            Exit 
         Else 
            If C(X,Y+1)=1
               Exit 
            End If 
         End If 
      End If 
   Loop 
   CURBONUS=Rnd(15)+37
   Bob BONUS,X*16,Y*16-1,CURBONUS
   Every On 
End Proc
Procedure EFFET_BONUS1
   Shared CURBONUS,ACTIVEMUSIQUE,CREDITS,JOUEUR1,JOUEUR2
   Shared PARALIZED1,PARALIZED2,PARIMG1,PARIMG2,TEMPS1
   A=CURBONUS
   If A=43
      Repeat 
         A=Rnd(15)+37
      Until A<>43
   End If 
   If A=37
      If ACTIVEMUSIQUE
         Sam Play %1,3
      End If 
      UPDSCORE[500,0]
      Pop Proc
   End If 
   If A=38
      If ACTIVEMUSIQUE
         Sam Play %1,3
      End If 
      UPDSCORE[250,0]
      Pop Proc
   End If 
   If A=39
      If ACTIVEMUSIQUE
         Sam Play %1,3
      End If 
      UPDSCORE[-100,0]
      Pop Proc
   End If 
   If A=40
      If ACTIVEMUSIQUE
         Sam Play %1,3
      End If 
      Inc CREDITS
      Pop Proc
   End If 
   If A=41
      If ACTIVEMUSIQUE
         Sam Play %1,3
      End If 
      UPDSCORE[1000,0]
      Pop Proc
   End If 
   If A=42
      If ACTIVEMUSIQUE
         Sam Play %1,3
      End If 
      UPDSCORE[500,0]
      Pop Proc
   End If 
   If A=44
      If ACTIVEMUSIQUE
         Sam Play %1,3
      End If 
      UPDSCORE[100,0]
      Pop Proc
   End If 
   If A=45
      If ACTIVEMUSIQUE
         Sam Play %1,3
      End If 
      UPDSCORE[5000,0]
      Pop Proc
   End If 
   If A=46
      If ACTIVEMUSIQUE
         Sam Play %1,3
      End If 
      UPDSCORE[250,0]
      Pop Proc
   End If 
   If A=47
      If ACTIVEMUSIQUE
         Sam Play %1,3
      End If 
      UPDSCORE[500,0]
      Pop Proc
   End If 
   If A=48
      If ACTIVEMUSIQUE
         Sam Play %1,1
      End If 
      PARALIZED1=1
      Amal Freeze 1
      PARIMG1=I Bob(JOUEUR1)
      Amal On 10
      UPDSCORE[0,0]
      Pop Proc
   End If 
   If A=49
      If ACTIVEMUSIQUE
         Sam Play %1,4
      End If 
      TEMPS1=189
      Ink 20
      Bar 1,18 To 14,205
      Pop Proc
   End If 
   If A=50
      If ACTIVEMUSIQUE
         Sam Play %1,3
      End If 
      UPDSCORE[2500,0]
      Pop Proc
   End If 
   If A=51
      If ACTIVEMUSIQUE
         Sam Play %1,6
      End If 
      PARALIZED2=1
      Amal Freeze 2
      PARIMG2=I Bob(JOUEUR2)
      Amal On 11
      UPDSCORE[0,0]
      Pop Proc
   End If 
   If A=52
      If ACTIVEMUSIQUE
         Sam Play %1,3
      End If 
      Swap SCORE1,SCORE2
      UPDSCORE[0,0]
      Pop Proc
   End If 
End Proc
Procedure EFFET_BONUS2
   Shared CURBONUS,ACTIVEMUSIQUE,CREDITS,JOUEUR1,JOUEUR2
   Shared PARALIZED1,PARALIZED2,PARIMG1,PARIMG2,TEMPS2
   A=CURBONUS
   If A=43
      Repeat 
         A=Rnd(15)+37
      Until A<>43
   End If 
   If A=37
      If ACTIVEMUSIQUE
         Sam Play %1,2
      End If 
      UPDSCORE[0,500]
      Pop Proc
   End If 
   If A=38
      If ACTIVEMUSIQUE
         Sam Play %1,1
      End If 
      UPDSCORE[0,250]
      Pop Proc
   End If 
   If A=39
      If ACTIVEMUSIQUE
         Sam Play %1,3
      End If 
      UPDSCORE[0,-100]
      Pop Proc
   End If 
   If A=40
      If ACTIVEMUSIQUE
         Sam Play %1,10
      End If 
      Inc CREDITS
      Pop Proc
   End If 
   If A=41
      If ACTIVEMUSIQUE
         Sam Play %1,2
      End If 
      UPDSCORE[0,1000]
      Pop Proc
   End If 
   If A=42
      If ACTIVEMUSIQUE
         Sam Play %1,1
      End If 
      UPDSCORE[0,500]
      Pop Proc
   End If 
   If A=44
      If ACTIVEMUSIQUE
         Sam Play %1,1
      End If 
      UPDSCORE[0,100]
      Pop Proc
   End If 
   If A=45
      If ACTIVEMUSIQUE
         Sam Play %1,2
      End If 
      UPDSCORE[0,5000]
      Pop Proc
   End If 
   If A=46
      If ACTIVEMUSIQUE
         Sam Play %1,1
      End If 
      UPDSCORE[0,250]
      Pop Proc
   End If 
   If A=47
      If ACTIVEMUSIQUE
         Sam Play %1,1
      End If 
      UPDSCORE[0,500]
      Pop Proc
   End If 
   If A=48
      If ACTIVEMUSIQUE
         Sam Play %1,8
      End If 
      PARALIZED2=1
      Amal Freeze 2
      PARIMG2=I Bob(JOUEUR2)
      Amal On 11
      UPDSCORE[0,0]
      Pop Proc
   End If 
   If A=49
      TEMPS2=189
      Ink 20
      Bar 241,18 To 254,205
      Pop Proc
   End If 
   If A=50
      If ACTIVEMUSIQUE
         Sam Play %1,2
      End If 
      UPDSCORE[0,2500]
      Pop Proc
   End If 
   If A=51
      If ACTIVEMUSIQUE
         Sam Play %1,3
      End If 
      PARALIZED1=1
      Amal Freeze 1
      PARIMG1=I Bob(JOUEUR1)
      Amal On 10
      UPDSCORE[0,0]
      Pop Proc
   End If 
   If A=52
      If ACTIVEMUSIQUE
         Sam Play %1,12
      End If 
      Swap SCORE1,SCORE2
      Pop Proc
   End If 
End Proc
Procedure WINNER[N]
   Clear Key 
   Cls 0
   Colour 1,0
   Paste Icon 40,30,1
   Paste Icon 72,80,2
   Paste Icon 112,120,N+2
   Fade 1,0,$FFF
   Wait 15
   While Fire(1) or Fire(0)
   Wend 
   Timer=0
   Repeat 
   Until Fire(1) or Fire(0) or Timer>=500 or Len(Inkey$)
   Fade 1
   Wait 15
   Bob Off 
   Cls 0
End Proc
Procedure _GETREADY
   Shared LEVEL
   Palette 0,0
   Cls 0
   If LEVEL<10
      Paste Icon 68,60,17
      Paste Icon 220,62,17+LEVEL
   Else 
      Paste Icon 60,60,17
      Paste Icon 212,62,17+(LEVEL/10)
      Paste Icon 234,62,17+(LEVEL mod 10)
   End If 
   Paste Icon 30,110,16
   Wait 15
   Fade 1,0,$FFF
End Proc
Procedure _GAME_OVER
   Cls 0
   Palette 0,0
   Paste Icon 30,80,29
   Wait 15
   Fade 1,0,$FFF
   While Fire(1) or Fire(0)
   Wend 
   Repeat 
   Until Fire(1) or Fire(0)
End Proc
Procedure _OH_NO
   Cls 0
   Palette 0,0
   Paste Icon 80,80,28
   Wait 15
   Fade 1,0,$FFF
   While Fire(1) or Fire(0)
   Wend 
   Repeat 
   Until Fire(1) or Fire(0)
End Proc
Procedure MUSIQUE[N,V]
   Shared MUSIQUEON,QUELLEMUSIQUE
   Volume 0
   If QUELLEMUSIQUE<>N
      Erase 3
      Erase 5
      If N=1
         Load "Twin:Zik-Intro.Abk",3
      Else 
         Load "Twin:Zik-Jeu.Abk",3
         Load "Twin:Samples-Jeu.Abk",5
      End If 
   End If 
   QUELLEMUSIQUE=N
   Led Off 
   Music 1
   For I=0 To V
      Volume I
      Wait Vbl 
   Next I
   MUSIQUEON=True
End Proc
Procedure MUSIQUE_OFF
   Shared MUSIQUEVOLUME,MUSIQUEON
   For I=MUSIQUEVOLUME To 0 Step -1
      Mvolume I
      Wait Vbl 
   Next I
   Music Off 
   Led On 
   MUSIQUEON=False
End Proc
Procedure REFOUS_PALETTE
   Palette $0,$FFF,$A,$F,$19F,$8EF,$500,$900,$F00,$F91,$FF0,$777,$708,$60,$A0,$E0
End Proc
Procedure UPDSCORE[J1PLUS,J2PLUS]
   Shared SCORE1,SCORE2,MORT1,MORT2,PARALIZED1,PARALIZED2
   '
   Add SCORE1,J1PLUS
   Add SCORE2,J2PLUS
   If SCORE1<0
      SCORE1=0
   End If 
   If SCORE2<0
      SCORE2=0
   End If 
   '
   Ink 11,0
   If MORT1=False
      If PARALIZED1=False
         Text 8,242,"SCORE"+Space$(8-Len(Str$(SCORE1)))+Str$(SCORE1)
      Else 
         Text 8,242," Oh my head ! "
      End If 
   Else 
      Text 8,242,"    OH NO !   "
   End If 
   If MORT2=False
      If PARALIZED2=False
         Text 136,242,"SCORE"+Space$(8-Len(Str$(SCORE2)))+Str$(SCORE2)
      Else 
         Text 136,242," Oh my head ! "
      End If 
   Else 
      Text 136,242,"    OH NO !   "
   End If 
End Proc
Procedure PUB[N]
   S=Screen
   Screen Hide S
   Load "Twin:Img-Publogo.Abk",10
   Unpack 10 To 4
   Erase 10
   Wait 60
   F$=("Twin:Img-Pub"+Str$(N)+".Abk")-" "
   If Exist(F$)
      Load F$,10
   Else 
      Load "Twin:Img-Pub1.Abk",10
   End If 
   Unpack 10 To 4
   Erase 10
   Repeat 
   Until Fire(1) or Fire(0)
   Fade 1
   Screen Close 4
   Screen S
   Screen Show S
   Wait 15
End Proc
Procedure HALL_OF_FAME
   Shared HISCORE(),HISCORE$()
   Flash Off 
   Palette 0,0,0,0,0
   Cls 0
   Paste Icon 65,30,5
   Ink 1,0
   For I=1 To 10
      H$=Str$(HISCORE(I))-" "
      S$=Right$(Str$(I),2)
      If I=1
         S$=S$+"ST"
      Else 
         If I=2
            S$=S$+"ND"
         Else 
            If I=3
               S$=S$+"RD"
            Else 
               S$=S$+"TH"
            End If 
         End If 
      End If 
      S$=S$+"  "+String$("0",6-Len(H$))+H$+"  "+HISCORE$(I)
      Text 16,12*I+80,S$
   Next I
End Proc
Procedure OPTIONS
   Shared ACTIVEMUSIQUE,ACTIVEPUB,NAME1$,NAME2$
   While Fire(0) or Fire(1)
   Wend 
   Screen 0
   Screen To Front 0
   Palette 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
   Load "Twin:Samples-Pres.Abk",5
   OPTAFF
   Get Sprite Palette 
   Palette $0,$AAA,$555,$FFF,$808,$F4A,$E00,$620,$FDB,$B96,$FE0,$80,$D0,$CC,$6F,$A
   '
   Reserve Zone 6
   Set Zone 1,24,65 To 110,125
   Set Zone 2,120,65 To 206,125
   Set Zone 3,216,65 To 300,125
   Set Zone 4,24,150 To 110,210
   Set Zone 5,120,150 To 206,210
   Set Zone 6,216,150 To 300,210
   '
   X=32
   Y=32
   Repeat 
      If Jleft(1)
         Add X,-2,0 To 319
      End If 
      If Jright(1)
         Add X,2,0 To 319
      End If 
      If Jup(1)
         Add Y,-2,0 To 255
      End If 
      If Jdown(1)
         Add Y,2,0 To 255
      End If 
      Sprite 8,X Hard(X-8),Y Hard(Y+8),44
      If Fire(1)<>0
         Z=Zone(X,Y)
         If Z=5
            Sam Play %1111,3,15000
            Paste Icon 120,150,14
            While Fire(1)
            Wend 
            Paste Icon 120,150,13
            Wait 5
            _ONYVA=True
         End If 
         While Fire(1)
         Wend 
         If Z=1
            If ACTIVEPUB=True
               ACTIVEPUB=False
               Cls 0,24,65 To 110,125
               Paste Icon 24,65,7
               Sam Play %1111,6,14000
            Else 
               ACTIVEPUB=True
               Cls 0,24,65 To 110,125
               Paste Icon 24,65,6
               Sam Play %1111,7,8000
            End If 
         End If 
         If Z=2
            If ACTIVEMUSIQUE=True
               ACTIVEMUSIQUE=False
               Cls 0,120,65 To 206,125
               Paste Icon 120,65,9
               Sam Play %1111,1,8000
            Else 
               ACTIVEMUSIQUE=True
               Cls 0,120,65 To 206,125
               Paste Icon 120,65,8
               Sam Play %1111,2,8000
            End If 
         End If 
         If Z=3
            Fade 1
            Sam Play %1111,8,8000
            Wait 15
            Sprite Off 
            SCR0LLING
            Sam Play %1111,1,8000
            OPTAFF
            Get Sprite Palette 
            Palette $0,$AAA,$555,$FFF,$808,$F4A,$E00,$620,$FDB,$B96,$FE0,$80,$D0,$CC,$6F,$A
         End If 
         If Z=4
            Sam Play %1111,4,10000
            Screen Open 5,320,32,2,Lowres
            Screen Display 5,128,260,320,32
            Palette $0,$FFF
            Print "  Donnez le nom du Joueur n°1 , svp ..."
            Cdown 
            Clear Key 
            Put Key NAME1$
            Input "  ";NAME1$
            NAME1$=Left$(NAME1$,20)
            Screen Close 5
            Sam Play %1111,1,8000
         End If 
         If Z=6
            Sam Play %1111,5,10000
            Screen Open 5,320,32,2,Lowres
            Screen Display 5,128,260,320,32
            Palette $0,$FFF
            Print "  Donnez le nom du Joueur n°2 , svp ..."
            Cdown 
            Clear Key 
            Put Key NAME2$
            Input "  ";NAME2$
            NAME2$=Left$(NAME2$,20)
            Screen Close 5
            Sam Play %1111,1,8000
         End If 
      End If 
      Wait Vbl 
   Until _ONYVA
   '
   Fade 1
   Wait 15
   Sprite Off 
   Reserve Zone 
   Erase 5
End Proc
Procedure OPTAFF
   Shared ACTIVEMUSIQUE,ACTIVEPUB
   Paper 0
   Pen 3
   Curs Off 
   Cls 
   Paste Icon 54,2,15
   If ACTIVEPUB
      Paste Icon 24,65,6
   Else 
      Paste Icon 24,65,7
   End If 
   If ACTIVEMUSIQUE
      Paste Icon 120,65,8
   Else 
      Paste Icon 120,65,9
   End If 
   Paste Icon 216,65,12
   Paste Icon 24,150,10
   Paste Icon 120,150,13
   Paste Icon 216,150,11
End Proc
Procedure SCR0LLING
   Shared V$
   Dim X(46),Y(46),X2(46),Y2(46),SIZE(46)
   SC$=Space$(15)
   SC$=SC$+"SALUT A TOUS  . . . VOICI TWIN BASTON VERSION DEMO "
   SC$=SC$+V$+"  . . . EUH . . TOUT A FAIT THIERRY . . . .    "
   SC$=SC$+"CREE PAR POWERMOB : CONCEPTION PAR RONAN AMICEL, "
   SC$=SC$+"AVEC LA COLLABORATION DES AUTRES MEMBRES  -  "
   SC$=SC$+"PROGRAMMATION PAR RONAN AMICEL ALIAS MR REED  -  "
   SC$=SC$+"GRAPHIQUES PAR DAVID THEVENOT  -  "
   SC$=SC$+"MUSIQUE PAR ERIC HILPIPRE ALIAS VUZZ  -  EH EH VOICI LA "
   SC$=SC$+"FIN DE CE SCROLLTEXT . . .  ALORS A TRES BIENTOT POUR "
   SC$=SC$+"-TWIN BASTON 2 THE REVENGE-  ET DE NOMBREUSES AUTRES "
   SC$=SC$+"LOGICIELS POWERMOB. . . . . .  PRESS FIRE . . ."
   SC$=Upper$(SC$)+Space$(16)
   A$="L: Move 0,100,100; Move 0,-100,100; Jump L;"
   Load "Twin:Img-Fonte.Abk",6
   Unpack 6 To 2
   Erase 6
   Screen Hide 2
   Screen Open 3,368,25,8,Lowres
   Curs Off 
   Flash Off 
   Cls 0
   Get Palette 2
   Hide On 
   Double Buffer 
   Autoback 0
   Screen Display 3,128,150,320,
   Channel 1 To Screen Display 3
   Amal 1,A$
   SCRL$="ABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789(!):-., "
   Restore SCR0LLY_DATA
   For F=1 To 44
      Read TEMP$,X(F),Y(F),X2(F),Y2(F),SIZE(F)
   Next F
   ' find letter
   SP=3
   TEMP=Varptr(SC$)
   TEMP2=Len(SC$)
   Wait Vbl 
   View 
   Amal On 1
   For LOP=1 To TEMP2-1
      CNUM=Instr(SCRL$,Chr$(Peek(TEMP+LOP)))
      Screen Copy 2,X(CNUM),Y(CNUM),X2(CNUM),Y2(CNUM)+1 To 3,320,0
      For LOP1=1 To SIZE(CNUM) Step SP
         Screen Swap 
         Wait Vbl 
         Screen Copy Physic(3),0,0,368,40 To Logic(3),-SP,0
      Next LOP1
      Exit If Fire(1)
   Next LOP
   Wait 10
   Shift Off 
   Wait Vbl 
   Amal Off 1
   Fade 1
   Wait 50
   Y=77
   X=320
   Screen Close 2
   Screen Close 3
   SCR0LLY_DATA:
   Data "A",1,1,31,25,32
   Data "B",34,1,64,25,32
   Data "C",67,1,97,25,32
   Data "D",100,1,130,25,32
   Data "E",133,1,163,25,32
   Data "F",166,1,196,25,32
   Data "G",199,1,229,25,32
   Data "H",232,1,262,25,32
   Data "I",265,1,276,25,12
   Data "J",1,27,31,51,32
   Data "K",34,27,64,51,32
   Data "L",67,27,97,51,32
   Data "M",100,27,130,51,32
   Data "N",133,27,163,51,32
   Data "O",166,27,196,51,32
   Data "P",199,27,229,51,32
   Data "Q",232,27,262,51,32
   Data "R",265,27,295,51,32
   Data "S",1,53,31,77,32
   Data "T",34,53,64,77,32
   Data "U",67,53,97,77,32
   Data "V",100,53,130,77,32
   Data "W",133,53,163,77,32
   Data "X",166,53,196,77,32
   Data "Y",199,53,229,77,32
   Data "Z",232,53,262,77,32
   Data "0",1,79,29,103,32
   Data "1",32,79,46,103,32
   Data "2",49,79,77,103,32
   Data "3",80,79,108,103,32
   Data "4",111,79,139,103,32
   Data "5",142,79,171,103,32
   Data "6",174,79,202,103,32
   Data "7",205,79,233,103,32
   Data "8",236,79,264,103,32
   Data "9",267,79,295,103,32
   Data "(",1,105,16,129,14
   Data "!",19,105,29,129,12
   Data ")",32,105,47,129,14
   Data ":",50,105,60,129,12
   Data "-",63,105,93,129,32
   Data ".",96,105,106,129,12
   Data ",",109,105,119,129,12
   Data " ",1,170,20,199,19
End Proc
Procedure _COPYRIGHT
   Load "Twin:Img-Copyright.Abk",10
   YDISPLAY=75
   Unpack 10 To 7
   For Y=2 To Screen Height/2 Step 8
      Screen Display 7,,YDISPLAY+Screen Height/2-Y,,Y*2
      Screen Offset 7,,Screen Height/2-Y
      View 
      Wait Vbl 
   Next 
   T=Timer
   Repeat 
      Wait Vbl 
   Until Len(Inkey$) or Fire(1) or Fire(0) or Timer-T>200
   For Y=Screen Height/2 To 0 Step -8
      Screen Display 7,,YDISPLAY+Screen Height/2-Y,,Y*2
      Screen Offset 7,,Screen Height/2-Y
      View 
      Wait Vbl 
   Next 
   Screen Close 7
   Erase 10
End Proc
