'
'                      ------------------- 
'                     |    Twin Baston    |
'                      ------------------- 
'
'                  ©1992  PowerMob Productions 
'
'
'                           CREDITS
'                          ~~~~~~~~~ 
'           Coding by :  Ronan Amicel ( Mr Reed )
'            Music by :  Eric Hilpipre ( Vuzz )
'         Graphics by :  David Thévenot ( Eagle )
'
'
'********************** Le Programme ************************
'
'  NOM           :  TWIN BASTON
'  VERSION       :  0.96 
'  AUTEUR        :  RONAN AMICEL alias MR REED 
'  DERNIERE MOD. :  27.09.92 
'  DESCRIPTION   :  Jeu de Plates-formes à 2 Joueurs.
'
'************************************************************
'
' Description:  Cette version n'utilise plus AMAL pour les 
' ~~~~~~~~~~~~  joueurs ( slmt ennemis, anims, et tirs ) 
' Il n'y a plus qu'une Pro Effet-Bonus, Options modifiées. 
' Les préférences et les scores sont sur disque maintenant.
' Affichage sur l'écran de jeu des crédits, du level et du 
' nombre de victoires de chacun. 
' Samples désopilants. 
' Intégration des nouvelles anims de David.
' Meilleure gestion des collisions joueur/briques. 
' Joueurs continuent à tomber quand assommés.
' Nouvelle méthode d'affichage des messages ( game-over, etc... )
' Jeux inter-tableaux ( Tron & Orangina )
' Trésors ne restent plus en suspension ( tombent ) .
' Nouvelles routines de chargement de tableaux, à cause de 
' la nouvelle version de l'éditeur ( V5.1 ) .
' Nouveaux-nouveaux sprites intégrés.
' Cà marche bien avec les nouvelles routines ( tests briques 
' par rapport à 0 et plus à 1 )
'
'************************************************************
'
' A Faire:   - D'autres petits jeux entre les tableaux.
' ~~~~~~~~   - Cryptage des données des tableaux.
'
'************************************************************
'
'
' D'abord des inits, variables, constantes, etc..
'
Set Buffer 16
'
Dim C(14,14),C2(14,14)
Global C(),C2()
Dim HISCORE(10),HISCORE$(10)
Dim SQ$(20),NBSQ(5)
Dim MX(5),MY(5),MA(5,10),MH(5,10),MV(5,10),MT(5,10)
Dim ENNEMI(5),DANSLESPOMMES(5)
Dim TRESOR(3),TRESOR_TOMBE(3)
Dim VIT(2),INV(2)
'
Restore DT
For I=1 To 20
   Read SQ$(I)
Next I
'
V$="0.96"
'
JOUEUR1=1
JOUEUR2=2
TIR1=3
TIR2=4
ENNEMI(1)=5
ENNEMI(2)=6
ENNEMI(3)=7
ENNEMI(4)=8
ENNEMI(5)=9
TRESOR(1)=10
TRESOR(2)=11
TRESOR(3)=12
BONUS=13
'
STANDING=0
WALKING=1
JUMPING=2
FALLING=3
GAUCHE=-1
DROITE=1
'
MSG_OH_NO=1
MSG_GAME_OVER=2
MSG_GET_READY=3
MSG_WINNER=4
MSG_EGALITE=5
MSG_BONUS=6
MSG_OPTIONS=7
MSG_SCORES=8
'
' Puis on lance le chargement/l'intro
'
Proc INIT
'
' On remet un zoli écran comme y faut... 
'
Auto View Off 
Wait Vbl 
Screen Open 0,320,256,32,Lowres
Screen Display 0,128,40,320,256
Curs Off 
Flash Off 
Hide On 
Cls 0
Double Buffer 
Auto View On 
Wait Vbl 
Make Mask 
'
'**************************************
'
'         Boucle principale
'
'**************************************
'
Do 
   '
   ' L'intro en boucle, puis le choix des options 
   '
   Autoback 2
   Proc PRESENTATION
   Proc OPTIONS
   '
   ' Début de la partie, initialisations
   '
   Load "Twin:Img-Back.Abk",9
   CREDITS=3
   LEVEL=1
   SCORE1=0
   SCORE2=0
   VICT1=0
   VICT2=0
   For I=1 To 5
      DANSLESPOMMES(I)=0
   Next I
   '
   ' Chargement de la bande sonore
   '
   If ZIKON
      Proc MUSIQUE[2,0]
   End If 
   QUELLEZIK=2
   Load "Twin:Samples-Jeu.Abk",5
   '
   ' Go! On y va !
   '
   Proc _MESSAGE[MSG_GET_READY,0]
   Proc CHARGER[LEVEL]
   Timer=0
   Repeat 
      Multi Wait 
   Until Fire(1) or Fire(0) or Timer>=400
   Fade 1
   Wait 15
   Bob Off 
   Cls 0
   '
   '****************************************
   '
   '    Boucle de jeu (tant que crédits)
   '
   '****************************************
   '
   Repeat 
      '
      ' Initialise pour le tableau 
      '
      PASSE=False
      '
      MORT1=False
      PARALIZED1=False
      STATE1=STANDING
      SENS1=DROITE
      LONGTEMPS1=0
      RAMENE1=0
      CARRY1=0
      EMPJP1=0
      GLOUGLOU1=0
      POUM1=0
      '
      MORT2=False
      PARALIZED2=False
      STATE2=STANDING
      SENS2=GAUCHE
      LONGTEMPS2=0
      RAMENE2=0
      CARRY2=0
      EMPJP2=0
      GLOUGLOU2=0
      POUM2=0
      '
      HURRY=False
      Proc INIT_TABLEAU
      If SFXON
         VOIX1=%1
         VOIX2=%10
      Else 
         VOIX1=%0
         VOIX2=%0
      End If 
      '
      BONUSWAIT=0
      '
      Wait Vbl 
      For I=1 To NBMONSTRES+4
         Amal On I
      Next I
      '
      Synchro Off 
      Timer=0
      TEMPSTOTAL=0
      '
      '************************************
      '
      '          The Big Boucle
      '
      '************************************
      Repeat 
         '
         '- - - - - JOUEUR 1 - - - - - -
         '
         If MORT1=False
            '
            ' Où es-tu ? 
            '
            X1=X Bob(1)
            Y1=Y Bob(1)
            X=X1/16
            Y=Y1/16
            '
            If PARALIZED1>0
               '
               ' Si on est paralizé 
               '
               Inc PARALIZED1
               If PARALIZED1=200
                  PARALIZED1=0
                  Amal Freeze 10
                  J1_ANIM
                  UPDSCORE[0,0]
               End If 
               '
               ' Version allégée des routines KO=False
               '
               If STATE1=JUMPING
                  Inc JUMP1
                  If JUMP1<17
                     Add Y1,-VIT(1)
                  Else 
                     If Y1<10
                        STATE1=FALLING
                        FALL1=0
                        LONGTEMPS1=1
                     Else 
                        If(C(X,Y+1)>0) or(C((X1+15)/16,Y+1)>0)
                           STATE1=FALLING
                           FALL1=0
                           LONGTEMPS1=1
                        Else 
                           If(C(X,Y+2)>0) or(C((X1+15)/16,Y+2)>0)
                              STATE1=STANDING
                           Else 
                              STATE1=FALLING
                              FALL1=0
                              LONGTEMPS1=1
                           End If 
                        End If 
                     End If 
                  End If 
               Else 
                  If STATE1=FALLING
                     Inc FALL1
                     If FALL1<9
                        Add Y1,VIT(1)
                     Else 
                        If Y1=223
                           STATE1=STANDING
                           LONGTEMPS1=0
                        Else 
                           If(C(X,Y+2)>0) or(C((X1+15)/16,Y+2)>0)
                              STATE1=STANDING
                              LONGTEMPS1=0
                           Else 
                              FALL1=0
                              Inc LONGTEMPS1
                           End If 
                        End If 
                     End If 
                  End If 
               End If 
               '
            Else 
               '
               ' Que fais-tu ?
               '
               If(STATE1=STANDING) or(STATE1=WALKING)
                  '
                  ' Test Collision : On Tombe ?
                  '
                  If Y1<223
                     If((C(X,Y+2)=0) and(C((X1+15)/16,Y+2)=0)) or(Y1<10)
                        STATE1=FALLING
                        FALL1=0
                        LONGTEMPS1=1
                        J1_ANIM
                     End If 
                  End If 
                  If STATE1<>FALLING
                     '
                     '
                     ' Cà bouge pas...
                     '
                     If(Joy(1)=0) or(Joy(1)=2)
                        If STATE1<>STANDING
                           STATE1=STANDING
                           J1_ANIM
                        End If 
                        EMPJP1=0
                     Else 
                        '
                        ' On saute 
                        '
                        If Jup(1)
                           If EMPJP1=0
                              STATE1=JUMPING
                              JUMP1=0
                              J1_ANIM
                              EMPJP1=1
                           End If 
                        Else 
                           '
                           EMPJP1=0
                           '
                           ' Gauche 
                           '
                           If Jleft(1)
                              If(STATE1=STANDING) or(SENS1=DROITE)
                                 STATE1=WALKING
                                 SENS1=GAUCHE
                                 J1_ANIM
                              End If 
                              If Y1>=0
                                 If X1>16
                                    If X1 mod 16=0
                                       If C(X-1,Y+1)=0
                                          Add X1,-VIT(1)
                                       End If 
                                    Else 
                                       Add X1,-VIT(1)
                                    End If 
                                 Else 
                                    X1=16
                                 End If 
                              End If 
                           Else 
                              '
                              ' Droite 
                              '
                              If Jright(1)
                                 If(STATE1=STANDING) or(SENS1=GAUCHE)
                                    STATE1=WALKING
                                    SENS1=DROITE
                                    J1_ANIM
                                 End If 
                                 If Y1>=0
                                    If X1<224
                                       If C(X+1,Y+1)=0
                                          Add X1,VIT(1)
                                       End If 
                                    Else 
                                       X1=224
                                    End If 
                                 End If 
                              End If 
                           End If 
                        End If 
                     End If 
                  End If 
               End If 
               '
               If STATE1=JUMPING
                  '
                  Inc JUMP1
                  '
                  ' Au cours du saut.
                  '
                  If JUMP1<17
                     Add Y1,-VIT(1)
                     '
                     ' Dep. gauche
                     '
                     If Jleft(1)
                        If SENS1=DROITE
                           SENS1=GAUCHE
                           J1_ANIM
                        End If 
                        If Y1>=0
                           If X1>16
                              If X1 mod 16=0
                                 If C(X-1,(Y1/16)+1)=0
                                    Add X1,-VIT(1)
                                 End If 
                              Else 
                                 Add X1,-VIT(1)
                              End If 
                           Else 
                              X1=16
                           End If 
                        End If 
                     Else 
                        '
                        ' Dep droite 
                        '
                        If Jright(1)
                           If SENS1=GAUCHE
                              SENS1=DROITE
                              J1_ANIM
                           End If 
                           If Y1>=0
                              If X1<224
                                 If C(X+1,(Y1/16)+1)=0
                                    Add X1,VIT(1)
                                 End If 
                              Else 
                                 X1=224
                              End If 
                           End If 
                        End If 
                     End If 
                  Else 
                     '
                     ' En haut du saut, doit-on retomber ?
                     '
                     If Y1<10
                        STATE1=FALLING
                        FALL1=0
                        LONGTEMPS1=1
                        J1_ANIM
                     Else 
                        If(C(X,Y+1)>0) or(C((X1+15)/16,Y+1)>0)
                           STATE1=FALLING
                           FALL1=0
                           LONGTEMPS1=1
                           J1_ANIM
                        Else 
                           If(C(X,Y+2)>0) or(C((X1+15)/16,Y+2)>0)
                              STATE1=STANDING
                              J1_ANIM
                           Else 
                              STATE1=FALLING
                              FALL1=0
                              LONGTEMPS1=1
                              J1_ANIM
                           End If 
                        End If 
                     End If 
                  End If 
               Else 
                  '
                  If STATE1=FALLING
                     '
                     ' oooooooooooooooo No, I'm Fallin' 
                     '
                     Inc FALL1
                     '
                     ' Au cours de la descente
                     '
                     If FALL1<9
                        If FALL1>1
                           '
                           ' Dep gauche 
                           '
                           If Jleft(1)
                              If SENS1=DROITE
                                 SENS1=GAUCHE
                                 J1_ANIM
                              End If 
                              If Y1>=0
                                 If X1>16
                                    If X1 mod 16=0
                                       If C(X-1,Y+1)=0
                                          Add X1,-VIT(1)
                                       End If 
                                    Else 
                                       Add X1,-VIT(1)
                                    End If 
                                 End If 
                              End If 
                           Else 
                              '
                              ' Droite 
                              '
                              If Jright(1)
                                 If SENS1=GAUCHE
                                    SENS1=DROITE
                                    J1_ANIM
                                 End If 
                                 If Y1>=0
                                    If X1<224
                                       If C(X+1,Y+1)=0
                                          Add X1,VIT(1)
                                       End If 
                                    End If 
                                 End If 
                              End If 
                           End If 
                        End If 
                        '
                        Add Y1,VIT(1)
                        '
                     Else 
                        '
                        ' Redescendu d'une case: continuer à tomber ?
                        '
                        If Y1=223
                           STATE1=STANDING
                           LONGTEMPS1=0
                           J1_ANIM
                        Else 
                           If(C(X,Y+2)>0) or(C((X1+15)/16,Y+2)>0)
                              STATE1=STANDING
                              LONGTEMPS1=0
                              J1_ANIM
                           Else 
                              FALL1=0
                              Inc LONGTEMPS1
                              J1_ANIM
                           End If 
                        End If 
                     End If 
                  End If 
               End If 
               '
               ' Poum ! 
               '
               If Fire(1) and(GLOUGLOU1=False)
                  If Amreg(0)=0
                     Amreg(0)=1
                     Amreg(2)=X1
                     Amreg(3)=Y1
                     Amreg(4)=SENS1
                     If(STATE1<>JUMPING) and(STATE1<>FALLING)
                        If SENS1=DROITE
                           Amal 1,"Let A=11;"
                        Else 
                           Amal 1,"Let A=30;"
                        End If 
                        Amal On 1
                        POUM1=1
                     End If 
                  End If 
               Else 
                  If POUM1=1
                     POUM1=0
                     J1_ANIM
                  End If 
               End If 
               '
               '++++++++++++++++++++++++++++++++++++
               '
               '
               ' On prend un Bonus ?
               '
               If Bob Col(JOUEUR1,BONUS To BONUS)
                  Proc EFFET_BONUS[1]
                  Bob BONUS,,300,
                  BONUSWAIT=150
               End If 
               '
               ' Un trésor ?
               '
               If Bob Col(JOUEUR1,TRESOR(1) To TRESOR(NBTRESORS))
                  If CARRY1=0
                     For I=1 To NBTRESORS
                        If Col(TRESOR(I))
                           CARRY1=I
                        End If 
                     Next I
                     TRESOR_TOMBE(CARRY1)=0
                     Bob Off TRESOR(CARRY1)
                     Proc UPDSCORE[1,100]
                     Sam Play VOIX1,1
                  End If 
               End If 
               '
               ' On est en bas à gauche ? 
               '
               If X1=16 and Y1=223
                  '
                  ' Ramène un trésor 
                  '
                  If CARRY1>0
                     CARRY1=0
                     Inc RAMENE1
                     Proc UPDSCORE[1,2000]
                     If RAMENE1+RAMENE2=NBTRESORS
                        PASSE=True
                     End If 
                     Sam Play VOIX1*8,2
                  End If 
                  '
                  ' Remonter le temps
                  '
                  If Joy(1)=2
                     If GLOUGLOU1=0
                        Limit Bob 1,0,0 To 256,224
                        Sam Play VOIX1,4,8000
                        GLOUGLOU1=1
                        Amal 1,"Anim 0,(31,2)(32,2)(33,2)(32,2);"
                        Amal On 1
                        SENS1=GAUCHE
                     End If 
                     If HURRY=False
                        Inc TEMPS1
                        TEMPS1=Min(TEMPS1,MXTEMPS1)
                        Ink 4
                        TP=208-TEMPS1
                        Draw 1,TP+1 To 14,TP+1
                        Draw 1,TP To 14,TP
                        If MEMETEMPS
                           Draw 241,TP+1 To 254,TP+1
                           Draw 241,TP To 254,TP
                        End If 
                     End If 
                  Else 
                     If GLOUGLOU1=1
                        J1_ANIM
                     End If 
                  End If 
               End If 
            End If 
            '
            ' --- Ceci doit toujours être exécuté ---
            '
            '
            ' On tire sur un monstre 
            '
            If Bob Col(TIR1,ENNEMI(1) To ENNEMI(NBMONSTRES))
               For I=ENNEMI(1) To ENNEMI(5)
                  Exit If Col(I)
               Next I
               If DANSLESPOMMES(I-4)=False
                  DANSLESPOMMES(I-4)=1
                  Amal Freeze I
                  Bob TIR1,,320,
                  If SFXON
                     Shoot 
                  End If 
                  Proc UPDSCORE[1,10]
               End If 
            End If 
            '
            ' On tire sur l'autre !
            '
            If Bob Col(TIR1,JOUEUR2 To JOUEUR2) and PARALIZED2=False
               PARALIZED2=1
               Amal Freeze 2
               Amal On 11
               Bob TIR1,,320,
               Sam Play VOIX2,5
               Proc UPDSCORE[1,50]
            End If 
            '
            ' Collision avec monstre, aïe !
            '
            If Bob Col(JOUEUR1,ENNEMI(1) To ENNEMI(NBMONSTRES))
               MORT1=True
               Sam Play VOIX1,5
               J1_ANIM
               Bob Off TIR1
               If CARRY1<>0
                  Bob TRESOR(CARRY1),X1-X1 mod 16,Y1,72
                  TRESOR_TOMBE(CARRY1)=True
               End If 
               UPDSCORE[0,0]
            End If 
            '
            ' Plus de temps !
            '
            If TEMPS1=0
               MORT1=True
               Sam Play VOIX1,5
               J1_ANIM
               Bob Off TIR1
               If CARRY1<>0
                  Bob TRESOR(CARRY1),X1-X1 mod 16,Y1,72
                  TRESOR_TOMBE(CARRY1)=True
               End If 
               UPDSCORE[0,0]
            End If 
         End If 
         '
         '- - - - - JOUEUR 2 - - - - - -
         '
         If MORT2=False
            '
            ' Où es-tu ? 
            '
            X2=X Bob(2)
            Y2=Y Bob(2)
            X=X2/16
            Y=Y2/16
            '
            If PARALIZED2>0
               '
               ' Si on est paralizé 
               '
               Inc PARALIZED2
               If PARALIZED2=200
                  PARALIZED2=0
                  Amal Freeze 11
                  J2_ANIM
                  UPDSCORE[0,0]
               End If 
               '
               ' Version allégée des routines KO=False
               '
               If STATE2=JUMPING
                  Inc JUMP2
                  If JUMP2<17
                     Add Y2,-VIT(2)
                  Else 
                     If Y2<10
                        STATE2=FALLING
                        FALL2=0
                        LONGTEMPS2=1
                     Else 
                        If(C(X,Y+1)>0) or(C((X2+15)/16,Y+1)>0)
                           STATE2=FALLING
                           FALL2=0
                           LONGTEMPS2=1
                        Else 
                           If(C(X,Y+2)>0) or(C((X2+15)/16,Y+2)>0)
                              STATE2=STANDING
                           Else 
                              STATE2=FALLING
                              FALL2=0
                              LONGTEMPS2=1
                           End If 
                        End If 
                     End If 
                  End If 
               Else 
                  If STATE2=FALLING
                     Inc FALL2
                     If FALL2<9
                        Add Y2,VIT(2)
                     Else 
                        If Y2=223
                           STATE2=STANDING
                           LONGTEMPS2=0
                        Else 
                           If(C(X,Y+2)>0) or(C((X2+15)/16,Y+2)>0)
                              STATE2=STANDING
                              LONGTEMPS2=0
                           Else 
                              FALL2=0
                              Inc LONGTEMPS2
                           End If 
                        End If 
                     End If 
                  End If 
               End If 
               '
            Else 
               '
               ' Que fais-tu ?
               '
               If(STATE2=STANDING) or(STATE2=WALKING)
                  '
                  ' Test Collision : On Tombe ?
                  '
                  If Y2<223
                     If((C(X,Y+2)=0) and(C((X2+15)/16,Y+2)=0)) or(Y2<10)
                        STATE2=FALLING
                        FALL2=0
                        LONGTEMPS2=1
                        J2_ANIM
                     End If 
                  End If 
                  If STATE2<>FALLING
                     '
                     '
                     ' Cà bouge pas...
                     '
                     If(Joy(0)=0) or(Joy(0)=2)
                        If STATE2<>STANDING
                           STATE2=STANDING
                           J2_ANIM
                        End If 
                        EMPJP2=0
                     Else 
                        '
                        ' On saute 
                        '
                        If Jup(0)
                           If EMPJP2=0
                              STATE2=JUMPING
                              JUMP2=0
                              J2_ANIM
                              EMPJP2=1
                           End If 
                        Else 
                           '
                           EMPJP2=0
                           '
                           ' Gauche 
                           '
                           If Jleft(0)
                              If(STATE2=STANDING) or(SENS2=DROITE)
                                 STATE2=WALKING
                                 SENS2=GAUCHE
                                 J2_ANIM
                              End If 
                              If Y2>=0
                                 If X2>16
                                    If X2 mod 16=0
                                       If C(X-1,Y+1)=0
                                          Add X2,-VIT(2)
                                       End If 
                                    Else 
                                       Add X2,-VIT(2)
                                    End If 
                                 Else 
                                    X2=16
                                 End If 
                              End If 
                           Else 
                              '
                              ' Droite 
                              '
                              If Jright(0)
                                 If(STATE2=STANDING) or(SENS2=GAUCHE)
                                    STATE2=WALKING
                                    SENS2=DROITE
                                    J2_ANIM
                                 End If 
                                 If Y2>=0
                                    If X2<224
                                       If C(X+1,Y+1)=0
                                          Add X2,VIT(2)
                                       End If 
                                    Else 
                                       X2=224
                                    End If 
                                 End If 
                              End If 
                           End If 
                        End If 
                     End If 
                  End If 
               End If 
               '
               If STATE2=JUMPING
                  '
                  Inc JUMP2
                  '
                  ' Au cours du saut.
                  '
                  If JUMP2<17
                     Add Y2,-VIT(2)
                     '
                     ' Dep. gauche
                     '
                     If Jleft(0)
                        If SENS2=DROITE
                           SENS2=GAUCHE
                           J2_ANIM
                        End If 
                        If Y2>=0
                           If X2>16
                              If X2 mod 16=0
                                 If C(X-1,(Y2/16)+1)=0
                                    Add X2,-VIT(2)
                                 End If 
                              Else 
                                 Add X2,-VIT(2)
                              End If 
                           Else 
                              X2=16
                           End If 
                        End If 
                     Else 
                        '
                        ' Dep droite 
                        '
                        If Jright(0)
                           If SENS2=GAUCHE
                              SENS2=DROITE
                              J2_ANIM
                           End If 
                           If Y2>=0
                              If X2<224
                                 If Y2>=0
                                    If C(X+1,(Y2/16)+1)=0
                                       Add X2,VIT(2)
                                    End If 
                                 End If 
                              Else 
                                 X2=224
                              End If 
                           End If 
                        End If 
                     End If 
                  Else 
                     '
                     ' En haut du saut, doit-on retomber ?
                     '
                     If Y2<10
                        STATE2=FALLING
                        FALL2=0
                        LONGTEMPS2=1
                        J2_ANIM
                     Else 
                        If(C(X,Y+1)>0) or(C((X2+15)/16,Y+1)>0)
                           STATE2=FALLING
                           FALL2=0
                           LONGTEMPS2=1
                           J2_ANIM
                        Else 
                           If(C(X,Y+2)>0) or(C((X2+15)/16,Y+2)>0)
                              STATE2=STANDING
                              J2_ANIM
                           Else 
                              STATE2=FALLING
                              FALL2=0
                              LONGTEMPS2=1
                              J2_ANIM
                           End If 
                        End If 
                     End If 
                  End If 
               Else 
                  '
                  If STATE2=FALLING
                     '
                     ' oooooooooooooooo No, I'm Fallin' 
                     '
                     Inc FALL2
                     '
                     ' Au cours de la descente
                     '
                     If FALL2<9
                        If FALL2>1
                           '
                           ' Dep gauche 
                           '
                           If Jleft(0)
                              If SENS2=DROITE
                                 SENS2=GAUCHE
                                 J2_ANIM
                              End If 
                              If Y2>=0
                                 If X2>16
                                    If X2 mod 16=0
                                       If C(X-1,Y+1)=0
                                          Add X2,-VIT(2)
                                       End If 
                                    Else 
                                       Add X2,-VIT(2)
                                    End If 
                                 End If 
                              End If 
                           Else 
                              '
                              ' Droite 
                              '
                              If Jright(0)
                                 If SENS2=GAUCHE
                                    SENS2=DROITE
                                    J2_ANIM
                                 End If 
                                 If Y2>=0
                                    If X2<224
                                       If Y2>=0
                                          If C(X+1,Y+1)=0
                                             Add X2,VIT(2)
                                          End If 
                                       End If 
                                    End If 
                                 End If 
                              End If 
                           End If 
                        End If 
                        '
                        Add Y2,VIT(2)
                        '
                     Else 
                        '
                        ' Redescendu d'une case: continuer à tomber ?
                        '
                        If Y2=223
                           STATE2=STANDING
                           LONGTEMPS2=0
                           J2_ANIM
                        Else 
                           If(C(X,Y+2)>0) or(C((X2+15)/16,Y+2)>0)
                              STATE2=STANDING
                              LONGTEMPS2=0
                              J2_ANIM
                           Else 
                              FALL2=0
                              Inc LONGTEMPS2
                              J2_ANIM
                           End If 
                        End If 
                     End If 
                  End If 
               End If 
               '
               ' Poum ! 
               '
               If Fire(0) and(GLOUGLOU2=False)
                  If Amreg(5)=0
                     Amreg(5)=1
                     Amreg(7)=X2
                     Amreg(8)=Y2
                     Amreg(9)=SENS2
                     If(STATE2<>JUMPING) and(STATE2<>FALLING)
                        If SENS2=DROITE
                           Amal 2,"Let A=156;"
                        Else 
                           Amal 2,"Let A=175;"
                        End If 
                        Amal On 2
                        POUM2=1
                     End If 
                  End If 
               Else 
                  If POUM2=1
                     POUM2=0
                     J2_ANIM
                  End If 
               End If 
               '
               '++++++++++++++++++++++++++++++++++++
               '
               '
               ' On prend un Bonus ?
               '
               If Bob Col(JOUEUR2,BONUS To BONUS)
                  Proc EFFET_BONUS[2]
                  Bob BONUS,,300,
                  BONUSWAIT=150
               End If 
               '
               ' Un trésor ?
               '
               If Bob Col(JOUEUR2,TRESOR(1) To TRESOR(NBTRESORS))
                  If CARRY2=0
                     For I=1 To NBTRESORS
                        If Col(TRESOR(I))
                           CARRY2=I
                        End If 
                     Next I
                     TRESOR_TOMBE(CARRY2)=0
                     Bob Off TRESOR(CARRY2)
                     Proc UPDSCORE[2,100]
                     Sam Play VOIX2,1
                  End If 
               End If 
               '
               ' On est en bas à droite ? 
               '
               If X2=224 and Y2=223
                  '
                  ' Ramène un trésor 
                  '
                  If CARRY2>0
                     CARRY2=0
                     Inc RAMENE2
                     Proc UPDSCORE[2,2000]
                     If RAMENE1+RAMENE2=NBTRESORS
                        PASSE=True
                     End If 
                     Sam Play VOIX2*2,2
                  End If 
                  '
                  ' Remonter le temps
                  '
                  If Joy(0)=2
                     If GLOUGLOU2=0
                        Limit Bob 2,0,0 To 256,224
                        Sam Play VOIX2,4,8000
                        GLOUGLOU2=1
                        Amal 2,"Anim 0,(157,2)(158,2)(159,2)(158,2);"
                        Amal On 2
                        SENS2=DROITE
                     End If 
                     If HURRY=False
                        If MEMETEMPS
                           Inc TEMPS1
                           TEMPS1=Min(TEMPS1,MXTEMPS1)
                           Ink 4
                           TP=208-TEMPS1
                           Draw 241,TP+1 To 254,TP+1
                           Draw 241,TP To 254,TP
                           Draw 1,TP+1 To 14,TP+1
                           Draw 1,TP To 14,TP
                        Else 
                           Inc TEMPS2
                           TEMPS2=Min(TEMPS2,MXTEMPS2)
                           Ink 4
                           TP=208-TEMPS2
                           Draw 241,TP+1 To 254,TP+1
                           Draw 241,TP To 254,TP
                        End If 
                     End If 
                  Else 
                     If GLOUGLOU2=1
                        J2_ANIM
                     End If 
                  End If 
               End If 
            End If 
            '
            ' --- Ceci doit toujours être exécuté ---
            '
            '
            ' On tire sur un monstre 
            '
            If Bob Col(TIR2,ENNEMI(1) To ENNEMI(NBMONSTRES))
               For I=ENNEMI(1) To ENNEMI(5)
                  Exit If Col(I)
               Next I
               If DANSLESPOMMES(I-4)=False
                  DANSLESPOMMES(I-4)=1
                  Amal Freeze I
                  Bob TIR2,,320,
                  If SFXON
                     Shoot 
                  End If 
                  Proc UPDSCORE[2,10]
               End If 
            End If 
            '
            ' On tire sur l'autre !
            '
            If Bob Col(TIR2,JOUEUR1 To JOUEUR1) and PARALIZED1=False
               PARALIZED1=1
               Amal Freeze 1
               Amal On 10
               Bob TIR2,,320,
               Sam Play VOIX2,5
               Proc UPDSCORE[2,50]
            End If 
            '
            ' Collision avec monstre, aïe !
            '
            If Bob Col(JOUEUR2,ENNEMI(1) To ENNEMI(NBMONSTRES))
               MORT2=True
               Sam Play VOIX2,5
               J2_ANIM
               Bob Off TIR2
               If CARRY2<>0
                  Bob TRESOR(CARRY2),X2-X2 mod 16,Y2,72
                  TRESOR_TOMBE(CARRY2)=True
               End If 
               UPDSCORE[0,0]
            End If 
            '
            ' Plus de temps !
            '
            If(TEMPS2=0) or(TEMPS1=0 and MEMETEMPS)
               MORT2=True
               Sam Play VOIX2,5
               J2_ANIM
               Bob Off TIR2
               If CARRY2<>0
                  Bob TRESOR(CARRY2),X2-X2 mod 16,Y2,72
                  TRESOR_TOMBE(CARRY2)=True
               End If 
               UPDSCORE[0,0]
            End If 
         End If 
         '
         '================== Diverses gestions ==================== 
         '
         ' Le bonus ! 
         '
         Inc BONUSWAIT
         If BONUSWAIT=200
            BONUSWAIT=0
            BONUS
         End If 
         '
         ' Les trésors tombent
         '
         For I=1 To NBTRESORS
            If TRESOR_TOMBE(I)=True
               T=TRESOR(I)
               X=X Bob(T)
               Y=Y Bob(T)
               If Y mod 16<15
                  Bob T,,Y+1,
               Else 
                  If Y=223
                     TRESOR_TOMBE(I)=0
                  Else 
                     If C(X/16,Y/16+2)>0
                        TRESOR_TOMBE(I)=0
                     Else 
                        Bob T,,Y+1,
                     End If 
                  End If 
               End If 
            End If 
         Next I
         '
         ' Le temps descend (sauf en trainer) 
         '
         If CHEAT=False
            If Timer>12
               Ink 0
               If MORT1=False
                  Dec TEMPS1
                  If TEMPS1>0
                     Draw 1,207-TEMPS1 To 14,207-TEMPS1
                     Draw 1,208-TEMPS1 To 14,208-TEMPS1
                  End If 
               End If 
               If MORT2=False
                  Dec TEMPS2
                  If TEMPS2>0
                     Draw 241,207-TEMPS2 To 254,207-TEMPS2
                     Draw 241,208-TEMPS2 To 254,208-TEMPS2
                  End If 
               End If 
               Timer=0
            End If 
         End If 
         Add TEMPSTOTAL,2
         If TEMPSTOTAL>=MXTEMPSTOTAL
            HURRY=True
            ' Mettre un sample...
         End If 
         '
         ' Ennemis paralizés ?
         '
         For I=ENNEMI(1) To ENNEMI(NBMONSTRES)
            If DANSLESPOMMES(I-4)=200
               DANSLESPOMMES(I-4)=0
               Amal On I
            End If 
            If DANSLESPOMMES(I-4)<>0
               Inc DANSLESPOMMES(I-4)
            End If 
         Next I
         '
         ' Le clavier...
         '
         K$=Inkey$
         If K$<>""
            K$=Upper$(K$)
            ' Esc
            If Asc(K$)=27
               If Key Shift<>0
                  Proc FIN
               Else 
                  MORT1=True
                  MORT2=True
                  CREDITS=1
               End If 
            End If 
            ' Pause
            If K$="P"
               Amal Freeze 
               Repeat 
                  Multi Wait 
               Until Upper$(Inkey$)="P"
               For I=1 To 4
                  Amal On I
               Next I
               For I=5 To NBMONSTRES+4
                  If DANSLESPOMMES(I-4)=0
                     Amal On I
                  End If 
               Next I
            End If 
            ' Mémoire
            If K$="M"
               Ink 1,0
               Text 10,8,"Fast:"+Str$(Fast Free)+" / Chip:"+Str$(Chip Free)+"  "
            End If 
            ' Cheat On/Off 
            If K$="§"
               CHEAT= Not(CHEAT)
            End If 
            ' Crédits +++
            If K$="C"
               If CHEAT=True
                  CREDITS=Min(99,CREDITS+1)
                  L$=Str$(CREDITS)-" "
                  If Len(L$)=1
                     L$="0"+L$
                  End If 
                  Ink 22,0
                  Text 280,218,L$
               End If 
            End If 
            ' NeXT 
            If K$="+"
               If CHEAT=True
                  PASSE=True
               End If 
            End If 
         End If 
         '
         Synchro 
         If MORT1=0
            Bob 1,X1,Y1,
         Else 
            Wait Vbl 
         End If 
         If MORT2=0
            Bob 2,X2,Y2,
         Else 
            Wait Vbl 
         End If 
         Wait Vbl 
         '
      Until((MORT1=True) and(MORT2=True)) or(PASSE=True)
      '
      '************************************************************
      '
      For I=1 To 8
         Synchro 
         Wait Vbl 
      Next I
      Amal Off 
      MUSIQUE_OFF
      Wait 4
      Fade 2
      Wait 30
      Wait Vbl 
      Bob Off 
      Wait Vbl 
      '
      ' On a gagné ! 
      '
      If PASSE
         If SFXON
            Sam Play %1111,3
         End If 
         If RAMENE1>RAMENE2
            Inc VICT1
            Proc _MESSAGE[MSG_WINNER,1]
         Else 
            If RAMENE2>RAMENE1
               Inc VICT2
               Proc _MESSAGE[MSG_WINNER,2]
            Else 
               Proc _MESSAGE[MSG_EGALITE,0]
            End If 
         End If 
         If PUBON
            If LEVEL mod 5=0
               _MESSAGE[MSG_BONUS,0]
               Proc JEU[LEVEL/5]
            Else 
               Proc PUB[LEVEL]
            End If 
         End If 
         Inc LEVEL
         _MESSAGE[MSG_GET_READY,0]
         CHARGER[LEVEL]
         Timer=0
         Repeat 
            Multi Wait 
         Until Fire(1) or Fire(0) or Timer>=400
         Fade 1
         Wait 15
         Cls 0
      Else 
         '
         ' On a perdu ! 
         '
         Dec CREDITS
         If CREDITS>0
            _MESSAGE[MSG_OH_NO,0]
         End If 
      End If 
   Until CREDITS=0
   '
   '************************************
   '
   ' Fin de la partie 
   '
   Proc _MESSAGE[MSG_GAME_OVER,0]
   '
   ' Bon score ?
   '
   For I=1 To 10
      If SCORE1>HISCORE(I)
         For J=10 To I+1 Step -1
            HISCORE(J)=HISCORE(J-1)
            HISCORE$(J)=HISCORE$(J-1)
         Next J
         HISCORE(I)=SCORE1
         HISCORE$(I)=NAME1$
         Exit 
      End If 
   Next I
   For I=1 To 10
      If SCORE2>HISCORE(I)
         For J=10 To I+1 Step -1
            HISCORE(J)=HISCORE(J-1)
            HISCORE$(J)=HISCORE$(J-1)
         Next J
         HISCORE(I)=SCORE2
         HISCORE$(I)=NAME2$
         Exit 
      End If 
   Next I
   '
   ' Affiche les scores 
   '
   Cls 0
   Erase 3
   Erase 5
   HALL_OF_FAME
   Fade 1,0,$FFF,$AAA,$555
   Wait 15
   '
   ' Sauvegarde 
   '
   If HISAVEON
      SAUVE_SCORES
   End If 
   '
   While Fire(1) or Fire(0)
      Multi Wait 
   Wend 
   Repeat 
      Multi Wait 
   Until Fire(1) or Fire(0)
   Fade 1
   Wait 15
   '
   ' Et c'est reparti pour un tour !
   '
Loop 
'
'***************** PROCEDURES **************** 
'
Procedure INIT
   '
   ' Charge les données principales + affiche imgs + lance zik
   ' pour faire patienter...
   ' Sans oublier ce merveilleux sample:
   ' 'First, there was nothing. Now, PowerMob Productions presents
   ' a T.H.A. Design game: Twin-Baston !' 
   '
   ' Banques:  1 = sprites
   ' ~~~~~~~~  2 = icones 
   '           3 = musique
   '           5 = samples
   '           6 = img fonte ( credits scroll ) 
   '           9 = image de fond / Img intro / Img prés.
   '          10 = img prés. / Imgs Pubs / Img Copyright
   '
   Shared ZIKPLAYING,QUELLEZIK,ZIKVOL
   FAST=0
   '
   ' Met tout propre
   '
   Close Editor 
   Close Workbench 
   While Screen>=0
      Screen Close Screen
   Wend 
   For I=1 To 16
      Erase I
   Next I
   '
   Colour Back 0
   Auto View Off 
   Screen Open 0,320,200,2,0
   Palette $0,$FFF
   Curs Off 
   Cls 0
   Hide On 
   If FAST=0
      Proc _COPYRIGHT
   End If 
   Auto View On 
   '
   CHARGE_PREFS
   Load "Twin:Zik-Intro.Abk",3
   Load "Twin:Img-Intro.Abk",9
   '
   If FAST=0
      Reserve As Chip Work 5,36662
      Reserve As Chip Work 6,53496
      Reserve As Chip Work 7,39876
      Reserve As Chip Work 8,19578
      Bload "Twin:Samples-First1",5
      Bload "Twin:Samples-First2",6
      Bload "Twin:Samples-First3",7
      Bload "Twin:Samples-First4",8
      '
      Volume 0
      Shoot 
      Volume 63
      Led On 
      '
      Sam Raw 15,Start(5),Length(5),14000
      Wait 140
      Erase 5
      Sam Raw 15,Start(6),Length(6),14000
      Wait 60
      Centre At(,5)+"POWERMOB PRODUCTIONS"
      Wait 130
      Erase 6
      Sam Raw 15,Start(7),Length(7),14000
      Wait 25
      Centre At(,10)+"THEVENOT"
      Wait 15
      Centre At(,11)+"H I L P I P R E"
      Wait 15
      Centre At(,12)+"A   M   I   C   E   L"
      Wait 65
      Fade 1
      Wait 15
   End If 
   Unpack 9 To 0
   Wait 10
   If FAST=0
      Erase 7
      Wait Vbl 
      Sam Raw 15,Start(8),Length(8),7000
      Wait Vbl 
      Wait 140
      Erase 8
   End If 
   Erase 9
   '
   Voice 15
   Mvolume ZIKVOL
   Led Off 
   Music 1
   ZIKPLAYING=True
   QUELLEZIK=1
   '
   Load "Twin:Lettres.Abk"
   Bank Swap 1,2
   Load "Twin:Off-Sprites.Abk"
   CHARGE_SCORES
   '
   Priority Reverse On 
End Proc
Procedure FIN
   '
   ' Serait-ce la fin ? 
   '
   Erase 2
   Music Off 
   Erase 3
   Erase 5
   Erase 9
   Erase 10
   Amal Off 
   Bob Off 
   Erase 1
   End 
End Proc
'
Procedure _COPYRIGHT
   '
   ' © © © © © © © © ® ® ® ® ® ® ® ®
   '
   Load "Twin:Img-Copyright.Abk",10
   YDISPLAY=75
   Unpack 10 To 7
   For Y=2 To Screen Height/2 Step 8
      Screen Display 7,,YDISPLAY+Screen Height/2-Y,,Y*2
      Screen Offset 7,,Screen Height/2-Y
      View 
      Wait Vbl 
   Next Y
   T=Timer
   Repeat 
      Multi Wait 
   Until Len(Inkey$) or Fire(1) or Fire(0) or Timer-T>200
   For Y=Screen Height/2 To 0 Step -8
      Screen Display 7,,YDISPLAY+Screen Height/2-Y,,Y*2
      Screen Offset 7,,Screen Height/2-Y
      View 
      Wait Vbl 
   Next Y
   Screen Close 7
   Erase 10
End Proc
Procedure PRESENTATION
   '
   ' Affiche en boucle des imgs avec zik, attend Fire.
   '
   Shared ZIKVOL,ZIKPLAYING
   Screen 0
   Cls 0
   Screen To Front 0
   Load "Twin:Img-Intro2.abk",9
   Load "Twin:Img-Intro3.abk",10
   If ZIKPLAYING=False
      Proc MUSIQUE[1,ZIKVOL]
   End If 
   Wait Vbl 
   HALL=False
   Do 
      '
      ' Img Twin Baston
      '
      Auto View Off 
      Wait Vbl 
      Unpack 9 To 2
      Screen Display 2,128,40,,
      Palette 0,0,0,0
      Auto View On 
      Wait Vbl 
      Fade 1,$0,$FFF,$AAA,$555
      Timer=0
      Repeat 
         Exit If Fire(1) or Fire(0),2
         If(Inkey$=Chr$(27)) and(Key Shift<>0)
            FIN
         End If 
         Multi Wait 
      Until Timer>=250
      Fade 1
      Wait 15
      '
      ' Img PowerMob 
      '
      Auto View Off 
      Wait Vbl 
      Unpack 10 To 2
      Screen Display 2,128,40,,
      Palette 0,0,0,0
      Auto View On 
      Wait Vbl 
      Fade 1,$0,$FFF,$AAA,$555
      Timer=0
      Repeat 
         Exit If Fire(1) or Fire(0),2
         If(Inkey$=Chr$(27)) and(Key Shift<>0)
            FIN
         End If 
         Multi Wait 
      Until Timer>=250
      Fade 1
      Wait 16
      '
      ' Img Scores 
      '
      Screen 0
      Screen To Front 0
      If HALL=False
         Proc HALL_OF_FAME
         HALL=True
      Else 
         Wait 15
      End If 
      Fade 1,0,$FFF,$AAA,$555
      Timer=0
      Repeat 
         Exit If Fire(1) or Fire(0),2
         If(Inkey$=Chr$(27)) and(Key Shift<>0)
            FIN
         End If 
         Multi Wait 
      Until Timer>=250
      Fade 1
      Wait 15
   Loop 
   '
   Repeat 
      Multi Wait 
   Until Fire(1)=0 and Fire(0)=0
   Fade 1
   Wait 15
   Screen Hide 0
   Screen Hide 2
   Screen Close 2
   Screen 0
   Screen To Front 0
   Flash Off 
   Cls 0
   Screen Show 0
   Erase 9
   Erase 10
End Proc
Procedure OPTIONS
   '
   '  Choix des options ( musique, pub, etc... )
   '
   Shared ZIKON,ZIKVOL,SFXON,SFXVOL,PUBON,NAME1$,NAME2$,HISAVEON,MSG_OPTIONS
   '
   ' Inits
   '
   Dim S$(1),S2$(1)
   S$(0)="OFF"
   S$(1)="ON"
   S2$(0)="NON"
   S2$(1)="OUI"
   C=1
   '
   Dim T$(10),Y(10),CO(10)
   Restore Z
   For I=1 To 10
      Read CO(I),Y(I),T$(I)
   Next I
   Z:
   Data 2,80," MUSIQUE - "+S$(-ZIKON)+" "
   Data 2,90," VOLUME -"+Str$(ZIKVOL)+" "
   Data 2,110," BRUITAGES - "+S$(-SFXON)+" "
   Data 2,120," VOLUME -"+Str$(SFXVOL)+" "
   Data 2,140," PUBLICITE - "+S$(-PUBON)+" "
   Data 2,160," JOUEUR 1 - "+NAME1$+" "
   Data 2,170," JOUEUR 2 - "+NAME2$+" "
   Data 2,190," SAUVER HI-SCORES - "+S2$(-HISAVEON)+" "
   Data 5,230,"SAUVER LA CONFIGURATION"
   Data 5,250,"- JOUER -"
   '
   ' Affichage
   '
   Palette 0,0,0,0,0,0
   _MESSAGE[MSG_OPTIONS,0]
   For I=1 To 10
      If I=C
         Ink 4,0
      Else 
         Ink CO(I),0
      End If 
      CTR[Y(I),T$(I)]
   Next I
   '
   F$="(11F,1)(33F,1)(55F,1)(77F,1)(99F,1)(BBF,1)(DDF,1)(FFF,1)(DDF,1)(BBF,1)(99F,1)(77F,1)(55F,1)(33F,1)"
   Fade 1,$0,$FFF,$AAA,$555,$F,$F0
   Wait 15
   Flash 4,F$
   '
   ' Boucle de gestion
   '
   Ink 4,0
   GO=False
   Repeat 
      '
      If Jup(1)
         If C>1
            Ink CO(C)
            CTR[Y(C),T$(C)]
            Dec C
            Ink 4
            CTR[Y(C),T$(C)]
         End If 
         Wait 2
      End If 
      '
      If Jdown(1)
         If C<10
            Ink CO(C)
            CTR[Y(C),T$(C)]
            Inc C
            Ink 4
            CTR[Y(C),T$(C)]
         End If 
         Wait 2
      End If 
      '  
      If Fire(1)
         '
         ' Pas de 'Select/Case' en AMOS...
         '
         If C=1
            ' Musique On/Off 
            ZIKON= Not(ZIKON)
            T$(1)=" MUSIQUE - "+S$(-ZIKON)+" "
            CTR[Y(1),T$(1)]
         End If 
         If C=2
            ' Volume Musique 
            While Fire(1)
               If Jleft(1)
                  ZIKVOL=Max(0,ZIKVOL-1)
                  T$(2)=" VOLUME -"+Str$(ZIKVOL)+" "
                  CTR[Y(2),T$(2)]
               End If 
               If Jright(1)
                  ZIKVOL=Min(63,ZIKVOL+1)
                  T$(2)=" VOLUME -"+Str$(ZIKVOL)+" "
                  CTR[Y(2),T$(2)]
               End If 
               Multi Wait 
            Wend 
         End If 
         If C=3
            ' Bruitages On/Off 
            SFXON= Not(SFXON)
            T$(3)=" BRUITAGES - "+S$(-SFXON)+" "
            CTR[Y(3),T$(3)]
         End If 
         If C=4
            ' Volume Bruitages 
            While Fire(1)
               If Jleft(1)
                  SFXVOL=Max(0,SFXVOL-1)
                  T$(4)=" VOLUME -"+Str$(SFXVOL)+" "
                  CTR[Y(4),T$(4)]
               End If 
               If Jright(1)
                  SFXVOL=Min(63,SFXVOL+1)
                  T$(4)=" VOLUME -"+Str$(SFXVOL)+" "
                  CTR[Y(4),T$(4)]
               End If 
               Multi Wait 
            Wend 
         End If 
         If C=5
            ' Pub On/Off 
            PUBON= Not(PUBON)
            T$(5)=" PUBLICITE - "+S$(-PUBON)+" "
            CTR[Y(5),T$(5)]
         End If 
         If C=6
            ' Nom du Joueur 1
            T$(6)=" JOUEUR 1 - "+NAME1$+"_ "
            CTR[Y(6),T$(6)]
            Clear Key 
            Repeat 
               Repeat 
                  A$=Inkey$
                  A=Scancode
               Until A$<>""
               If A$=Chr$(8)
                  NAME1$=""
                  T$(6)=" JOUEUR 1 - _ "
                  CTR[Y(6),Space$(40)]
                  CTR[Y(6),T$(6)]
               End If 
               If((A$>="A") and(A$<="Z")) or((A$>="a") and(A$<="z")) or((A$="-") or(A$=" ") or(A$="!") or(A$="é") or(A$="è") or(A$="ê") or(A$=".") or(A$="ç"))
                  If Len(NAME1$)<20
                     NAME1$=NAME1$+A$
                     T$(6)=" JOUEUR 1 - "+NAME1$+"_ "
                     CTR[Y(6),T$(6)]
                  End If 
               End If 
            Until A$=Chr$(13)
            T$(6)=" JOUEUR 1 - "+NAME1$+" "
            CTR[Y(6),T$(6)]
         End If 
         If C=7
            ' Nom du Joueur 2
            T$(7)=" JOUEUR 2 - "+NAME2$+"_ "
            CTR[Y(7),T$(7)]
            Clear Key 
            Repeat 
               Repeat 
                  A$=Inkey$
                  A=Scancode
               Until A$<>""
               If A$=Chr$(8)
                  NAME2$=""
                  T$(7)=" JOUEUR 2 - _ "
                  CTR[Y(7),Space$(40)]
                  CTR[Y(7),T$(7)]
               End If 
               If((A$>="A") and(A$<="Z")) or((A$>="a") and(A$<="z")) or((A$="-") or(A$=" ") or(A$="!") or(A$="é") or(A$="è") or(A$="ê") or(A$=".") or(A$="ç"))
                  If Len(NAME2$)<20
                     NAME2$=NAME2$+A$
                     T$(7)=" JOUEUR 2 - "+NAME2$+"_ "
                     CTR[Y(7),T$(7)]
                  End If 
               End If 
            Until A$=Chr$(13)
            T$(7)=" JOUEUR 2 - "+NAME2$+" "
            CTR[Y(7),T$(7)]
         End If 
         If C=8
            ' Sauver scores on/off 
            HISAVEON= Not(HISAVEON)
            T$(8)=" SAUVER HI-SCORES - "+S2$(-HISAVEON)+" "
            CTR[Y(8),T$(8)]
         End If 
         If C=9
            ' Sauver Config
            SAUVE_PREFS
         End If 
         If C=10
            ' Let's Go ! 
            GO=True
         End If 
         '
         While Fire(1)
         Wend 
         '
      End If 
      '
      Multi Wait 
      '
   Until GO
   '
   MUSIQUE_OFF
   Flash Off 
   Fade 1
   Wait 15
   Cls 0
   Erase 3
   '
End Proc
Procedure HALL_OF_FAME
   '
   ' Affiche les 'Ten Top Scores' 
   '
   Shared HISCORE(),HISCORE$(),MSG_SCORES
   Autoback 0
   Flash Off 
   Palette 0,0,0,0
   Cls 0
   _MESSAGE[MSG_SCORES,0]
   Ink 1,0
   For I=1 To 10
      H$=Str$(HISCORE(I))-" "
      S$=Right$(Str$(I),2)
      If I=1
         S$=S$+"ST"
      Else 
         If I=2
            S$=S$+"ND"
         Else 
            If I=3
               S$=S$+"RD"
            Else 
               S$=S$+"TH"
            End If 
         End If 
      End If 
      S$=S$+"  "+String$("0",6-Len(H$))+H$+"  "+HISCORE$(I)
      Text 16,12*I+80,S$
   Next I
   Screen Copy Logic(0) To Physic(0)
   Autoback 2
End Proc
'
Procedure INIT_TABLEAU
   '
   ' Prépare tout pour le tableau à venir 
   '
   Shared LEVEL,VICT1,VICT2,CREDITS,ZIKON,ZIKVOL,SFXVOL
   Shared JOUEUR1,JOUEUR2,TIR1,TIR2,MORT1,MORT2,VIT(),INV()
   Shared TEMPS1,TEMPS2,MXTEMPS1,MXTEMPS2,MEMETEMPS,MXTEMPSTOTAL
   Shared ENNEMI(),DANSLESPOMMES(),NBMONSTRES
   Shared TRESOR(),NBTRESORS,TRESOR_TOMBE()
   '
   ' *** Ecran
   '
   Auto View Off 
   Screen Open 0,320,256,32,Lowres
   Screen Hide 0
   Curs Off 
   Flash Off 
   Hide On 
   Wait Vbl 
   '
   Cls 0
   Unpack 9 To 1
   Screen 0
   Get Palette 1
   Wait Vbl 
   Screen Copy 1 To 0
   Wait Vbl 
   Screen Close 1
   Screen Display 0,128,40,320,256
   Ink 24,0
   Text 268,185,"LEVEL"
   Text 260,208,"CREDITS"
   Text 260,231,"OVERALL"
   Ink 22,0
   L$=Str$(LEVEL)-" "
   If Len(L$)=1
      L$="0"+L$
   End If 
   Text 280,195,L$
   L$=Str$(CREDITS)-" "
   If Len(L$)=1
      L$="0"+L$
   End If 
   Text 280,218,L$
   L$=(Str$(VICT1)-" ")+" /"+Str$(VICT2)
   If Len(Str$(VICT1))+Len(Str$(VICT2))=4
      L$=" "+L$
   End If 
   Text 260,241,L$
   '
   ' *** Temps
   '
   Ink 4
   TEMPS1=MXTEMPS1
   Bar 1,208-TEMPS1 To 14,207
   If MEMETEMPS
      MXTEMPS2=MXTEMPS1
   End If 
   TEMPS2=MXTEMPS2
   Bar 241,208-TEMPS2 To 254,207
   '
   Proc UPDSCORE[0,0]
   '
   Double Buffer 
   Autoback 1
   '
   '
   ' *** Briques et trésors 
   '
   '
   NBTRESORS=0
   For X=1 To 14
      For Y=1 To 14
         C(X,Y)=C2(X,Y)
         If C(X,Y)>0
            If C(X,Y)=72
               C(X,Y)=0
               If NBTRESORS<4
                  Inc NBTRESORS
                  Bob TRESOR(NBTRESORS),X*16,Y*16-1,72
               End If 
            Else 
               Paste Bob X*16,Y*16-16,C(X,Y)
            End If 
         End If 
      Next Y
   Next X
   '
   For I=1 To NBTRESORS
      TRESOR_TOMBE(I)=0
   Next I
   '
   '
   ' ***  Joueurs 
   '
   '
   Bob JOUEUR1,16,223,1
   Channel 1 To Bob JOUEUR1
   Channel 10 To Bob JOUEUR1
   Amal 10,"Anim 0,(39,8)(40,8)(41,8);"
   '
   Bob JOUEUR2,224,223,165
   Channel 2 To Bob JOUEUR2
   Channel 11 To Bob JOUEUR2
   Amal 11,"Anim 0,(184,8)(185,8)(186,8);"
   '
   Limit Bob 16,0 To 240,224
   '
   '
   ' *** Tir 1
   '
   A$="      Let RA=0;"
   A$=A$+"Loop:"
   A$=A$+"   If RA=0 then Jump Loop;"
   A$=A$+"   Let X=RC; Let Y=RD;"
   A$=A$+"   If RE=1 then Jump M;"
   A$=A$+"   Let A=43;"
   A$=A$+"   Move -48,0,12;"
   A$=A$+"   Jump N;"
   A$=A$+"M: Let A=42"
   A$=A$+"   Move 48,0,12;"
   A$=A$+"N: Let Y=320;"
   A$=A$+"   Let RA=0"
   A$=A$+"   Jump Loop;"
   '
   Bob TIR1,10,320,42
   Limit Bob TIR1,16,0 To 240,224
   Channel 3 To Bob TIR1
   Amal 3,A$
   '
   '
   ' *** Tir 2
   '
   A$="      Let RF=0;"
   A$=A$+"Loop:"
   A$=A$+"   If RF=0 then Jump Loop;"
   A$=A$+"   Let X=RH; Let Y=RI;"
   A$=A$+"   If RJ=1 then Jump M;"
   A$=A$+"   Let A=43;"
   A$=A$+"   Move -48,0,12;"
   A$=A$+"   Jump N;"
   A$=A$+"M: Let A=42;"
   A$=A$+"   Move 48,0,12;"
   A$=A$+"N: Let Y=320;"
   A$=A$+"   Let RF=0"
   A$=A$+"   Jump Loop;"
   '
   Bob TIR2,30,320,43
   Limit Bob TIR2,16,0 To 240,224
   Channel 4 To Bob TIR2
   Amal 4,A$
   '
   '
   ' *** Ennemis
   '
   '
   FOUTRE_MONSTRES
   '
   For I=1 To NBMONSTRES
      DANSLESPOMMES(I)=0
   Next I
   '
   ' *** Divers 
   '
   Clear Key 
   '
   Auto View On 
   Screen Show 0
   '
   Volume SFXVOL
   Mvolume 0
   If ZIKON
      MUSIQUE[2,ZIKVOL]
   End If 
   '
   VIT(1)=2
   VIT(2)=2
   INV(1)=False
   INV(2)=False
   '
   Wait Vbl 
   '
End Proc
Procedure BONUS
   '
   '  Afficher un nouveau bonus 
   '
   Shared BONUS,LEVEL,CURBONUS
   Do 
      X=Rnd(13)+1
      Y=Rnd(13)+1
      If C(X,Y)=0
         If Y=14
            Exit 
         Else 
            If C(X,Y+1)=1
               Exit 
            End If 
         End If 
      End If 
   Loop 
   CURBONUS=Rnd(16)+75
   Bob BONUS,X*16,Y*16-16,CURBONUS
End Proc
Procedure EFFET_BONUS[N]
   '
   '  S'occupe des bonus ( j1 & j2 )
   '
   Shared CURBONUS,SFXON,CREDITS,JOUEUR1,JOUEUR2,TEMPS1,TEMPS2
   Shared PARALIZED1,PARALIZED2,MXTEMPS1,MXTEMPS2,MEMETEMPS
   Shared VIT(),INV(),SCORE1,SCORE2
   '
   A=CURBONUS-74
   If N=1
      VOIX=%1
   Else 
      VOIX=%10
   End If 
   If SFXON=False
      VOIX=%0
   End If 
   '
   '----------------
   '
   '  ? 
   If A=13
      Repeat 
         A=Rnd(16)+1
      Until A<>13
   End If 
   ' Epée 
   If A=1
      Sam Play VOIX,1
      UPDSCORE[N,200]
      Pop Proc
   End If 
   ' Anneau 'Sonic' 
   If A=2
      Sam Play VOIX,1
      UPDSCORE[N,500]
      Pop Proc
   End If 
   ' Citron 
   If A=3
      Sam Play VOIX,1
      UPDSCORE[N,500]
      Pop Proc
   End If 
   ' Fraise 
   If A=4
   End If 
   ' Fleur bleue
   If A=5
      Sam Play VOIX,1
      UPDSCORE[N,-100]
      Pop Proc
   End If 
   ' Fleur rouge
   If A=6
      Sam Play VOIX,1
      UPDSCORE[N,-100]
      Pop Proc
   End If 
   ' Diamant brillant 
   If A=7
      Sam Play VOIX,1
      UPDSCORE[N,5000]
      Pop Proc
   End If 
   ' Diamant carré
   If A=8
      Sam Play VOIX,1
      UPDSCORE[N,2500]
      Pop Proc
   End If 
   ' "Soleil" 
   If A=9
      Sam Play VOIX,1
      UPDSCORE[N,200]
      Pop Proc
   End If 
   ' Coeur
   If A=10
      Sam Play VOIX,1
      CREDITS=Min(99,CREDITS+1)
      L$=Str$(CREDITS)-" "
      If Len(L$)=1
         L$="0"+L$
      End If 
      Ink 22,0
      Text 280,218,L$
      Pop Proc
   End If 
   ' Skate board
   If A=11
      Sam Play VOIX,1
      VIT(N)=4
      Pop Proc
   End If 
   ' Patin à roulettes
   If A=12
      Sam Play VOIX,1
      UPDSCORE[N,250]
      Pop Proc
   End If 
   ' Gant de boxe 
   If A=14
      B=Rnd(2)
      If((N=1) and(B=0)) or((N=2) and(B>0))
         If PARALIZED1=0
            Sam Play VOIX,5
            PARALIZED1=1
            Amal Freeze 1
            Amal On 10
            UPDSCORE[2,750]
         End If 
      Else 
         If PARALIZED2=0
            Sam Play VOIX,5
            PARALIZED2=1
            Amal Freeze 2
            Amal On 11
            UPDSCORE[1,750]
         End If 
      End If 
      Pop Proc
   End If 
   ' Shaker 
   If A=15
      Sam Play VOIX,1
      Swap SCORE1,SCORE2
      UPDSCORE[0,0]
      Pop Proc
   End If 
   ' Verre de coca
   If A=16
      Sam Play VOIX,4,8000
      Ink 4
      If(N=1) or(MEMETEMPS=True)
         TEMPS1=MXTEMPS1
         Bar 1,208-TEMPS1 To 14,207
      End If 
      If(N=2) or(MEMETEMPS=True)
         TEMPS2=MXTEMPS2
         Bar 241,208-TEMPS2 To 254,207
      End If 
      Pop Proc
   End If 
   ' Joystick 
   If A=50
      Sam Play VOIX,1
      UPDSCORE[N,500]
      If N=1
         INV(2)=True
      Else 
         INV(1)=True
      End If 
      Pop Proc
   End If 
End Proc
'
Procedure J1_ANIM
   '
   ' Met en place l'anim qu'il faut 
   '
   Shared STATE1,SENS1,STANDING,WALKING,JUMPING,FALLING,DROITE,GAUCHE
   Shared GLOUGLOU1,MORT1,LONGTEMPS1
   If MORT1=True
      A$="Let A=16; Move 0,-16,4; Move 0,300,75;"
   Else 
      If STATE1=STANDING
         If SENS1=DROITE
            A$="Let A=1;"
         Else 
            A$="Let A=20;"
         End If 
      End If 
      If STATE1=WALKING
         If SENS1=DROITE
            A$="Anim 0,(2,2)(3,2)(4,2)(5,2)(4,2)(3,2);"
         Else 
            A$="Anim 0,(21,2)(22,2)(23,2)(24,2)(23,2)(22,2);"
         End If 
      End If 
      If STATE1=JUMPING
         If SENS1=DROITE
            A$="Let A=15;"
         Else 
            A$="Let A=34;"
         End If 
      End If 
      If STATE1=FALLING
         A=16
         If SENS1=GAUCHE
            Add A,19
         End If 
         If LONGTEMPS1>2
            Inc A
         End If 
         A$="Let A="+Str$(A)+";"
      End If 
   End If 
   Limit Bob 1,16,0 To 240,224
   Amal 1,A$
   Amal On 1
   GLOUGLOU1=0
End Proc
Procedure J2_ANIM
   '
   ' Met en place l'anim qu'il faut 
   '
   Shared STATE2,SENS2,STANDING,WALKING,JUMPING,FALLING,DROITE,GAUCHE
   Shared GLOUGLOU2,MORT2,LONGTEMPS2
   If MORT2=True
      A$="Let A=161; Move 0,-16,4; Move 0,300,75;"
   Else 
      If STATE2=STANDING
         If SENS2=DROITE
            A$="Let A=146;"
         Else 
            A$="Let A=165;"
         End If 
      End If 
      If STATE2=WALKING
         If SENS2=DROITE
            A$="Anim 0,(147,2)(148,2)(149,2)(150,2)(149,2)(148,2);"
         Else 
            A$="Anim 0,(166,2)(167,2)(168,2)(169,2)(168,2)(167,2);"
         End If 
      End If 
      If STATE2=JUMPING
         If SENS2=DROITE
            A$="Let A=160;"
         Else 
            A$="Let A=179;"
         End If 
      End If 
      If STATE2=FALLING
         A=161
         If SENS2=GAUCHE
            Add A,19
         End If 
         If LONGTEMPS2>2
            Inc A
         End If 
         A$="Let A="+Str$(A)+";"
      End If 
   End If 
   Limit Bob 2,16,0 To 240,224
   Amal 2,A$
   Amal On 2
   GLOUGLOU2=0
End Proc
Procedure _MESSAGE[N,W]
   '
   ' Affiche les messages 
   '
   ' 1 = OH NO !
   ' 2 = GAME OVER
   ' 3 = GET READY ...
   ' 4 = THE WINNER IS ...
   ' 5 = EGALITE
   ' 6 = BONUS STAGE
   ' 7 = OPTIONS
   ' 8 = SCORES 
   '
   Shared LEVEL
   Bank Swap 1,2
   L=Len(Str$(LEVEL)-" ")
   If L=1
      L1=28
      L2=LEVEL+28
   Else 
      L1=(LEVEL/10)+28
      L2=(LEVEL mod 10)+28
   End If 
   Cls 0
   Palette 0,0,0,0
   Restore "M"+Str$(N)-" "
   Read NB
   For I=1 To NB
      Read X,Y,A
      If Y=0
         Y=80
      End If 
      If N=3
         If I=6 and A=29
            Add X,10
         End If 
      End If 
      Paste Bob X,Y,A
   Next I
   If N<7
      Fade 1,0,$FFF,$AAA,$555
      Wait 15
      If N<>3
         While Fire(1) or Fire(0)
            Multi Wait 
         Wend 
         Clear Key 
         Repeat 
            Multi Wait 
         Until Fire(1) or Fire(0) or Len(Inkey$)
         Fade 1
         Wait 15
         Cls 0
      End If 
   End If 
   Bank Swap 1,2
   '
   M1: Data 4,70,,15,107,,8,156,,14,190,,15
   M2: Data 8,30,,7,62,,1,98,,13,130,,5,170,,15,202,,22,244,,5,270,,18
   M3: Data 15,45,50,12,73,50,5,97,50,22,139,50,5,163,50,12,205,50,L1,235-(L2=29)*4,50,L2
   Data 35,100,7,69,100,5,93,100,20,137,100,18,165,100,5,187,100,1,225,100,4,250,100,25
   M4: Data 18,20,70,20,50,70,8,80,70,5,124,70,23,158,70,9,180,70,14,214,70,14,247,70,5,271,70,18
   Data 20,110,9,42,110,19,87,110,16,116,110,12,144,110,1,176,110,25,206,110,5
   Data 232,110,18,272-(W=1)*8,110,W+28
   M5: Data 7,54,77,27,80,,7,114,,1,154,,12,184,,9,206,,20,236,77,27
   M6: Data 10,78,60,2,108,60,15,145,60,14,180,60,21,211,60,19
   Data 82,110,19,112,110,20,136,110,1,174,110,7,210,110,5
   M7: Data 7,57,20,15,91,20,16,120,20,20,149,20,9,170,20,15,204,20,14,237,20,19
   M8: Data 6,70,26,19,98,26,3,131,26,15,164,26,18,192,26,5,216,26,19
End Proc
Procedure MUSIQUE[N,V]
   '
   ' Lance une musique
   '
   Shared ZIKPLAYING,QUELLEZIK
   Mvolume 0
   If(QUELLEZIK<>N) or(Length(3)=0)
      Erase 3
      Erase 5
      If N=1
         ' My First 
         Load "Twin:Zik-Intro.Abk",3
      Else 
         ' Magic Forest 
         Load "Twin:Zik-Jeu.Abk",3
      End If 
   End If 
   QUELLEZIK=N
   Led Off 
   Music 1
   Mvolume V
   Wait Vbl 
   ZIKPLAYING=True
End Proc
Procedure MUSIQUE_OFF
   '
   ' Arrête la musique en Fading
   '
   Shared ZIKVOL,ZIKPLAYING
   For I=ZIKVOL To 0 Step -1
      Mvolume I
      Wait Vbl 
   Next I
   Music Off 
   Led On 
   ZIKPLAYING=False
End Proc
Procedure UPDSCORE[JOUEUR,PLUS]
   '
   ' Met à jour les scores, s'occupe des affichages en bas de l'écran 
   '
   Shared SCORE1,SCORE2,MORT1,MORT2,PARALIZED1,PARALIZED2,NAME1$,NAME2$,HURRY
   '
   If JOUEUR=1
      Add SCORE1,PLUS
      SCORE1=Max(0,SCORE1)
   Else 
      Add SCORE2,PLUS
      SCORE2=Max(0,SCORE2)
   End If 
   '
   If HURRY=True
      Ink 8,0
      Text 4,242,"  PLUS VITE !  "
      Text 132,242,"  PLUS VITE !  "
   Else 
      Ink 11,0
      If MORT1=False
         If PARALIZED1=False
            Text 8,242,"SCORE"+Space$(8-Len(Str$(SCORE1)))+Str$(SCORE1)
         Else 
            Text 8,242," Oh my head ! "
         End If 
      Else 
         Text 8,242,"    OH NO !   "
      End If 
      If MORT2=False
         If PARALIZED2=False
            Text 136,242,"SCORE"+Space$(8-Len(Str$(SCORE2)))+Str$(SCORE2)
         Else 
            Text 136,242," Oh my head ! "
         End If 
      Else 
         Text 136,242,"    OH NO !   "
      End If 
   End If 
End Proc
Procedure CTR[Y,A$]
   '
   ' Utilisé par 'Options'
   '
   Text(320-Len(A$)*8)/2,Y,A$
End Proc
'
Procedure CHARGER[LEVEL]
   '
   Shared NBMONSTRES,MX(),MY(),NBSQ(),MA(),MH(),MV(),MT()
   Shared MEMETEMPS,MXTEMPS1,MXTEMPS2,MXTEMPSTOTAL,COM$
   '
   Paper 0
   Pen 1
   Locate 0,25
   L$=("Twin:Levels/L"+Str$(LEVEL))-" "
   If Exist(L$)
      Open In 1,L$
      B$=Input$(1,196)
      I=0
      For X=1 To 14
         For Y=1 To 14
            Inc I
            C2(X,Y)=Asc(Mid$(B$,I,1))
         Next Y
      Next X
      Input #1,B$
      Input #1,MXTEMPS1
      Input #1,MXTEMPS2
      Input #1,MEMETEMPS
      Input #1,MXTEMPSTOTAL
      Input #1,NBMONSTRES
      Input #1,COM$
      If NBMONSTRES>0
         For I=1 To NBMONSTRES
            Input #1,MX(I)
            Input #1,MY(I)
            Input #1,NBSQ(I)
            If NBSQ(I)>0
               For J=1 To NBSQ(I)
                  Input #1,MA(I,J)
                  Input #1,MH(I,J)
                  Input #1,MV(I,J)
                  Input #1,MT(I,J)
               Next J
            End If 
         Next I
      End If 
      Close 1
      Centre COM$
   Else 
      Centre "Plus de tableaux !"
      FIN
   End If 
End Proc
Procedure CHARGE_SCORES
   '
   ' Charge la table des scores 
   '
   Shared HISCORE$(),HISCORE()
   Open In 1,"Twin:High-Scores"
   For I=1 To 10
      Input #1,HISCORE$(I)
      Input #1,HISCORE(I)
   Next I
   Close 1
End Proc
Procedure SAUVE_SCORES
   '
   ' Sauve les scores 
   '
   Shared HISCORE$(),HISCORE()
   Open Out 1,"Twin:High-Scores"
   For I=1 To 10
      Print #1,HISCORE$(I)
      Print #1,HISCORE(I)
   Next I
   Close 1
End Proc
Procedure CHARGE_PREFS
   '
   ' Charge les préférences 
   '
   Shared ZIKON,ZIKVOL,SFXON,SFXVOL,PUBON,NAME1$,NAME2$,HISAVEON
   Open In 1,"Twin:Prefs"
   Input #1,ZIKON
   Input #1,ZIKVOL
   Input #1,SFXON
   Input #1,SFXVOL
   Input #1,PUBON
   Input #1,NAME1$
   Input #1,NAME2$
   Input #1,HISAVEON
   Close 1
End Proc
Procedure SAUVE_PREFS
   '
   ' Sauve les prefs
   '
   Shared ZIKON,ZIKVOL,SFXON,SFXVOL,PUBON,NAME1$,NAME2$,HISAVEON
   Open Out 1,"Twin:Prefs"
   Print #1,ZIKON
   Print #1,ZIKVOL
   Print #1,SFXON
   Print #1,SFXVOL
   Print #1,PUBON
   Print #1,NAME1$
   Print #1,NAME2$
   Print #1,HISAVEON
   Close 1
End Proc
Procedure BANK_LOAD[F$,BANK]
   '
   ' PowerMob DecryptFile adapté
   ' cf. AMOS_Save2:Utils/File_Coder.AMOS 
   '
   CLE$="Twin-Baston est le meilleur jeu du monde ! §è!('çà"
   '
   Open In 1,F$
   L=Lof(1)
   Close 1
   L2=Len(CLE$)
   '
   Reserve As Work 13,L
   Reserve As Work BANK,L
   P1=Start(13)
   P2=Start(BANK)
   P3=Varptr(CLE$)
   Bload F$,P1
   '
   For I=1 To L
      Poke P2,Peek(P3)-Peek(P1)
      Inc P1
      Inc P2
      Add P3,1,1 To L2
   Next I
   '
   Erase 13
   '
End Proc
'
Procedure PUB[N]
   '
   ' On se retrouve après ce 'A tout de suite'
   '
   S=Screen
   Screen Hide S
   Load "Twin:Img-Publogo.Abk",10
   Unpack 10 To 4
   Erase 10
   Wait 60
   F$=("Twin:Img-Pub"+Str$(N)+".Abk")-" "
   If Exist(F$)
      Load F$,10
   Else 
      Load "Twin:Img-Pub1.Abk",10
   End If 
   Unpack 10 To 4
   Erase 10
   Repeat 
      Multi Wait 
   Until Fire(1) or Fire(0)
   Fade 1
   Screen Close 4
   Screen S
   Screen Show S
   Wait 15
End Proc
Procedure JEU[N]
   '
   ' Renvoie vers le jeu qu'il faut...
   '
   N=N mod 2
   If N=1
      JEU1
   End If 
   If N=0
      JEU2
   End If 
End Proc
Procedure JEU1
   '
   ' Tron light cycles
   '
   Shared MSG_EGALITE,MSG_WINNER,SCORE1,SCORE2
   '
   Palette 0,$F,$F00,$FFF
   Paper 0
   Pen 3
   Cls 0
   Centre At(,0)+"EVITEZ LES COLLISIONS !"
   Centre At(,2)+"BONUS 10.000 Pts"
   Autoback 1
   Bob Update Off 
   '
   Ink 3,0
   Box 0,32 To 319,255
   '
   X1=32
   Y1=64
   S1=8
   P1=0
   '
   X2=280
   Y2=200
   S2=4
   P2=0
   '
   Repeat 
      '
      '--------- Joueur 1 ---------
      '
      If Jup(1) and S1<>2
         S1=1
      End If 
      If Jdown(1) and S1<>1
         S1=2
      End If 
      If Jleft(1) and S1<>8
         S1=4
      End If 
      If Jright(1) and S1<>4
         S1=8
      End If 
      '
      If S1=1
         Dec Y1
      End If 
      If S1=2
         Inc Y1
      End If 
      If S1=4
         Dec X1
      End If 
      If S1=8
         Inc X1
      End If 
      '
      If Point(X1,Y1)>0
         FIN=True
         P1=True
      End If 
      '
      '-------- Joueur 2 ----------
      '
      If Jup(0) and S2<>2
         S2=1
      End If 
      If Jdown(0) and S2<>1
         S2=2
      End If 
      If Jleft(0) and S2<>8
         S2=4
      End If 
      If Jright(0) and S2<>4
         S2=8
      End If 
      '
      If S2=1
         Dec Y2
      End If 
      If S2=2
         Inc Y2
      End If 
      If S2=4
         Dec X2
      End If 
      If S2=8
         Inc X2
      End If 
      '
      If Point(X2,Y2)>0
         FIN=True
         P2=True
      End If 
      '
      '-------- Affichage ---------
      '
      Plot X1,Y1,1
      Plot X2,Y2,2
      '
      Wait Vbl 
      '
   Until FIN
   '
   Bob Update On 
   Fade 1
   Wait 15
   Cls 0
   '
   If(P1=True) and(P2=True)
      _MESSAGE[MSG_EGALITE,0]
      Add SCORE1,5000
      Add SCORE2,5000
   Else 
      If P2=True
         _MESSAGE[MSG_WINNER,1]
         Add SCORE1,10000
      Else 
         _MESSAGE[MSG_WINNER,2]
         Add SCORE2,10000
      End If 
   End If 
   '
End Proc
Procedure JEU2
   '
   ' Tableau bonus: secouer son joystick
   '
   Shared MSG_EGALITE,MSG_WINNER
   '
   Palette 0,$F,$F00,$FFF
   Paper 0
   Pen 3
   '
   Autoback 1
   Centre At(,3)+"SECOUEZ-MOI DE GAUCHE A DROITE !"
   Centre At(,5)+"BONUS 10.000 Pts"
   Ink 3,0
   Bar 160,96 To 161,130
   '
   T=499
   J1=8
   J2=8
   G=0
   '
   Repeat 
      J=Joy(1)
      If((J=4) and(J1=8)) or((J=8) and(J1=4))
         J1=J
         G=Min(128,G+2)
      End If 
      J=Joy(0)
      If((J=4) and(J2=8)) or((J=8) and(J2=4))
         J2=J
         G=Max(-128,G-2)
      End If 
      Ink 1,0
      Bar 30,100 To 160+G,126
      Ink 2,0
      Bar 160+G+1,100 To 290,126
      Ink 3,0
      Text 120,180,"TEMPS ="+Str$(T/50+1)+" "
      Wait Vbl 
      Dec T
   Until T=0
   '
   Fade 1
   Wait 16
   Cls 0
   '
   If G=0
      _MESSAGE[MSG_EGALITE,0]
      Add SCORE1,5000
      Add SCORE2,5000
   Else 
      If G>0
         _MESSAGE[MSG_WINNER,1]
         Add SCORE1,10000
      Else 
         _MESSAGE[MSG_WINNER,2]
         Add SCORE2,10000
      End If 
   End If 
   '
End Proc
Procedure DESSINER
   S=Screen
   Screen 0
   Cls 0,16,0 To 240,224
   For X=1 To 14
      For Y=1 To 14
         If C(X,Y)>0
            Paste Bob X*16,(Y-1)*16,C(X,Y)
         End If 
      Next Y
   Next X
   Screen S
End Proc
Procedure FOUTRE_MONSTRES
   Shared NBMONSTRES,NBSQ(),MX(),MY(),MA(),MH(),MV(),MT(),SQ$()
   If NBMONSTRES>0
      For M=5 To NBMONSTRES+4
         I=M-4
         Bob M,MX(I),MY(I),92
         Channel M To Bob M
         A$="L:"
         For J=1 To NBSQ(I)
            A$=A$+SQ$(MA(I,J))
            A$=A$+"M"+Str$(MH(I,J))+","+Str$(MV(I,J))+","+Str$(MT(I,J))+";"
         Next J
         A$=A$-" "
         Amal M,A$+"JL;"
      Next M
   End If 
End Proc
'
'
DT:
Data "A0,(96,4)(97,4)(98,4)(99,4);","A0,(92,4)(93,4)(94,4)(95,4);"
'
Data "LA=100;","LA=105;","A1,(105,2)(104,2)(103,2)(102,2)(101,2)(100,2);FR0=1T13;NR0;"
Data "A1,(100,2)(101,2)(102,2)(103,2)(104,2)(105,2);FR0=1T13;NR0;"
'
Data "A0,(110,8)(111,8);","A0,(106,8)(107,8);"
'
Data "A0,(115,4)(116,4)(117,4);","A0,(112,4)(113,4)(114,4);"
'
Data "A0,(121,4)(122,4)(123,4);","A0,(118,4)(119,4)(120,4);"
'
Data "A0,(128,8)(129,8);","A0,(124,8)(125,8);"
'
Data "LA=139;","LA=130;","A1,(139,2)(138,2)(137,2)(136,2)(135,2)(134,2)(133,2)(132,2)(131,2)(130,2);FR0=1T21;NR0;"
Data "A1,(130,2)(131,2)(132,2)(133,2)(134,2)(135,2)(136,2)(137,2)(138,2)(139,2);FR0=1T21;NR0;"
'
Data "LA=141;","LA=140;"
