'
'                      ------------------- 
'                     |    Twin Baston    |
'                      ------------------- 
'
'                  ©1992  PowerMob Productions 
'
'
'                           CREDITS  
'                          ~~~~~~~~~ 
'           Coding by :  Ronan Amicel  ( Mr Reed ) 
'         Graphics by :  David Thévenot ( Fainéant! )
'            Music by :  Eric Hilpipre  ( Vuzz ) 
'
'
'********************** Le Programme ************************
'
'  NOM           :  TWIN BASTON
'  VERSION       :  0.91 ( No more Amal )
'  AUTEUR        :  RONAN AMICEL alias MR REED 
'  DERNIERE MOD. :  26.08.92 
'  DESCRIPTION   :  Jeu de Plates-formes à 2 Joueurs.
'
'************************************************************
'
' A Faire:   - Que les collisions Joueur/Briques soient parfaites
' ~~~~~~~~   - Jeux inter-tableaux 
'            - Cryptage des données
'            - Samples 
'
'************************************************************
'
'
' D'abord des inits, variables, etc..
'
Dim C(14,14)
Dim HISCORE(10)
Dim HISCORE$(10)
Dim MN(5),MX(5),MY(5),MV(5)
Global C()
V$="0.91"
Dim ENNEMI(5),TRESOR(3)
Dim DANSLESPOMMES(5)
'
JOUEUR1=1
JOUEUR2=2
TIR1=3
TIR2=4
ENNEMI(1)=5
ENNEMI(2)=6
ENNEMI(3)=7
ENNEMI(4)=8
ENNEMI(5)=9
TRESOR(1)=10
TRESOR(2)=11
TRESOR(3)=12
BONUS=13
'
' Puis on lance le chargement/l'intro
'
Proc INIT
'
' On remet un zoli écran comme y faut... 
'
Auto View Off 
Wait Vbl 
Screen Open 0,320,256,32,Lowres
Screen Display 0,128,40,320,256
Curs Off 
Flash Off 
Hide On 
Cls 0
Double Buffer 
Auto View On 
Wait Vbl 
Make Mask 
'
'**************************************
'
'         Boucle principale
'
'**************************************
'
Do 
   '
   ' L'intro en boucle, puis le choix des options 
   '
   Autoback 2
   Proc PRESENTATION
   Proc OPTIONS
   '
   ' Début de la partie, initialisations
   '
   Load "Twin:Img-Back.Abk",9
   CREDITS=3
   LEVEL=1
   SCORE1=0
   SCORE2=0
   VICT1=0
   VICT2=0
   For I=1 To 5
      DANSLESPOMMES(I)=0
   Next I
   '
   ' De la musique ?
   '
   If ZIKON
      Proc MUSIQUE[2,0]
   End If 
   Load "Twin:Samples-Jeu.Abk",5
   '
   ' Go! On y va !
   '
   Proc _GETREADY
   Proc CHARGER[LEVEL]
   Timer=0
   Repeat 
      Multi Wait 
   Until Fire(1) or Fire(0) or Timer>=400
   Fade 1
   Wait 15
   Bob Off 
   Cls 0
   '
   '****************************************
   '
   '    Boucle de jeu (tant que crédits)
   '
   '****************************************
   '
   Repeat 
      '
      ' Initialise pour le tableau 
      '
      PASSE=False
      '
      MORT1=False
      RAMENE1=0
      CARRY1=0
      PARALIZED1=False
      STATE1=0
      EMPJP1=0
      '
      MORT2=False
      RAMENE2=0
      CARRY2=0
      PARALIZED2=False
      STATE2=0
      '
      Proc INIT_TABLEAU
      '
      Every 300 Proc BONUS
      Every On 
      '
      Wait Vbl 
      For I=1 To NBMONSTRES+4
         Amal On I
      Next I
      '
      Synchro Off 
      Timer=0
      '
      '************************************
      '
      '          The Big Boucle
      '
      '************************************
      Repeat 
         '
         '- - - - - JOUEUR 1 - - - - - -
         '
         If MORT1=False
            '
            ' A éxécuter si on n'est pas dans les pommes 
            '
            If PARALIZED1>0
               '
               ' Si on est paralizé 
               '
               Inc PARALIZED1
               If PARALIZED1=200
                  PARALIZED1=0
                  Amal Freeze 10
                  Bob JOUEUR1,,,PARIMG1
                  Amal On 1
                  UPDSCORE[0,0]
               End If 
               '
               ' Sinon
               '
            Else 
               '
               ' Où es-tu ? 
               '
               X1=X Bob(1)
               Y1=Y Bob(1)
               I1=I Bob(1)
               X=X1/16
               Y=Y1/16
               '
               ' Que fais-tu ?
               '
               If STATE1=0
                  '
                  ' ooooooooooooooooo Je me promène dans la forêt !  
                  '
                  '
                  ' Test Collision : On Tombe ?
                  '
                  If Y1<223
                     If((C(X,Y+2)<>1) and(C((X1+15)/16,Y+2)<>1)) or(Y1<10)
                        STATE1=2
                        FALL=0
                     End If 
                  End If 
                  If STATE1=0
                     '
                     ' Cà bouge ? 
                     '
                     If Joy(1)>0
                        '
                        ' On saute 
                        '
                        If Jup(1)
                           If EMPJP1=0
                              STATE1=1
                              JUMP=0
                              EMPJP1=1
                           End If 
                        Else 
                           EMPJP1=0
                           '
                           ' Gauche 
                           '
                           If Jleft(1)
                              I1=3
                              If X1>16
                                 If X1 mod 16=0
                                    If C(X-1,Y+1)<>1
                                       Add X1,-2
                                    End If 
                                 Else 
                                    Add X1,-2
                                 End If 
                              End If 
                           Else 
                              '
                              ' Droite 
                              '
                              If Jright(1)
                                 I1=2
                                 If X1<224
                                    If C(X+1,Y+1)<>1
                                       Add X1,2
                                    End If 
                                 End If 
                              End If 
                           End If 
                        End If 
                     Else 
                        EMPJP1=0
                     End If 
                  End If 
               Else 
                  If STATE1=1
                     '
                     ' ooooooooooooo Are ya Jumpin' ? 
                     '
                     Inc JUMP
                     '
                     ' Au cours du saut.
                     '
                     If JUMP<17
                        Add Y1,-2
                        '
                        ' Dep. gauche
                        '
                        If Jleft(1)
                           I1=3
                           If X1>16
                              If X1 mod 16=0
                                 If C(X-1,(Y1/16)+1)<>1
                                    Add X1,-2
                                 End If 
                              Else 
                                 Add X1,-2
                              End If 
                           End If 
                        Else 
                           '
                           ' Dep droite 
                           '
                           If Jright(1)
                              I1=2
                              If X1<224
                                 If C(X+1,(Y1/16)+1)<>1
                                    Add X1,2
                                 End If 
                              End If 
                           End If 
                        End If 
                     Else 
                        '
                        ' En haut du saut, doit-on retomber ?
                        '
                        If Y1<10
                           STATE1=2
                           FALL=0
                        Else 
                           If(C(X,Y+1)=1) or(C((X1+15)/16,Y+1)=1)
                              STATE1=2
                              FALL=0
                           Else 
                              If(C(X,Y+2)=1) or(C((X1+15)/16,Y+2)=1)
                                 STATE1=0
                              Else 
                                 STATE1=2
                                 FALL=0
                              End If 
                           End If 
                        End If 
                     End If 
                  Else 
                     '
                     ' oooooooooooooooo No, I'm Fallin' 
                     '
                     Inc FALL
                     '
                     ' Au cours de la descente
                     '
                     If FALL<9
                        If FALL>1
                           '
                           ' Dep gauche 
                           '
                           If Jleft(1)
                              I1=3
                              If X1>16
                                 If X1 mod 16=0
                                    If C(X-1,Y+1)<>1
                                       Add X1,-2
                                    End If 
                                 Else 
                                    Add X1,-2
                                 End If 
                              End If 
                           Else 
                              '
                              ' Droite 
                              '
                              If Jright(1)
                                 I1=2
                                 If X1<224
                                    If C(X+1,Y+1)<>1
                                       Add X1,2
                                    End If 
                                 End If 
                              End If 
                           End If 
                        End If 
                        '
                        Add Y1,2
                        '
                     Else 
                        '
                        ' Redescendu d'une case: continuer à tomber ?
                        '
                        If Y1=223
                           STATE1=0
                        Else 
                           If(C(X,Y+2)=1) or(C((X1+15)/16,Y+2)=1)
                              STATE1=0
                           Else 
                              STATE1=2
                              FALL=0
                           End If 
                        End If 
                     End If 
                  End If 
               End If 
               '
               ' Poum ! 
               '
               If Fire(1)
                  If Amreg(0)=0
                     Amreg(0)=1
                     Amreg(2)=X1
                     Amreg(3)=Y1
                     Amreg(4)=I1
                  End If 
               End If 
               '
               ' On met donc le Bob là où il faut...
               '
               Bob 1,X1,Y1,I1
               '
               '++++++++++++++++++++++++++++++++++++
               '
               '
               ' On prend un Bonus ?
               '
               If Bob Col(JOUEUR1,BONUS To BONUS)
                  Proc EFFET_BONUS1
                  Proc BONUS
               End If 
               '
               ' Un trésor ?
               '
               If Bob Col(JOUEUR1,TRESOR(1) To TRESOR(NBTRESORS))
                  If CARRY1=0
                     For I=1 To NBTRESORS
                        If Col(TRESOR(I))
                           CARRY1=I
                        End If 
                     Next I
                     Bob Off TRESOR(CARRY1)
                     Proc UPDSCORE[100,0]
                     Sam Play %1,2
                  End If 
               End If 
               '
               ' On est en bas à gauche ? 
               '
               If X1=16 and Y1=223
                  '
                  ' Ramène un trésor 
                  '
                  If CARRY1>0
                     CARRY1=0
                     Inc RAMENE1
                     Proc UPDSCORE[2000,0]
                     If RAMENE1+RAMENE2=NBTRESORS
                        PASSE=True
                     End If 
                     Sam Play %1,10
                  End If 
                  '
                  ' Remonter le temps
                  '
                  If Jdown(1)
                     Inc TEMPS1
                     TEMPS1=Min(TEMPS1,MXTEMPS1)
                     Ink 4
                     TP=208-TEMPS1
                     Draw 1,TP+1 To 14,TP+1
                     Draw 1,TP To 14,TP
                  End If 
               End If 
            End If 
            '
            ' --- Ceci doit toujours être exécuté ---
            '
            '
            ' On tire sur un monstre 
            '
            If Bob Col(TIR1,ENNEMI(1) To ENNEMI(NBMONSTRES))
               For I=ENNEMI(1) To ENNEMI(5)
                  Exit If Col(I)
               Next I
               If DANSLESPOMMES(I-4)=False
                  DANSLESPOMMES(I-4)=1
                  Amal Freeze I
                  Bob TIR1,,320,
                  Shoot 
                  Proc UPDSCORE[10,0]
               End If 
            End If 
            '
            ' On tire sur l'autre !
            '
            If Bob Col(TIR1,JOUEUR2 To JOUEUR2) and PARALIZED2=False
               PARALIZED2=1
               PARIMG2=I Bob(JOUEUR2)
               Amal Freeze 2
               Amal On 11
               Bob TIR1,,320,
               Sam Play %10,1,12000
               Proc UPDSCORE[50,0]
            End If 
            '
            ' Collision avec monstre, aïe !
            '
            If Bob Col(JOUEUR1,ENNEMI(1) To ENNEMI(NBMONSTRES))
               MORT1=True
               Boom 
               Amal 1,"Let A=59; P; P; P; P; P; Let Y=300; P; End"
               Amal On 1
               Bob Off TIR1
               If CARRY1<>0
                  Bob TRESOR(CARRY1),X1,Y1,14
               End If 
               UPDSCORE[0,0]
            End If 
            '
            ' Plus de temps !
            '
            If TEMPS1=0
               MORT1=True
               Sam Play %1,4
               Amal 1,"Let A=59; P; P; P; P; P; Let Y=300; P; End"
               Amal On 1
               Bob Off TIR1
               If CARRY1<>0
                  Bob TRESOR(CARRY1),X1,Y1,14
               End If 
               UPDSCORE[0,0]
            End If 
         End If 
         '
         ' - - - - - - JOUEUR 2 - - - - - - - - - 
         '
         If MORT2=False
            If PARALIZED2=False
               Y2=Y Bob(JOUEUR2)
               X2=X Bob(JOUEUR2)
               If Y2>=208
                  Amreg(2,0)=1
                  Amreg(2,1)=1
               Else 
                  X=X2/16
                  Y=Y2/16
                  Add Y,2
                  Amreg(2,0)=C(X,Y)
                  If X<14
                     X=(X2+14)/16
                     Amreg(2,1)=C(X,Y)
                  Else 
                     Amreg(2,1)=0
                  End If 
               End If 
               If Y2<0
                  Amreg(2,0)=0
                  Amreg(2,1)=0
               End If 
               X=X2/16
               Y=Y2/16
               Inc Y
               Amreg(2,6)=C(X,Y)
               Amreg(2,7)=C((X2+14)/16,Y)
               If X=1
                  Amreg(2,2)=0
               Else 
                  Dec X
                  Amreg(2,2)=C(X,Y)
                  Inc X
               End If 
               If X<14
                  Inc X
                  Amreg(2,3)=C(X,Y)
               End If 
               '
               If Bob Col(JOUEUR2,BONUS To BONUS)
                  Proc EFFET_BONUS2
                  Proc BONUS
               End If 
               If Bob Col(JOUEUR2,TRESOR(1) To TRESOR(NBTRESORS))
                  If CARRY2=0
                     For I=1 To NBTRESORS
                        If Col(TRESOR(I))
                           CARRY2=I
                        End If 
                     Next I
                     Bob Off TRESOR(CARRY2)
                     Proc UPDSCORE[0,100]
                     Sam Play %10,2
                  End If 
               End If 
               If(X2=224) and(Y2=223)
                  If CARRY2>0
                     CARRY2=0
                     Inc RAMENE2
                     If RAMENE1+RAMENE2=NBTRESORS
                        PASSE=True
                     End If 
                     Proc UPDSCORE[0,2000]
                     Sam Play %10,10
                  End If 
                  If Jdown(0)
                     Inc TEMPS2
                     TEMPS2=Min(TEMPS2,MXTEMPS2)
                     Ink 4
                     TP=208-TEMPS2
                     Draw 241,TP+1 To 254,TP+1
                     Draw 241,TP To 254,TP
                  End If 
               End If 
            Else 
               Inc PARALIZED2
               If PARALIZED2=200
                  PARALIZED2=0
                  Amal Freeze 11
                  Bob JOUEUR2,,,PARIMG2
                  Amal On 2
                  UPDSCORE[0,0]
               End If 
            End If 
            If Bob Col(TIR2,ENNEMI(1) To ENNEMI(NBMONSTRES))
               For I=ENNEMI(1) To ENNEMI(NBMONSTRES)
                  Exit If Col(I)
               Next I
               If DANSLESPOMMES(I-4)=False
                  DANSLESPOMMES(I-4)=1
                  Amal Freeze I
                  Bob TIR2,,320,
                  Shoot 
                  Proc UPDSCORE[0,10]
               End If 
            End If 
            If Bob Col(TIR2,JOUEUR1 To JOUEUR1) and(PARALIZED1=False)
               PARALIZED1=1
               Amal Freeze 1
               PARIMG1=I Bob(JOUEUR1)
               Amal On 10
               Bob TIR2,,310,
               Sam Play %1,1,6000
               Proc UPDSCORE[0,50]
            End If 
            If Bob Col(JOUEUR2,ENNEMI(1) To ENNEMI(NBMONSTRES))
               MORT2=True
               Boom 
               Amal 2,"Let A=59; P; P; P; P; P; Let Y=300; P; End"
               Amal On 2
               Bob Off TIR2
               If CARRY2>0
                  Bob TRESOR(CARRY2),X2,Y2,14
               End If 
               UPDSCORE[0,0]
            End If 
            ' gestion du temps 
            If TEMPS2<1
               MORT2=True
               Sam Play %10,4
               Amal 2,"Let A=59; P; P; P; P; P; Let Y=300; P; End"
               Amal On 2
               Bob Off TIR2
               If CARRY2<>0
                  Bob TRESOR(CARRY2),X2,Y2,14
               End If 
               UPDSCORE[0,0]
            End If 
         End If 
         '
         '================== Diverses gestions ==================== 
         '
         ' Le temps descend (sauf en trainer) 
         '
         If CHEAT=False
            If Timer>12
               Ink 0
               If MORT1=False
                  Dec TEMPS1
                  Draw 1,208-TEMPS1 To 14,208-TEMPS1
                  Draw 1,207-TEMPS1 To 14,207-TEMPS1
               End If 
               If MORT2=False
                  Dec TEMPS2
                  Draw 241,208-TEMPS2 To 254,208-TEMPS2
                  Draw 241,207-TEMPS2 To 254,207-TEMPS2
               End If 
               Timer=0
            End If 
         End If 
         '
         ' Ennemis paralizés ?
         '
         For I=ENNEMI(1) To ENNEMI(NBMONSTRES)
            If DANSLESPOMMES(I-4)=200
               DANSLESPOMMES(I-4)=0
               Amal On I
            End If 
            If DANSLESPOMMES(I-4)<>0
               Inc DANSLESPOMMES(I-4)
            End If 
         Next I
         '
         ' Le clavier...
         '
         K$=Inkey$
         If K$<>""
            K$=Upper$(K$)
            ' Esc
            If Asc(K$)=27
               If Key Shift<>0
                  Proc FIN
               Else 
                  MORT1=True
                  MORT2=True
                  CREDITS=1
               End If 
            End If 
            ' Pause
            If K$="P"
               Every Off 
               Amal Freeze 
               Repeat 
                  Multi Wait 
               Until Upper$(Inkey$)="P"
               For I=1 To 4
                  Amal On I
               Next I
               For I=5 To NBMONSTRES+4
                  If DANSLESPOMMES(I-4)=0
                     Amal On I
                  End If 
               Next I
               Every On 
            End If 
            ' Mémoire
            If K$="M"
               Print At(3,1);"Fast:";Fast Free;" / Chip:";Chip Free;"  "
            End If 
            ' Cheat On/Off 
            If K$="§"
               CHEAT= Not(CHEAT)
            End If 
            ' Crédits +++
            If K$="C"
               If CHEAT=True
                  Inc CREDITS
                  L$=Str$(CREDITS)-" "
                  If Len(L$)=1
                     L$="0"+L$
                  End If 
                  Ink 18,0
                  Text 280,218,L$
               End If 
            End If 
            ' NeXT 
            If K$="+"
               If CHEAT=True
                  PASSE=True
               End If 
            End If 
         End If 
         '
         Synchro 
         Wait Vbl 
         '
      Until((MORT1=True) and(MORT2=True)) or(PASSE=True)
      '
      '**********************************************
      '
      Every Off 
      Wait 6
      Amal Off 
      MUSIQUE_OFF
      Wait 4
      Fade 2
      Wait 30
      Wait Vbl 
      Bob Off 
      Wait Vbl 
      '
      ' On a gagné ! 
      '
      If PASSE
         Sam Play %1111,5
         If RAMENE1>RAMENE2
            Inc VICT1
            Proc WINNER[1]
         Else 
            If RAMENE2>RAMENE1
               Inc VICT2
               Proc WINNER[2]
            Else 
               Proc EGALITE
            End If 
         End If 
         If PUBON
            Proc PUB[LEVEL]
         End If 
         Inc LEVEL
         _GETREADY
         CHARGER[LEVEL]
         Timer=0
         Repeat 
            Multi Wait 
         Until Fire(1) or Fire(0) or Timer>=400
         Fade 1
         Wait 15
         Cls 0
      Else 
         '
         ' On a perdu ! 
         '
         Dec CREDITS
         If CREDITS>0
            _OH_NO
         End If 
      End If 
   Until CREDITS=0
   '
   '************************************
   '
   ' Fin de la partie 
   '
   Proc _GAME_OVER
   '
   ' Bon score ?
   '
   For I=1 To 10
      If SCORE1>HISCORE(I)
         HISCORE(I)=SCORE1
         HISCORE$(I)=NAME1$
         Exit 
      End If 
   Next I
   For I=1 To 10
      If SCORE2>HISCORE(I)
         HISCORE(I)=SCORE2
         HISCORE$(I)=NAME2$
      End If 
   Next I
   '
   ' Affiche les scores 
   '
   Cls 0
   Erase 3
   Erase 5
   HALL_OF_FAME
   Fade 1,0,$FFF
   Wait 15
   While Fire(1) or Fire(0)
      Multi Wait 
   Wend 
   Repeat 
      Multi Wait 
   Until Fire(1) or Fire(0)
   Fade 1
   '
   ' Et c'est reparti pour un tour !
   '
Loop 
'
'***************** PROCEDURES **************** 
'
'
Procedure INIT
   '
   ' Charge les données principales + affiche imgs + lance zik
   ' pour faire patienter...
   '
   ' Banques:  1 = sprites
   ' ~~~~~~~~  2 = icones 
   '           3 = musique
   '           5 = samples
   '           6 = img fonte ( credits scroll ) 
   '           9 = image de fond / Img intro / Img prés.
   '          10 = img prés. / Imgs Pubs / Img Copyright
   '
   Shared ZIKPLAYING,QUELLEZIK,ZIKVOL
   '
   ' Met tout propre
   '
   Close Editor 
   Close Workbench 
   While Screen>=0
      Screen Close Screen
   Wend 
   For I=1 To 16
      Erase I
   Next I
   '
   Colour Back 0
   Auto View Off 
   Hide On 
   Proc _COPYRIGHT
   Auto View On 
   '
   CHARGE_PREFS
   Load "Twin:Zik-Intro.Abk",3
   Load "Twin:Img-Intro.Abk",9
   '   Load "Twin:Samples-First.Abk",5
   '   Sam Play 1 
   '   Wait 166 
   '   Sam Play 2 
   '   Wait 166 
   '   Sam Play 3 
   '   Wait 114 
   '   Sam Play 4 
   Unpack 9 To 0
   Wait Vbl 
   Erase 9
   '   Wait 100 
   '   Erase 5
   Wait 2
   Voice 0
   Mvolume 0
   Voice 15
   Mvolume ZIKVOL
   Music 1
   ZIKPLAYING=True
   QUELLEZIK=1
   Load "Twin:Sprites.Abk"
   Load "Twin:Icons.Abk",2
   CHARGE_SCORES
   Priority Reverse On 
End Proc
Procedure PRESENTATION
   '
   ' Affiche en boucle des imgs avec zik, attend Fire.
   '
   Shared ZIKVOL,ZIKPLAYING
   Screen 0
   Cls 0
   Screen To Front 0
   Load "Twin:Img-Intro2.abk",9
   Load "Twin:Img-Intro3.abk",10
   If ZIKPLAYING=False
      Proc MUSIQUE[1,ZIKVOL]
   End If 
   Wait Vbl 
   HALL=False
   Do 
      '
      ' Img Twin Baston
      '
      Auto View Off 
      Wait Vbl 
      Unpack 9 To 2
      Screen Display 2,128,40,,
      Palette 0,0
      Auto View On 
      Wait Vbl 
      Fade 1,$0,$FFF
      Timer=0
      Repeat 
         Exit If Fire(1) or Fire(0),2
         If(Inkey$=Chr$(27)) and(Key Shift<>0)
            FIN
         End If 
         Multi Wait 
      Until Timer>=250
      Fade 1
      Wait 15
      '
      ' Img PowerMob 
      '
      Auto View Off 
      Wait Vbl 
      Unpack 10 To 2
      Screen Display 2,128,40,,
      Palette 0,0
      Auto View On 
      Wait Vbl 
      Fade 1,$0,$FFF
      Timer=0
      Repeat 
         Exit If Fire(1) or Fire(0),2
         If(Inkey$=Chr$(27)) and(Key Shift<>0)
            FIN
         End If 
         Multi Wait 
      Until Timer>=250
      Fade 1
      Wait 15
      '
      ' Img Scores 
      '
      Screen 0
      Screen To Front 0
      If HALL=False
         Proc HALL_OF_FAME
         HALL=True
      End If 
      Fade 1,0,$FFF
      Timer=0
      Repeat 
         Exit If Fire(1) or Fire(0),2
         If(Inkey$=Chr$(27)) and(Key Shift<>0)
            FIN
         End If 
         Multi Wait 
      Until Timer>=250
      Fade 1
      Wait 15
   Loop 
   '
   Repeat 
      Multi Wait 
   Until Fire(1)=0 and Fire(0)=0
   Fade 1
   Wait 15
   Screen Hide 0
   Screen Hide 2
   Screen Close 2
   Screen 0
   Screen To Front 0
   Flash Off 
   Cls 0
   Screen Show 0
   Proc MUSIQUE_OFF
   Erase 9
   Erase 10
   Erase 3
End Proc
Procedure INIT_TABLEAU
   '
   ' Prépare tout pour le tableau à venir 
   '
   Shared LEVEL,TEMPS1,TEMPS2,ZIKON,ZIKVOL,V$,MORT1,MORT2
   Shared ENNEMI(),NBMONSTRES,JOUEUR1,JOUEUR2,TIR1,TIR2,TRESOR()
   Shared MX(),MY(),MN(),MV(),NBTRESORS,MXTEMPS1,MXTEMPS2
   '
   ' *** Ecran
   '
   Auto View Off 
   Screen Open 0,320,256,32,Lowres
   Screen Hide 0
   Curs Off 
   Flash Off 
   Hide On 
   Wait Vbl 
   '
   Cls 0
   Unpack 9 To 1
   Screen 0
   Get Palette 1
   Wait Vbl 
   Screen Copy 1 To 0
   Wait Vbl 
   Screen Close 1
   Screen Display 0,128,40,320,256
   Proc REFOUS_PALETTE
   Cls 0,260,176 To 310,250
   Ink 19,0
   Text 268,185,"LEVEL"
   Text 260,208,"CREDITS"
   Text 260,231,"OVERALL"
   Ink 18,0
   L$=Str$(LEVEL)-" "
   If Len(L$)=1
      L$="0"+L$
   End If 
   Text 280,195,L$
   L$=Str$(CREDITS)-" "
   If Len(L$)=1
      L$="0"+L$
   End If 
   Text 280,218,L$
   L$=(Str$(VICT1)-" ")+" /"+Str$(VICT2)
   If Len(Str$(VICT1))+Len(Str$(VICT2))=4
      L$=" "+L$
   End If 
   Text 260,241,L$
   '
   Ink 4
   TEMPS1=MXTEMPS1
   TEMPS2=MXTEMPS2
   Bar 1,208-TEMPS1 To 14,208
   Bar 241,208-TEMPS2 To 254,208
   '
   Proc UPDSCORE[0,0]
   '
   Double Buffer 
   Autoback 1
   '
   NBTRESORS=0
   For X=1 To 14
      For Y=1 To 14
         If C(X,Y)=1
            Paste Bob X*16,Y*16-16,15
         Else 
            If(C(X,Y)=2) and(NBTRESORS<4)
               Inc NBTRESORS
               Bob TRESOR(NBTRESORS),X*16,Y*16-1,14
            End If 
         End If 
      Next Y
   Next X
   '
   ' ***  Bobs
   '
   Bob JOUEUR1,16,223,2
   Channel 1 To Bob JOUEUR1
   Channel 10 To Bob JOUEUR1
   '
   Bob JOUEUR2,224,223,9
   Channel 2 To Bob JOUEUR2
   Channel 11 To Bob JOUEUR2
   '
   Bob TIR1,10,320,18
   Channel 3 To Bob TIR1
   '
   Bob TIR2,30,320,17
   Channel 4 To Bob TIR2
   '
   Limit Bob 16,0 To 240,224
   '
   ' ***  AMAL
   '
   '     Tir 1
   '
   C$="   Loop:"
   C$=C$+"   If RA=0 then Jump Loop;"
   C$=C$+"   Let X=RC; Let Y=RD;"
   C$=C$+"   If RE=2 then Jump M;"
   C$=C$+"   Let A=17;"
   C$=C$+"   Move -48,0,12;"
   C$=C$+"   Jump N;"
   C$=C$+"M: Let A=18"
   C$=C$+"   Move 48,0,12;"
   C$=C$+"N: Let Y=310;"
   C$=C$+"   Let RA=0"
   C$=C$+"   Jump Loop;"
   '
   '    Joueur 2
   '
   B$="      r zero case en dessous gauche ;"
   B$=B$+"   r un case en dessous droite ;"
   B$=B$+"   r deux case a gauche ;"
   B$=B$+"   r trois case a droite ;"
   B$=B$+"   r quatre et r cinq empjump ;"
   B$=B$+"   Let R0=1 ;"
   B$=B$+"AUtotest("
   B$=B$+"   If R4=0 then Jump Gauche ;"
   B$=B$+"   If J0&1<>0 then Jump Gauche ;"
   B$=B$+"   Let R4=0 ;"
   B$=B$+"Gauche:"
   B$=B$+"   If J0&4=0 then Jump Droite ;"
   B$=B$+"   If X=16 then Jump Droite ;"
   B$=B$+"   Let R5=X/16*16=X ;"
   B$=B$+"   If R2=1&R5 then Jump Droite ;"
   B$=B$+"   Let X=X-1 ; Let A=9 ;"
   B$=B$+"Droite:"
   B$=B$+"   If J0&8=0 then Jump Fire ;"
   B$=B$+"   If R3=1 then Jump Fire ;"
   B$=B$+"   If X=224 then Jump Fire ;"
   B$=B$+"   Let X=X+1 ; Let A=8 ;"
   B$=B$+"Fire:"
   B$=B$+"   If J0&16=0 then eXit;"
   B$=B$+"   If RA=1 then eXit;"
   B$=B$+"   Let RF=1; Let RG=1;"
   B$=B$+"   Let RH=X; Let RI=Y;"
   B$=B$+"   Let RJ=A;"
   B$=B$+"   eXit; )"
   B$=B$+"Sauter:"
   B$=B$+"   If R6=1 then Jump Xomber;"
   B$=B$+"   If R7=1 then Jump Xomber;"
   B$=B$+"   If R4=1 then Jump Tomber ;"
   B$=B$+"   If J0&1=0 then Jump Tomber ;"
   B$=B$+"   Let R4=1 ;"
   B$=B$+"   Move 0,-32,16 ; Pause; Pause;"
   B$=B$+"Tomber:"
   B$=B$+"   If R0=1 then Jump Sauter ;"
   B$=B$+"   If R1=1 then Jump Sauter ;"
   B$=B$+"X: If Y=223 then Jump Sauter;"
   B$=B$+"   Move 0,16,8 ;"
   B$=B$+"   Jump Tomber ;"
   '
   '     Tir 2
   '
   D$="   Loop:"
   D$=D$+"   If RG=0 then Jump Loop;"
   D$=D$+"   Let RG=0;"
   D$=D$+"   Let X=RH; Let Y=RI;"
   D$=D$+"   If RJ=8 then Jump M;"
   D$=D$+"   Let A=17;"
   D$=D$+"   Move -48,0,12;"
   D$=D$+"   Jump N;"
   D$=D$+"M: Let A=18;"
   D$=D$+"   Move 48,0,12;"
   D$=D$+"N: Let Y=320;"
   D$=D$+"   Let RF=0"
   D$=D$+"   Jump Loop;"
   '
   Amal 2,B$
   Amal 3,C$
   Amal 4,D$
   '
   Amal 10,"Anim 0,(5,8)(6,8)(7,8);"
   Amal 11,"Anim 0,(11,8)(12,8)(13,8);"
   '
   '  Ennemis 
   '
   For I=1 To 5
      Exit If MN(I)=0
      Bob ENNEMI(I),MX(I)*16,MY(I)*16-1,MN(I)
      Channel ENNEMI(I) To Bob ENNEMI(I)
      A$="L: Move "+Str$(MV(I)*16)+",0,"+Str$(MV(I)*16)+";"
      A$=A$+"Let A="+Str$(MN(I)+1)+";"
      A$=A$+"Move -"+Str$(MV(I)*16)+",0,"+Str$(MV(I)*16)+";"
      A$=A$+"Let A="+Str$(MN(I))+";"
      A$=A$+"Jump L;"
      Amal ENNEMI(I),A$
   Next I
   NBMONSTRES=I-1
   '
   ' *** Divers 
   '
   Clear Key 
   '
   Auto View On 
   Screen Show 0
   '
   If ZIKON
      MUSIQUE[2,ZIKVOL]
   End If 
   '
   Wait Vbl 
   '
End Proc
Procedure CHARGER[N]
   '
   ' Charger les données du tableau depuis le disque
   '
   Shared C(),MN(),MX(),MY(),MV(),TEMPS1,MXTEMPS1,TEMPS2,MXTEMPS2,COM$
   Open Random 1,"Twin:NewLevels.Def"
   Field 1,1 As CASE$
   INDEX=(N-1)*196+1
   For X=1 To 14
      For Y=1 To 14
         Get 1,INDEX
         C(X,Y)=Val(CASE$)
         Inc INDEX
      Next Y
   Next X
   Close 1
   '
   Open Random 1,"Twin:NewLevels.Mst"
   Field 1,2 As NUM$,2 As XD$,2 As YD$,2 As MV$
   INDEX=(N-1)*5
   For I=1 To 5
      Inc INDEX
      Get 1,INDEX
      MN(I)=Val(NUM$)
      MX(I)=Val(XD$)
      MY(I)=Val(YD$)
      MV(I)=Val(MV$)
   Next I
   Close 1
   '
   Open Random 1,"Twin:NewLevels.Zob"
   Field 1,3 As TM1$,3 As TM2$,35 As COM$
   Get 1,N
   Close 1
   TEMPS1=Val(TM1$)
   MXTEMPS1=TEMPS1
   TEMPS2=Val(TM2$)
   MXTEMPS2=TEMPS2
   Paper 0
   Pen 1
   Do 
      If Right$(COM$,1)=" "
         COM$=Left$(COM$,Len(COM$)-1)
      Else 
         Exit 
      End If 
   Loop 
   Locate 0,25
   Centre COM$
   '
End Proc
Procedure BONUS
   '
   '  Afficher un nouveau bonus ( sous interruptions )
   '
   Shared BONUS,LEVEL,CURBONUS
   Do 
      X=Rnd(13)+1
      Y=Rnd(13)+1
      If C(X,Y)=0
         If Y=14
            Exit 
         Else 
            If C(X,Y+1)=1
               Exit 
            End If 
         End If 
      End If 
   Loop 
   CURBONUS=Rnd(15)+37
   Bob BONUS,X*16,Y*16-1,CURBONUS
   Every On 
End Proc
Procedure EFFET_BONUS1
   '
   ' S'occupe des bonus pour le j1
   '
   Shared CURBONUS,SFXON,CREDITS,JOUEUR1,JOUEUR2
   Shared PARALIZED1,PARALIZED2,PARIMG1,PARIMG2,MXTEMPS1,TEMPS1
   A=CURBONUS
   If A=43
      Repeat 
         A=Rnd(15)+37
      Until A<>43
   End If 
   If A=37
      Sam Play %1,3
      UPDSCORE[500,0]
      Pop Proc
   End If 
   If A=38
      Sam Play %1,3
      UPDSCORE[250,0]
      Pop Proc
   End If 
   If A=39
      Sam Play %1,3
      UPDSCORE[-100,0]
      Pop Proc
   End If 
   If A=40
      Sam Play %1,3
      Inc CREDITS
      L$=Str$(CREDITS)-" "
      If Len(L$)=1
         L$="0"+L$
      End If 
      Ink 18,0
      Text 280,218,L$
      Pop Proc
   End If 
   If A=41
      Sam Play %1,3
      UPDSCORE[1000,0]
      Pop Proc
   End If 
   If A=42
      Sam Play %1,3
      UPDSCORE[500,0]
      Pop Proc
   End If 
   If A=44
      Sam Play %1,3
      UPDSCORE[100,0]
      Pop Proc
   End If 
   If A=45
      Sam Play %1,3
      UPDSCORE[5000,0]
      Pop Proc
   End If 
   If A=46
      Sam Play %1,3
      UPDSCORE[250,0]
      Pop Proc
   End If 
   If A=47
      Sam Play %1,3
      UPDSCORE[500,0]
      Pop Proc
   End If 
   If A=48
      Sam Play %1,1
      PARALIZED1=1
      Amal Freeze 1
      PARIMG1=I Bob(JOUEUR1)
      Amal On 10
      UPDSCORE[0,0]
      Pop Proc
   End If 
   If A=49
      Sam Play %1,4
      TEMPS1=MXTEMPS1
      Ink 4
      Bar 1,208-TEMPS1 To 14,205
      Pop Proc
   End If 
   If A=50
      Sam Play %1,3
      UPDSCORE[2500,0]
      Pop Proc
   End If 
   If A=51
      Sam Play %1,6
      PARALIZED2=1
      Amal Freeze 2
      PARIMG2=I Bob(JOUEUR2)
      Amal On 11
      UPDSCORE[0,0]
      Pop Proc
   End If 
   If A=52
      Sam Play %1,3
      Swap SCORE1,SCORE2
      UPDSCORE[0,0]
      Pop Proc
   End If 
End Proc
Procedure EFFET_BONUS2
   '
   ' S'occupe des bonus pour le j2
   '
   Shared CURBONUS,ZIK,CREDITS,JOUEUR1,JOUEUR2
   Shared PARALIZED1,PARALIZED2,PARIMG1,PARIMG2,MXTEMPS2,TEMPS2
   A=CURBONUS
   If A=43
      Repeat 
         A=Rnd(15)+37
      Until A<>43
   End If 
   If A=37
      Sam Play %10,2
      UPDSCORE[0,500]
      Pop Proc
   End If 
   If A=38
      Sam Play %10,1
      UPDSCORE[0,250]
      Pop Proc
   End If 
   If A=39
      Sam Play %10,3
      UPDSCORE[0,-100]
      Pop Proc
   End If 
   If A=40
      Sam Play %10,3
      Inc CREDITS
      L$=Str$(CREDITS)-" "
      If Len(L$)=1
         L$="0"+L$
      End If 
      Ink 18,0
      Text 280,218,L$
      Pop Proc
   End If 
   If A=41
      Sam Play %10,2
      UPDSCORE[0,1000]
      Pop Proc
   End If 
   If A=42
      Sam Play %10,1
      UPDSCORE[0,500]
      Pop Proc
   End If 
   If A=44
      Sam Play %10,1
      UPDSCORE[0,100]
      Pop Proc
   End If 
   If A=45
      Sam Play %10,2
      UPDSCORE[0,5000]
      Pop Proc
   End If 
   If A=46
      Sam Play %10,1
      UPDSCORE[0,250]
      Pop Proc
   End If 
   If A=47
      Sam Play %10,1
      UPDSCORE[0,500]
      Pop Proc
   End If 
   If A=48
      Sam Play %10,8
      PARALIZED2=1
      Amal Freeze 2
      PARIMG2=I Bob(JOUEUR2)
      Amal On 11
      UPDSCORE[0,0]
      Pop Proc
   End If 
   If A=49
      TEMPS2=MXTEMPS2
      Ink 4
      Bar 241,208-TEMPS2 To 254,205
      Pop Proc
   End If 
   If A=50
      Sam Play %10,2
      UPDSCORE[0,2500]
      Pop Proc
   End If 
   If A=51
      Sam Play %10,3
      PARALIZED1=1
      Amal Freeze 1
      PARIMG1=I Bob(JOUEUR1)
      Amal On 10
      UPDSCORE[0,0]
      Pop Proc
   End If 
   If A=52
      Sam Play %10,12
      Swap SCORE1,SCORE2
      Pop Proc
   End If 
End Proc
Procedure WINNER[N]
   '
   ' Affiche 'Winner is player n' 
   '
   Clear Key 
   Cls 0
   Screen Swap 
   Wait Vbl 
   Cls 0
   Colour 1,0
   Paste Icon 40,30,1
   Paste Icon 72,80,2
   Paste Icon 112,120,N+2
   Fade 1,0,$FFF
   Wait 15
   While Fire(1) or Fire(0)
      Multi Wait 
   Wend 
   Timer=0
   Repeat 
      Multi Wait 
   Until Fire(1) or Fire(0) or Timer>=500 or Len(Inkey$)
   Fade 1
   Wait 15
   Bob Off 
   Cls 0
End Proc
Procedure EGALITE
   '
   ' Affiche 'Egalité'
   '
   Clear Key 
   Cls 0
   Screen Swap 
   Wait Vbl 
   Cls 0
   Colour 1,0
   Paste Icon 24,80,30
   Fade 1,0,$FFF
   Wait 15
   While Fire(1) or Fire(0)
      Multi Wait 
   Wend 
   Timer=0
   Repeat 
      Multi Wait 
   Until Fire(1) or Fire(0) or Timer>=500 or Len(Inkey$)
   Fade 1
   Wait 15
   Bob Off 
   Cls 0
End Proc
Procedure _GETREADY
   '
   ' Affiche 'Level n / Get Ready'
   '
   Shared LEVEL
   Clear Key 
   Cls 0
   Screen Swap 
   Wait Vbl 
   Cls 0
   Colour 1,0
   If LEVEL<10
      Paste Icon 68,60,17
      Paste Icon 220,62,17+LEVEL
   Else 
      Paste Icon 60,60,17
      Paste Icon 212,62,17+(LEVEL/10)
      If LEVEL mod 10=0
         Paste Icon 240,62,27
      Else 
         Paste Icon 240,62,17+(LEVEL mod 10)
      End If 
   End If 
   Paste Icon 30,110,16
   Wait 15
   Fade 1,0,$FFF
End Proc
Procedure _GAME_OVER
   '
   ' Affiche 'Game Over'
   '
   Cls 0
   Palette 0,0
   Paste Icon 30,80,29
   Wait 15
   Fade 1,0,$FFF
   While Fire(1) or Fire(0)
      Multi Wait 
   Wend 
   Repeat 
      Multi Wait 
   Until Fire(1) or Fire(0)
   Fade 1
   Wait 15
   Cls 0
End Proc
Procedure _OH_NO
   '
   ' Affiche 'Oh no!' (perte d'un crédit) 
   '
   Cls 0
   Palette 0,0
   Paste Icon 80,80,28
   Wait 15
   Fade 1,0,$FFF
   While Fire(1) or Fire(0)
      Multi Wait 
   Wend 
   Repeat 
      Multi Wait 
   Until Fire(1) or Fire(0)
   Fade 1
   Wait 15
   Cls 0
End Proc
Procedure MUSIQUE[N,V]
   '
   ' Lance une musique
   '
   Shared ZIKPLAYING,QUELLEZIK
   Mvolume 0
   If QUELLEZIK<>N
      Erase 3
      Erase 5
      If N=1
         ' My First 
         Load "Twin:Zik-Intro.Abk",3
      Else 
         ' Magic Forest 
         Load "Twin:Zik-Jeu.Abk",3
      End If 
   End If 
   QUELLEZIK=N
   Led Off 
   Music 1
   Mvolume V
   Wait Vbl 
   ZIKPLAYING=True
End Proc
Procedure MUSIQUE_OFF
   '
   ' Arrête la musique en Fading
   '
   Shared ZIKVOL,ZIKPLAYING
   For I=ZIKVOL To 0 Step -1
      Mvolume I
      Wait Vbl 
   Next I
   Music Off 
   Led On 
   ZIKPLAYING=False
End Proc
Procedure REFOUS_PALETTE
   '
   ' Le nom est parlant, non ?
   '
   Palette $0,$FFF,$A,$F,$19F,$8EF,$500,$900,$F00,$F91,$FF0,$777,$708,$60,$A0,$E0
End Proc
Procedure UPDSCORE[J1PLUS,J2PLUS]
   '
   ' Met à jour les scores, s'occupe des affichages en bas de l'écran 
   '
   Shared SCORE1,SCORE2,MORT1,MORT2,PARALIZED1,PARALIZED2,NAME1$,NAME2$
   '
   Add SCORE1,J1PLUS
   Add SCORE2,J2PLUS
   If SCORE1<0
      SCORE1=0
   End If 
   If SCORE2<0
      SCORE2=0
   End If 
   '
   Ink 11,0
   If MORT1=False
      If PARALIZED1=False
         Text 8,242,"SCORE"+Space$(8-Len(Str$(SCORE1)))+Str$(SCORE1)
      Else 
         Text 8,242," Oh my head ! "
      End If 
   Else 
      Text 8,242,"    OH NO !   "
   End If 
   If MORT2=False
      If PARALIZED2=False
         Text 136,242,"SCORE"+Space$(8-Len(Str$(SCORE2)))+Str$(SCORE2)
      Else 
         Text 136,242," Oh my head ! "
      End If 
   Else 
      Text 136,242,"    OH NO !   "
   End If 
End Proc
Procedure PUB[N]
   '
   ' On se retrouve après ce 'A tout de suite'
   '
   S=Screen
   Screen Hide S
   Load "Twin:Img-Publogo.Abk",10
   Unpack 10 To 4
   Erase 10
   Wait 60
   F$=("Twin:Img-Pub"+Str$(N)+".Abk")-" "
   If Exist(F$)
      Load F$,10
   Else 
      Load "Twin:Img-Pub1.Abk",10
   End If 
   Unpack 10 To 4
   Erase 10
   Repeat 
      Multi Wait 
   Until Fire(1) or Fire(0)
   Fade 1
   Screen Close 4
   Screen S
   Screen Show S
   Wait 15
End Proc
Procedure HALL_OF_FAME
   '
   ' Affiche les 'Ten Top Scores' 
   '
   Shared HISCORE(),HISCORE$()
   Flash Off 
   Palette 0,0,0,0,0
   Cls 0
   Paste Icon 65,30,5
   Ink 1,0
   For I=1 To 10
      H$=Str$(HISCORE(I))-" "
      S$=Right$(Str$(I),2)
      If I=1
         S$=S$+"ST"
      Else 
         If I=2
            S$=S$+"ND"
         Else 
            If I=3
               S$=S$+"RD"
            Else 
               S$=S$+"TH"
            End If 
         End If 
      End If 
      S$=S$+"  "+String$("0",6-Len(H$))+H$+"  "+HISCORE$(I)
      Text 16,12*I+80,S$
   Next I
End Proc
Procedure OPTIONS
   '
   '  Choix des options ( musique, pub, etc... )
   '
   Shared ZIKON,ZIKVOL,SFXON,SFXVOL,PUBON,NAME1$,NAME2$,HISAVEON
   '
   ' Inits
   '
   Dim S$(1),S2$(1)
   S$(0)="OFF"
   S$(1)="ON "
   S2$(0)="NON"
   S2$(1)="OUI"
   C=1
   '
   Dim T$(10),Y(10),CO(10)
   Restore Z
   For I=1 To 10
      Read CO(I),Y(I),T$(I)
   Next I
   Z:
   Data 1,80,"MUSIQUE - "+S$(-ZIKON)
   Data 1,90,"VOLUME -"+Str$(ZIKVOL)
   Data 1,110,"BRUITAGES - "+S$(-SFXON)
   Data 1,120,"VOLUME -"+Str$(SFXVOL)
   Data 1,140,"PUBLICITE - "+S$(-PUBON)
   Data 1,160,"JOUEUR 1 - "+NAME1$
   Data 1,170,"JOUEUR 2 - "+NAME2$
   Data 1,190,"SAUVER HI-SCORES - "+S2$(-HISAVEON)
   Data 4,230,"SAUVER LA CONFIGURATION"
   Data 4,250,"- JOUER -"
   '
   ' Affichage
   '
   Palette 0,0,0,0,0,0
   Paste Icon 64,10,15
   For I=1 To 10
      If I=C
         Ink 2,0
      Else 
         Ink CO(I),0
      End If 
      CTR[Y(I),T$(I)]
   Next I
   '
   F$="(11F,1)(33F,1)(55F,1)(77F,1)(99F,1)(BBF,1)(DDF,1)(FFF,1)(DDF,1)(BBF,1)(99F,1)(77F,1)(55F,1)(33F,1)"
   Fade 1,$0,$CCC,$F,$FFF,$F0
   Wait 15
   Flash 2,F$
   '
   ' Boucle de gestion
   '
   GO=False
   Repeat 
      '
      If Jup(1)
         If C>1
            Ink CO(C)
            CTR[Y(C),T$(C)]
            Dec C
            Ink 2
            CTR[Y(C),T$(C)]
         End If 
         Wait 2
      End If 
      '
      If Jdown(1)
         If C<10
            Ink CO(C)
            CTR[Y(C),T$(C)]
            Inc C
            Ink 2
            CTR[Y(C),T$(C)]
         End If 
         Wait 2
      End If 
      '  
      If Fire(1)
         '
         If C=1
            ' Musique On/Off 
            ZIKON= Not(ZIKON)
            T$(1)="MUSIQUE - "+S$(-ZIKON)
            CTR[Y(1),T$(1)]
         Else 
            If C=2
               ' Volume Musique 
               CTR[Y(2),T$(2)]
            Else 
               If C=3
                  ' Bruitages On/Off 
                  SFXON= Not(SFXON)
                  T$(3)="BRUITAGES - "+S$(-SFXON)
                  CTR[Y(3),T$(3)]
               Else 
                  If C=4
                     ' Volume Bruitages 
                     CTR[Y(4),T$(4)]
                  Else 
                     If C=5
                        ' Pub On/Off 
                        PUBON= Not(PUBON)
                        T$(5)="PUBLICITE - "+S$(-PUBON)
                        CTR[Y(5),T$(5)]
                     Else 
                        If C=6
                           ' Nom du Joueur 1
                           CTR[Y(6),T$(6)]
                        Else 
                           If C=7
                              ' Nom du Joueur 2
                              CTR[Y(7),T$(7)]
                           Else 
                              If C=8
                                 ' Sauver scores on/off 
                                 HISAVEON= Not(HISAVEON)
                                 T$(8)="SAUVER HI-SCORES - "+S2$(-HISAVEON)
                                 CTR[Y(8),T$(8)]
                              Else 
                                 If C=9
                                    ' Sauver Config
                                    SAUVE_PREFS
                                 Else 
                                    If C=10
                                       ' Let's Go ! 
                                       GO=True
                                    End If 
                                 End If 
                              End If 
                           End If 
                        End If 
                     End If 
                  End If 
               End If 
            End If 
         End If 
         '
         While Fire(1)
         Wend 
         '
      End If 
      '
      Multi Wait 
      '
   Until GO
   '
   Flash Off 
   Fade 1
   Wait 15
   Cls 0
   '
End Proc
Procedure CTR[Y,A$]
   '
   ' Utilisé par 'Options'
   '
   Text(320-Len(A$)*8)/2,Y,A$
End Proc
Procedure _COPYRIGHT
   '
   ' © © © © © © © © ® ® ® ® ® ® ® ®
   '
   Load "Twin:Img-Copyright.Abk",10
   YDISPLAY=75
   Unpack 10 To 7
   For Y=2 To Screen Height/2 Step 8
      Screen Display 7,,YDISPLAY+Screen Height/2-Y,,Y*2
      Screen Offset 7,,Screen Height/2-Y
      View 
      Wait Vbl 
   Next Y
   T=Timer
   Repeat 
      Multi Wait 
   Until Len(Inkey$) or Fire(1) or Fire(0) or Timer-T>200
   For Y=Screen Height/2 To 0 Step -8
      Screen Display 7,,YDISPLAY+Screen Height/2-Y,,Y*2
      Screen Offset 7,,Screen Height/2-Y
      View 
      Wait Vbl 
   Next Y
   Screen Close 7
   Erase 10
End Proc
Procedure CHARGE_SCORES
   '
   ' Charge la table des scores 
   '
   Shared HISCORE$(),HISCORE()
   Open In 1,"Twin:High-Scores"
   For I=1 To 10
      Input #1,HISCORE$(I)
      Input #1,HISCORE(I)
   Next I
   Close 1
End Proc
Procedure SAUVE_SCORES
   '
   ' Sauve les scores 
   '
   Shared HISCORE$(),HISCORE()
   Open Out 1,"Twin:High-Scores"
   For I=1 To 10
      Print #1,HISCORE$(I)
      Print #1,HISCORE(I)
   Next I
   Close 1
End Proc
Procedure CHARGE_PREFS
   '
   ' Charge les préférences 
   '
   Shared ZIKON,ZIKVOL,SFXON,SFXVOL,PUBON,NAME1$,NAME2$
   Open In 1,"Twin:Prefs"
   Input #1,ZIKON
   Input #1,ZIKVOL
   Input #1,SFXON
   Input #1,SFXVOL
   Input #1,PUBON
   Input #1,NAME1$
   Input #1,NAME2$
   Close 1
End Proc
Procedure SAUVE_PREFS
   '
   ' Sauve les prefs
   '
   Shared ZIKON,ZIKVOL,SFXON,SFXVOL,PUBON,NAME1$,NAME2$
   Open Out 1,"Twin:Prefs"
   Print #1,ZIKON
   Print #1,ZIKVOL
   Print #1,SFXON
   Print #1,SFXVOL
   Print #1,PUBON
   Print #1,NAME1$
   Print #1,NAME2$
   Close 1
End Proc
Procedure FIN
   '
   ' Serait-ce la fin ? 
   '
   Erase 2
   Music Off 
   Erase 3
   Erase 5
   Erase 9
   Amal Off 
   Bob Off 
   Erase 1
   End 
End Proc
'
Procedure BANK_LOAD[F$,CLE$,BANK]
   '
   ' PowerMob DecryptFile adapté
   ' cf. AMOS_Save2:Utils/File_Coder.AMOS 
   '
   Open In 1,F$
   L=Lof(1)
   Close 1
   Open In 1,CLE$
   L2=Lof(1)
   Close 1
   '
   Reserve As Work 13,L
   Reserve As Work BANK,L
   Reserve As Work 14,L2
   Bload F$,6
   Bload CLE$,8
   P1=Start(13)
   P2=Start(7)
   P3=Start(14)
   '
   For I=1 To L
      Poke P2,Peek(P3)-Peek(P1)
      Inc P1
      Inc P2
      Add P3,1,1 To L2
   Next I
   '
   Erase 13
   Erase 14
   '
End Proc
