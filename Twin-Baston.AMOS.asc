'
'                      ------------------- 
'                     |    Twin Baston    |
'                      ------------------- 
'
'                  ©1992  PowerMob Productions 
'
'
'                           CREDITS
'                          ~~~~~~~~~ 
'           Coding by :  Ronan Amicel ( Mr Reed )
'            Music by :  Eric Hilpipre ( Vuzz )
'         Graphics by :  David Thévenot ( Eagle )
'
'
'********************** Le Programme ************************
'
'  NOM           :  TWIN BASTON
'  VERSION       :  0.99 
'  AUTEUR        :  RONAN AMICEL alias MR REED 
'  DERNIERE MOD. :  28.10.92 
'  DESCRIPTION   :  Jeu de Plates-formes à 1/2 Joueurs.
'
'************************************************************
'
' Historique:  Cette version n'utilise plus AMAL pour les
' ~~~~~~~~~~~  joueurs ( slmt ennemis, anims, et tirs )
' Il n'y a plus qu'une Pro Effet-Bonus, Options modifiées. 
' Les préférences et les scores sont sur disque maintenant.
' Affichage sur l'écran de jeu des crédits, du level et du 
' nombre de victoires de chacun. 
' Samples désopilants. 
' Intégration des nouvelles anims de David.
' Meilleure gestion des collisions joueur/briques. 
' Joueurs continuent à tomber quand assommés.
' Nouvelle méthode d'affichage des messages ( game-over, etc... )
' Jeux inter-tableaux ( Tron & Orangina )
' Trésors ne restent plus en suspension
' Nouvelles routines de chargement de tableaux, à cause de 
' la nouvelle version de l'éditeur ( V5.1 )
' Nouveaux-nouveaux sprites intégrés.
' Cà marche bien avec les nouvelles routines (tests briques
' par rapport à 0 et plus à 1 )
' 0.96b - 10.10.92 : Enlevé le bug du skate-board
' 0.97a - 10.10.92 : Intro modifiée, début nouvelles options.
'         16.10.92 : Apparition de l'img TB-The Duel.
'         18.10.92 : Gestion de l'invincibilité, enlevé un bug 
'                    dans l'apparition des bonus, légère modif.
'                    de la pro Effet-Bonus.
' 0.97b - 20.10.92 : Case de départ. Debuggé 'Même temps'
'         21.10.92 : Enlevé un léger bug.
' 0.97b'- 23.10.92 : Rajouté des briques intraversables
'                    Enlevé bugs: case de départ, tableaux ma()
'                    & Cie trop petits ( 10 -> 20 )
' 0.98  - 26.10.92 : Bien travaillé sur les options
'                    Commencé la version 1 Joueur
'         27.10.92 : Continué v1J
'                    Petites améliorations/enlevé bugs...
'         28.10.92 : idem
' 0.99  - 28.10.92 : Encore des nouveaux sprites intégrés. 
'                    Normalement ce sont les sprites définitifs... 
'  
'************************************************************
'
'  A Faire:
'  ~~~~~~~~
'   - Mode 1 joueur (bien tester)
'   - Options (finir)
'   - Inertie
'   - Revoir saut
'   - Etc... cf feuille
'
'************************************************************
'
'
' D'abord des inits, variables, constantes, etc..
'
Set Buffer 30
'
Dim C(14,14),C2(14,14)
Global C(),C2()
Dim HISCORE(10),HISCORE$(10)
Dim SQ$(20),NBSQ(5)
Dim MX(5),MY(5),MA(5,20),MH(5,20),MV(5,20),MT(5,20)
Dim ENNEMI(5),DANSLESPOMMES(5)
Dim TRESOR(3),TRESOR_TOMBE(3)
Dim VITX(2),VITY(2),INV(2),SUPER(2)
'
Restore DT
For I=1 To 20
   Read SQ$(I)
Next I
'
V$="0.98"
'
JOUEUR1=1
JOUEUR2=2
TIR1=3
TIR2=4
ENNEMI(1)=5
ENNEMI(2)=6
ENNEMI(3)=7
ENNEMI(4)=8
ENNEMI(5)=9
TRESOR(1)=10
TRESOR(2)=11
TRESOR(3)=12
BONUS=13
'
STANDING=0
WALKING=1
JUMPING=2
FALLING=3
GAUCHE=-1
DROITE=1
'
MSG_OH_NO=1
MSG_GAME_OVER=2
MSG_GET_READY=3
MSG_WINNER=4
MSG_EGALITE=5
MSG_BONUS=6
MSG_OPTIONS=7
MSG_SCORES=8
'
' Puis on lance le chargement/l'intro
'
Proc INIT
'
' On remet un zoli écran comme y faut... 
'
If Screen>=0
   Fade 1
   Wait 16
   For X=320 To 0 Step -8
      Screen Offset 1,X,0
      Wait Vbl 
   Next X
   For X=0 To 320 Step 8
      Screen Offset 0,X,0
      Wait Vbl 
   Next X
   Screen Close 0
   Screen Close 1
   Screen Close 2
End If 
Auto View Off 
Wait Vbl 
Screen Open 0,320,256,32,Lowres
Screen Display 0,128,40,320,256
Curs Off 
Flash Off 
Hide On 
Cls 0
Double Buffer 
Auto View On 
Wait Vbl 
Make Mask 
'
'**************************************
'
'         Boucle principale
'
'**************************************
'
Do 
   '
   ' L'intro en boucle, puis le choix des options 
   '
   Autoback 2
   Proc PRESENTATION
   Proc OPTIONS2
   '
   ' Début de la partie, initialisations
   '
   Load "Twin:Img-Back1.Abk",9
   CREDITS=3
   LEVEL=1
   SCORE1=0
   SCORE2=0
   VICT1=0
   VICT2=0
   For I=1 To 5
      DANSLESPOMMES(I)=0
   Next I
   '
   ' Chargement de la bande sonore
   '
   If ZIKON
   Proc MUSIQUE[2,0]
   End If 
   QUELLEZIK=2
   Load "Twin:Samples-Jeu.Abk",5
   '
   ' Go! On y va !
   '
   Proc _MESSAGE[MSG_GET_READY,0]
   Proc CHARGER[LEVEL]
   Sam Play 8
   Timer=0
   Repeat 
      Multi Wait 
   Until Fire(1) or Fire(0) or Timer>=400
   Fade 1
   Wait 15
   Bob Off 
   Cls 0
   '
   '****************************************
   '
   '    Boucle de jeu (tant que crédits)
   '
   '****************************************
   '
   While CREDITS>0
      '
      ' Initialise pour le tableau 
      '
      PASSE=False
      HURRY=False
      '
      MORT1=False
      PARALIZED1=False
      STATE1=STANDING
      SENS1=DROITE
      LONGTEMPS1=0
      RAMENE1=0
      CARRY1=0
      EMPJP1=0
      GLOUGLOU1=0
      POUM1=0
      VITX(1)=2
      VITY(1)=2
      INV(1)=0
      SUPER(1)=0
      '
      MORT2=False
      PARALIZED2=False
      STATE2=STANDING
      SENS2=GAUCHE
      LONGTEMPS2=0
      RAMENE2=0
      CARRY2=0
      EMPJP2=0
      GLOUGLOU2=0
      POUM2=0
      VITX(2)=2
      VITY(2)=2
      INV(2)=0
      SUPER(2)=0
      '
      Proc INIT_TABLEAU
      If SFXON
         VOIX1=%1
         VOIX2=%10
      Else 
         VOIX1=%0
         VOIX2=%0
      End If 
      '
      BONUSWAIT=0
      '
      Wait Vbl 
      For I=1 To NBMONSTRES+4
         Amal On I
      Next I
      '
      Synchro Off 
      Timer=0
      TEMPSTOTAL=0
      '
      '************************************
      '
      '          The Big Boucle
      '
      '************************************
      If PARTIE=2
         '
         '  Mode 2 joueurs
         '
         Repeat 
            '
            '- - - - - JOUEUR 1 - - - - - -
            '
            If MORT1=False
               '
               ' Où es-tu ? 
               '
               X1=X Bob(1)
               Y1=Y Bob(1)
               X=X1/16
               Y=Y1/16
               '
               If PARALIZED1>0
                  '
                  ' -------------------- Si on est paralizé
                  '
                  Inc PARALIZED1
                  If PARALIZED1=200
                     PARALIZED1=0
                     Amal Freeze 10
                     J1_ANIM
                     UPDSCORE[0,0]
                  End If 
                  '
                  ' Version allégée des routines KO=False
                  '
                  If STATE1=JUMPING
                     Inc JUMP1
                     If(Y1 mod 16=15) and(Y>=1)
                        If(C(X,Y)=50) or(C((X1+15)/16,Y)=50)
                           JUMP1=17
                        End If 
                     End If 
                     If JUMP1<17
                        Add Y1,-VITY(1)
                     Else 
                        If Y1<10
                           STATE1=FALLING
                           FALL1=0
                           LONGTEMPS1=1
                        Else 
                           If(C(X,Y+1)>0) or(C((X1+15)/16,Y+1)>0)
                              STATE1=FALLING
                              FALL1=0
                              LONGTEMPS1=1
                           Else 
                              If Y=13
                                 STATE1=STANDING
                              Else 
                                 If(C(X,Y+2)>0) or(C((X1+15)/16,Y+2)>0)
                                    STATE1=STANDING
                                 Else 
                                    STATE1=FALLING
                                    FALL1=0
                                    LONGTEMPS1=1
                                 End If 
                              End If 
                           End If 
                        End If 
                     End If 
                  Else 
                     If STATE1=FALLING
                        Inc FALL1
                        If FALL1<9
                           Add Y1,VITY(1)
                        Else 
                           If Y1=223
                              STATE1=STANDING
                              LONGTEMPS1=0
                           Else 
                              If(C(X,Y+2)>0) or(C((X1+15)/16,Y+2)>0)
                                 STATE1=STANDING
                                 LONGTEMPS1=0
                              Else 
                                 FALL1=0
                                 Inc LONGTEMPS1
                              End If 
                           End If 
                        End If 
                     End If 
                  End If 
                  '
               Else 
                  '
                  ' --------------------- Que fais-tu ?
                  '
                  If(STATE1=STANDING) or(STATE1=WALKING)
                     '
                     ' Test Collision : On Tombe ?
                     '
                     If Y1<223
                        If((C(X,Y+2)=0) and(C((X1+15)/16,Y+2)=0)) or(Y1<10)
                           STATE1=FALLING
                           FALL1=0
                           LONGTEMPS1=1
                           J1_ANIM
                        End If 
                     End If 
                     If STATE1<>FALLING
                        '
                        '
                        ' Cà bouge pas...
                        '
                        If(Joy(1)=0) or(Joy(1)=2)
                           If STATE1<>STANDING
                              STATE1=STANDING
                              J1_ANIM
                           End If 
                           EMPJP1=0
                        Else 
                           '
                           ' On saute 
                           '
                           If Jup(1)
                              If EMPJP1=0
                                 STATE1=JUMPING
                                 JUMP1=0
                                 J1_ANIM
                                 EMPJP1=1
                              End If 
                           Else 
                              '
                              EMPJP1=0
                              '
                              ' Gauche 
                              '
                              If(Jleft(1) and INV(1)=False) or(Jright(1) and INV(1)=True)
                                 If(STATE1=STANDING) or(SENS1=DROITE)
                                    STATE1=WALKING
                                    SENS1=GAUCHE
                                    J1_ANIM
                                 End If 
                                 If Y1>=0
                                    If X1>16
                                       If X1 mod 16=0
                                          If C(X-1,Y+1)=0
                                             Add X1,-VITX(1)
                                          End If 
                                       Else 
                                          Add X1,-VITX(1)
                                       End If 
                                    Else 
                                       X1=16
                                    End If 
                                 End If 
                              Else 
                                 '
                                 ' Droite 
                                 '
                                 If(Jright(1) and INV(1)=False) or(Jleft(1) and INV(1)=True)
                                    If(STATE1=STANDING) or(SENS1=GAUCHE)
                                       STATE1=WALKING
                                       SENS1=DROITE
                                       J1_ANIM
                                    End If 
                                    If Y1>=0
                                       If X1<224
                                          If C(X+1,Y+1)=0
                                             Add X1,VITX(1)
                                          End If 
                                       Else 
                                          X1=224
                                       End If 
                                    End If 
                                 End If 
                              End If 
                           End If 
                        End If 
                     End If 
                  End If 
                  '
                  If STATE1=JUMPING
                     '
                     Inc JUMP1
                     If(Y1 mod 16=15) and(Y>=1)
                        If(C(X,Y)=50) or(C((X1+15)/16,Y)=50)
                           JUMP1=17
                        End If 
                     End If 
                     '
                     ' Au cours du saut.
                     '
                     If JUMP1<17
                        Add Y1,-VITY(1)
                        '
                        ' Dep. gauche
                        '
                        If(Jleft(1) and INV(1)=False) or(Jright(1) and INV(1)=True)
                           If SENS1=DROITE
                              SENS1=GAUCHE
                              J1_ANIM
                           End If 
                           If Y1>=0
                              If X1>16
                                 If X1 mod 16=0
                                    If C(X-1,(Y1/16)+1)=0
                                       Add X1,-VITX(1)
                                    End If 
                                 Else 
                                    Add X1,-VITX(1)
                                 End If 
                              Else 
                                 X1=16
                              End If 
                           End If 
                        Else 
                           '
                           ' Dep droite 
                           '
                           If(Jright(1) and INV(1)=False) or(Jleft(1) and INV(1)=True)
                              If SENS1=GAUCHE
                                 SENS1=DROITE
                                 J1_ANIM
                              End If 
                              If Y1>=0
                                 If X1<224
                                    If C(X+1,(Y1/16)+1)=0
                                       Add X1,VITX(1)
                                    End If 
                                 Else 
                                    X1=224
                                 End If 
                              End If 
                           End If 
                        End If 
                     Else 
                        '
                        ' En haut du saut, doit-on retomber ?
                        '
                        If Y1<10
                           STATE1=FALLING
                           FALL1=0
                           LONGTEMPS1=1
                           J1_ANIM
                        Else 
                           If(C(X,Y+1)>0) or(C((X1+15)/16,Y+1)>0)
                              STATE1=FALLING
                              FALL1=0
                              LONGTEMPS1=1
                              J1_ANIM
                           Else 
                              If Y=13
                                 STATE1=STANDING
                                 J1_ANIM
                              Else 
                                 If(C(X,Y+2)>0) or(C((X1+15)/16,Y+2)>0)
                                    STATE1=STANDING
                                    J1_ANIM
                                 Else 
                                    STATE1=FALLING
                                    FALL1=0
                                    LONGTEMPS1=1
                                    J1_ANIM
                                 End If 
                              End If 
                           End If 
                        End If 
                     End If 
                  Else 
                     '
                     If STATE1=FALLING
                        '
                        ' oooooooooooooooo No, I'm Fallin' 
                        '
                        Inc FALL1
                        '
                        ' Au cours de la descente
                        '
                        If FALL1<9
                           If FALL1>1
                              '
                              ' Dep gauche 
                              '
                              If(Jleft(1) and INV(1)=False) or(Jright(1) and INV(1)=True)
                                 If SENS1=DROITE
                                    SENS1=GAUCHE
                                    J1_ANIM
                                 End If 
                                 If Y1>=0
                                    If X1>16
                                       If X1 mod 16=0
                                          If C(X-1,Y+1)=0
                                             Add X1,-VITX(1)
                                          End If 
                                       Else 
                                          Add X1,-VITX(1)
                                       End If 
                                    Else 
                                       X1=16
                                    End If 
                                 End If 
                              Else 
                                 '
                                 ' Droite 
                                 '
                                 If(Jright(1) and INV(1)=False) or(Jleft(1) and INV(1)=True)
                                    If SENS1=GAUCHE
                                       SENS1=DROITE
                                       J1_ANIM
                                    End If 
                                    If Y1>=0
                                       If X1<224
                                          If C(X+1,Y+1)=0
                                             Add X1,VITX(1)
                                          End If 
                                       Else 
                                          X1=224
                                       End If 
                                    End If 
                                 End If 
                              End If 
                           End If 
                           '
                           Add Y1,VITY(1)
                           '
                        Else 
                           '
                           ' Redescendu d'une case: continuer à tomber ?
                           '
                           If Y1=223
                              STATE1=STANDING
                              LONGTEMPS1=0
                              J1_ANIM
                           Else 
                              If(C(X,Y+2)>0) or(C((X1+15)/16,Y+2)>0)
                                 STATE1=STANDING
                                 LONGTEMPS1=0
                                 J1_ANIM
                              Else 
                                 FALL1=0
                                 Inc LONGTEMPS1
                                 J1_ANIM
                              End If 
                           End If 
                        End If 
                     End If 
                  End If 
                  '
                  ' Poum ! 
                  '
                  If Fire(1) and(GLOUGLOU1=False)
                     If Amreg(0)=0
                        Amreg(0)=1
                        Amreg(2)=X1
                        Amreg(3)=Y1
                        Amreg(4)=SENS1
                        If(STATE1<>JUMPING) and(STATE1<>FALLING)
                           If SENS1=DROITE
                              Amal 1,"Let A=188;"
                           Else 
                              Amal 1,"Let A=189;"
                           End If 
                           Amal On 1
                           POUM1=1
                        End If 
                     End If 
                  Else 
                     If POUM1=1
                        POUM1=0
                        J1_ANIM
                     End If 
                  End If 
                  '
                  '++++++++++++++++++++++++++++++++++++
                  '
                  '
                  ' On prend un Bonus ?
                  '
                  If Bob Col(JOUEUR1,BONUS To BONUS)
                     Proc EFFET_BONUS[1]
                     Bob BONUS,,300,
                     BONUSWAIT=150
                  End If 
                  '
                  ' Un trésor ?
                  '
                  If Bob Col(JOUEUR1,TRESOR(1) To TRESOR(NBTRESORS))
                     If CARRY1=0
                        For I=1 To NBTRESORS
                           If Col(TRESOR(I))
                              CARRY1=I
                           End If 
                        Next I
                        TRESOR_TOMBE(CARRY1)=0
                        Bob Off TRESOR(CARRY1)
                        Proc UPDSCORE[1,100]
                        Sam Play VOIX1,1
                     End If 
                  End If 
                  '
                  ' On est en bas à gauche ? 
                  '
                  If X1=16 and Y1=223
                     '
                     ' Ramène un trésor 
                     '
                     If CARRY1>0
                        CARRY1=0
                        Inc RAMENE1
                        Proc UPDSCORE[1,2000]
                        If RAMENE1+RAMENE2=NBTRESORS
                           PASSE=True
                        End If 
                        Sam Play VOIX1*8,2
                     End If 
                     '
                     ' Remonter le temps
                     '
                     If Joy(1)=2
                        If GLOUGLOU1=0
                           Limit Bob 1,0,0 To 256,224
                           Sam Play VOIX1,3
                           GLOUGLOU1=1
                           Amal 1,"Anim 0,(209,2)(210,2)(211,2)(210,2);"
                           Amal On 1
                           SENS1=GAUCHE
                        End If 
                        If HURRY=False
                           Inc TEMPS1
                           TEMPS1=Min(TEMPS1,MXTEMPS1)
                           Ink 4
                           TP=208-TEMPS1
                           Draw 1,TP+1 To 14,TP+1
                           Draw 1,TP To 14,TP
                           If MEMETEMPS
                              Draw 241,TP+1 To 254,TP+1
                              Draw 241,TP To 254,TP
                           End If 
                        End If 
                     Else 
                        If GLOUGLOU1=1
                           J1_ANIM
                        End If 
                     End If 
                  End If 
               End If 
               '
               ' --- Ceci doit toujours être exécuté ---
               '
               '
               ' On tire sur un monstre 
               '
               If Bob Col(TIR1,ENNEMI(1) To ENNEMI(NBMONSTRES))
                  For I=ENNEMI(1) To ENNEMI(5)
                     Exit If Col(I)
                  Next I
                  If DANSLESPOMMES(I-4)=False
                     DANSLESPOMMES(I-4)=200
                     Amal Freeze I
                     Bob TIR1,,320,
                     If SFXON
                        Shoot 
                     End If 
                     Proc UPDSCORE[1,10]
                  End If 
               End If 
               '
               ' On tire sur l'autre !
               '
               If Bob Col(TIR1,JOUEUR2 To JOUEUR2) and PARALIZED2=False
                  PARALIZED2=1
                  Amal Freeze 2
                  Amal On 11
                  Bob TIR1,,320,
                  Sam Play VOIX2,5
                  Proc UPDSCORE[1,50]
               End If 
               '
               ' Collision avec monstre, aïe !
               '
               If SUPER(1)=False
                  If Bob Col(JOUEUR1,ENNEMI(1) To ENNEMI(NBMONSTRES))
                     MORT1=True
                     Sam Play VOIX1,4
                     J1_ANIM
                     Bob Off TIR1
                     If CARRY1<>0
                        Bob TRESOR(CARRY1),X1-X1 mod 16,Y1,43
                        TRESOR_TOMBE(CARRY1)=True
                     End If 
                     UPDSCORE[0,0]
                  End If 
               End If 
               '
               ' Plus de temps !
               '
               If TEMPS1=0
                  MORT1=True
                  Sam Play VOIX1,4
                  J1_ANIM
                  Bob Off TIR1
                  If CARRY1<>0
                     Bob TRESOR(CARRY1),X1-X1 mod 16,Y1,43
                     TRESOR_TOMBE(CARRY1)=True
                  End If 
                  UPDSCORE[0,0]
               End If 
            End If 
            '
            '- - - - - JOUEUR 2 - - - - - -
            '
            If MORT2=False
               '
               ' Où es-tu ? 
               '
               X2=X Bob(2)
               Y2=Y Bob(2)
               X=X2/16
               Y=Y2/16
               '
               If PARALIZED2>0
                  '
                  ' Si on est paralizé 
                  '
                  Inc PARALIZED2
                  If PARALIZED2=200
                     PARALIZED2=0
                     Amal Freeze 11
                     J2_ANIM
                     UPDSCORE[0,0]
                  End If 
                  '
                  ' Version allégée des routines KO=False
                  '
                  If STATE2=JUMPING
                     Inc JUMP2
                     If(Y2 mod 16=15) and(Y>=1)
                        If(C(X,Y)=50) or(C((X2+15)/16,Y)=50)
                           JUMP2=17
                        End If 
                     End If 
                     If JUMP2<17
                        Add Y2,-VITY(2)
                     Else 
                        If Y2<10
                           STATE2=FALLING
                           FALL2=0
                           LONGTEMPS2=1
                        Else 
                           If(C(X,Y+1)>0) or(C((X2+15)/16,Y+1)>0)
                              STATE2=FALLING
                              FALL2=0
                              LONGTEMPS2=1
                           Else 
                              If(C(X,Y+2)>0) or(C((X2+15)/16,Y+2)>0)
                                 STATE2=STANDING
                              Else 
                                 STATE2=FALLING
                                 FALL2=0
                                 LONGTEMPS2=1
                              End If 
                           End If 
                        End If 
                     End If 
                  Else 
                     If STATE2=FALLING
                        Inc FALL2
                        If FALL2<9
                           Add Y2,VITY(2)
                        Else 
                           If Y2=223
                              STATE2=STANDING
                              LONGTEMPS2=0
                           Else 
                              If(C(X,Y+2)>0) or(C((X2+15)/16,Y+2)>0)
                                 STATE2=STANDING
                                 LONGTEMPS2=0
                              Else 
                                 FALL2=0
                                 Inc LONGTEMPS2
                              End If 
                           End If 
                        End If 
                     End If 
                  End If 
                  '
               Else 
                  '
                  ' Que fais-tu ?
                  '
                  If(STATE2=STANDING) or(STATE2=WALKING)
                     '
                     ' Test Collision : On Tombe ?
                     '
                     If Y2<223
                        If((C(X,Y+2)=0) and(C((X2+15)/16,Y+2)=0)) or(Y2<10)
                           STATE2=FALLING
                           FALL2=0
                           LONGTEMPS2=1
                           J2_ANIM
                        End If 
                     End If 
                     If STATE2<>FALLING
                        '
                        '
                        ' Cà bouge pas...
                        '
                        If(Joy(0)=0) or(Joy(0)=2)
                           If STATE2<>STANDING
                              STATE2=STANDING
                              J2_ANIM
                           End If 
                           EMPJP2=0
                        Else 
                           '
                           ' On saute 
                           '
                           If Jup(0)
                              If EMPJP2=0
                                 STATE2=JUMPING
                                 JUMP2=0
                                 J2_ANIM
                                 EMPJP2=1
                              End If 
                           Else 
                              '
                              EMPJP2=0
                              '
                              ' Gauche 
                              '
                              If(Jleft(0) and INV(2)=False) or(Jright(0) and INV(2)=True)
                                 If(STATE2=STANDING) or(SENS2=DROITE)
                                    STATE2=WALKING
                                    SENS2=GAUCHE
                                    J2_ANIM
                                 End If 
                                 If Y2>=0
                                    If X2>16
                                       If X2 mod 16=0
                                          If C(X-1,Y+1)=0
                                             Add X2,-VITX(2)
                                          End If 
                                       Else 
                                          Add X2,-VITX(2)
                                       End If 
                                    Else 
                                       X2=16
                                    End If 
                                 End If 
                              Else 
                                 '
                                 ' Droite 
                                 '
                                 If(Jright(0) and INV(2)=False) or(Jleft(0) and INV(2)=True)
                                    If(STATE2=STANDING) or(SENS2=GAUCHE)
                                       STATE2=WALKING
                                       SENS2=DROITE
                                       J2_ANIM
                                    End If 
                                    If Y2>=0
                                       If X2<224
                                          If C(X+1,Y+1)=0
                                             Add X2,VITX(2)
                                          End If 
                                       Else 
                                          X2=224
                                       End If 
                                    End If 
                                 End If 
                              End If 
                           End If 
                        End If 
                     End If 
                  End If 
                  '
                  If STATE2=JUMPING
                     '
                     Inc JUMP2
                     If(Y2 mod 16=15) and(Y>=1)
                        If(C(X,Y)=50) or(C((X2+15)/16,Y)=50)
                           JUMP2=17
                        End If 
                     End If 
                     '
                     ' Au cours du saut.
                     '
                     If JUMP2<17
                        Add Y2,-VITY(2)
                        '
                        ' Dep. gauche
                        '
                        If(Jleft(0) and INV(2)=False) or(Jright(0) and INV(2)=True)
                           If SENS2=DROITE
                              SENS2=GAUCHE
                              J2_ANIM
                           End If 
                           If Y2>=0
                              If X2>16
                                 If X2 mod 16=0
                                    If C(X-1,(Y2/16)+1)=0
                                       Add X2,-VITX(2)
                                    End If 
                                 Else 
                                    Add X2,-VITX(2)
                                 End If 
                              Else 
                                 X2=16
                              End If 
                           End If 
                        Else 
                           '
                           ' Dep droite 
                           '
                           If(Jright(0) and INV(2)=False) or(Jleft(0) and INV(2)=True)
                              If SENS2=GAUCHE
                                 SENS2=DROITE
                                 J2_ANIM
                              End If 
                              If Y2>=0
                                 If X2<224
                                    If Y2>=0
                                       If C(X+1,(Y2/16)+1)=0
                                          Add X2,VITX(2)
                                       End If 
                                    End If 
                                 Else 
                                    X2=224
                                 End If 
                              End If 
                           End If 
                        End If 
                     Else 
                        '
                        ' En haut du saut, doit-on retomber ?
                        '
                        If Y2<10
                           STATE2=FALLING
                           FALL2=0
                           LONGTEMPS2=1
                           J2_ANIM
                        Else 
                           If(C(X,Y+1)>0) or(C((X2+15)/16,Y+1)>0)
                              STATE2=FALLING
                              FALL2=0
                              LONGTEMPS2=1
                              J2_ANIM
                           Else 
                              If(C(X,Y+2)>0) or(C((X2+15)/16,Y+2)>0)
                                 STATE2=STANDING
                                 J2_ANIM
                              Else 
                                 STATE2=FALLING
                                 FALL2=0
                                 LONGTEMPS2=1
                                 J2_ANIM
                              End If 
                           End If 
                        End If 
                     End If 
                  Else 
                     '
                     If STATE2=FALLING
                        '
                        ' oooooooooooooooo No, I'm Fallin' 
                        '
                        Inc FALL2
                        '
                        ' Au cours de la descente
                        '
                        If FALL2<9
                           If FALL2>1
                              '
                              ' Dep gauche 
                              '
                              If(Jleft(0) and INV(2)=False) or(Jright(0) and INV(2)=True)
                                 If SENS2=DROITE
                                    SENS2=GAUCHE
                                    J2_ANIM
                                 End If 
                                 If Y2>=0
                                    If X2>16
                                       If X2 mod 16=0
                                          If C(X-1,Y+1)=0
                                             Add X2,-VITX(2)
                                          End If 
                                       Else 
                                          Add X2,-VITX(2)
                                       End If 
                                    Else 
                                       X2=16
                                    End If 
                                 End If 
                              Else 
                                 '
                                 ' Droite 
                                 '
                                 If(Jright(0) and INV(2)=False) or(Jleft(0) and INV(2)=True)
                                    If SENS2=GAUCHE
                                       SENS2=DROITE
                                       J2_ANIM
                                    End If 
                                    If Y2>=0
                                       If X2<224
                                          If Y2>=0
                                             If C(X+1,Y+1)=0
                                                Add X2,VITX(2)
                                             End If 
                                          End If 
                                       Else 
                                          X2=224
                                       End If 
                                    End If 
                                 End If 
                              End If 
                           End If 
                           '
                           Add Y2,VITY(2)
                           '
                        Else 
                           '
                           ' Redescendu d'une case: continuer à tomber ?
                           '
                           If Y2=223
                              STATE2=STANDING
                              LONGTEMPS2=0
                              J2_ANIM
                           Else 
                              If(C(X,Y+2)>0) or(C((X2+15)/16,Y+2)>0)
                                 STATE2=STANDING
                                 LONGTEMPS2=0
                                 J2_ANIM
                              Else 
                                 FALL2=0
                                 Inc LONGTEMPS2
                                 J2_ANIM
                              End If 
                           End If 
                        End If 
                     End If 
                  End If 
                  '
                  ' Poum ! 
                  '
                  If Fire(0) and(GLOUGLOU2=False)
                     If Amreg(5)=0
                        Amreg(5)=1
                        Amreg(7)=X2
                        Amreg(8)=Y2
                        Amreg(9)=SENS2
                        If(STATE2<>JUMPING) and(STATE2<>FALLING)
                           If SENS2=DROITE
                              Amal 2,"Let A=166;"
                           Else 
                              Amal 2,"Let A=167;"
                           End If 
                           Amal On 2
                           POUM2=1
                        End If 
                     End If 
                  Else 
                     If POUM2=1
                        POUM2=0
                        J2_ANIM
                     End If 
                  End If 
                  '
                  '++++++++++++++++++++++++++++++++++++
                  '
                  '
                  ' On prend un Bonus ?
                  '
                  If Bob Col(JOUEUR2,BONUS To BONUS)
                     Proc EFFET_BONUS[2]
                     Bob BONUS,,300,
                     BONUSWAIT=150
                  End If 
                  '
                  ' Un trésor ?
                  '
                  If Bob Col(JOUEUR2,TRESOR(1) To TRESOR(NBTRESORS))
                     If CARRY2=0
                        For I=1 To NBTRESORS
                           If Col(TRESOR(I))
                              CARRY2=I
                           End If 
                        Next I
                        TRESOR_TOMBE(CARRY2)=0
                        Bob Off TRESOR(CARRY2)
                        Proc UPDSCORE[2,100]
                        Sam Play VOIX2,1
                     End If 
                  End If 
                  '
                  ' On est en bas à droite ? 
                  '
                  If X2=224 and Y2=223
                     '
                     ' Ramène un trésor 
                     '
                     If CARRY2>0
                        CARRY2=0
                        Inc RAMENE2
                        Proc UPDSCORE[2,2000]
                        If RAMENE1+RAMENE2=NBTRESORS
                           PASSE=True
                        End If 
                        Sam Play VOIX2*2,2
                     End If 
                     '
                     ' Remonter le temps
                     '
                     If Joy(0)=2
                        If GLOUGLOU2=0
                           Limit Bob 2,0,0 To 256,224
                           Sam Play VOIX2,3
                           GLOUGLOU2=1
                           Amal 2,"Anim 0,(200,2)(201,2)(202,2)(201,2);"
                           Amal On 2
                           SENS2=DROITE
                        End If 
                        If HURRY=False
                           If MEMETEMPS
                              Inc TEMPS1
                              TEMPS1=Min(TEMPS1,MXTEMPS1)
                              Ink 4
                              TP=208-TEMPS1
                              Draw 241,TP+1 To 254,TP+1
                              Draw 241,TP To 254,TP
                              Draw 1,TP+1 To 14,TP+1
                              Draw 1,TP To 14,TP
                           Else 
                              Inc TEMPS2
                              TEMPS2=Min(TEMPS2,MXTEMPS2)
                              Ink 4
                              TP=208-TEMPS2
                              Draw 241,TP+1 To 254,TP+1
                              Draw 241,TP To 254,TP
                           End If 
                        End If 
                     Else 
                        If GLOUGLOU2=1
                           J2_ANIM
                        End If 
                     End If 
                  End If 
               End If 
               '
               ' --- Ceci doit toujours être exécuté ---
               '
               '
               ' On tire sur un monstre 
               '
               If Bob Col(TIR2,ENNEMI(1) To ENNEMI(NBMONSTRES))
                  For I=ENNEMI(1) To ENNEMI(5)
                     Exit If Col(I)
                  Next I
                  If DANSLESPOMMES(I-4)=False
                     DANSLESPOMMES(I-4)=200
                     Amal Freeze I
                     Bob TIR2,,320,
                     If SFXON
                        Shoot 
                     End If 
                     Proc UPDSCORE[2,10]
                  End If 
               End If 
               '
               ' On tire sur l'autre !
               '
               If Bob Col(TIR2,JOUEUR1 To JOUEUR1) and PARALIZED1=False
                  PARALIZED1=1
                  Amal Freeze 1
                  Amal On 10
                  Bob TIR2,,320,
                  Sam Play VOIX2,5
                  Proc UPDSCORE[2,50]
               End If 
               '
               ' Collision avec monstre, aïe !
               '
               If SUPER(2)=False
                  If Bob Col(JOUEUR2,ENNEMI(1) To ENNEMI(NBMONSTRES))
                     MORT2=True
                     Sam Play VOIX2,5
                     J2_ANIM
                     Bob Off TIR2
                     If CARRY2<>0
                        Bob TRESOR(CARRY2),X2-X2 mod 16,Y2,43
                        TRESOR_TOMBE(CARRY2)=True
                     End If 
                     UPDSCORE[0,0]
                  End If 
               End If 
               '
               ' Plus de temps !
               '
               If(TEMPS2=0) or(TEMPS1=0 and MEMETEMPS)
                  MORT2=True
                  Sam Play VOIX2,5
                  J2_ANIM
                  Bob Off TIR2
                  If CARRY2<>0
                     Bob TRESOR(CARRY2),X2-X2 mod 16,Y2,43
                     TRESOR_TOMBE(CARRY2)=True
                  End If 
                  UPDSCORE[0,0]
               End If 
            End If 
            '
            '================== Diverses gestions ==================== 
            '
            ' Le bonus ! 
            '
            Inc BONUSWAIT
            If BONUSWAIT=200
               BONUSWAIT=0
               BONUS
            End If 
            '
            ' Les trésors tombent
            '
            For I=1 To NBTRESORS
               If TRESOR_TOMBE(I)=True
                  T=TRESOR(I)
                  X=X Bob(T)
                  Y=Y Bob(T)
                  If Y mod 16<15
                     Bob T,,Y+1,
                  Else 
                     If Y=223
                        TRESOR_TOMBE(I)=0
                     Else 
                        If C(X/16,Y/16+2)>0
                           TRESOR_TOMBE(I)=0
                        Else 
                           Bob T,,Y+1,
                        End If 
                     End If 
                  End If 
               End If 
            Next I
            '
            ' Le temps descend (sauf en trainer) 
            '
            If CHEAT=False
               If Timer>12
                  Ink 0
                  If MORT1=False
                     Dec TEMPS1
                     If TEMPS1>0
                        Draw 1,207-TEMPS1 To 14,207-TEMPS1
                        Draw 1,208-TEMPS1 To 14,208-TEMPS1
                     End If 
                  End If 
                  If MORT2=False
                     If MEMETEMPS
                        If TEMPS1>0
                           Draw 241,207-TEMPS1 To 254,207-TEMPS1
                           Draw 241,208-TEMPS1 To 254,208-TEMPS1
                        End If 
                     Else 
                        Dec TEMPS2
                        If TEMPS2>0
                           Draw 241,207-TEMPS2 To 254,207-TEMPS2
                           Draw 241,208-TEMPS2 To 254,208-TEMPS2
                        End If 
                     End If 
                  End If 
                  Timer=0
               End If 
            End If 
            Add TEMPSTOTAL,2
            If HURRY=False
               If TEMPSTOTAL>=MXTEMPSTOTAL
                  HURRY=True
                  UPDSCORE[0,0]
                  ' Mettre un sample...
               End If 
            End If 
            '
            ' Ennemis paralizés ?
            '
            For I=ENNEMI(1) To ENNEMI(NBMONSTRES)
               If DANSLESPOMMES(I-4)=1
                  Amal On I
               End If 
               If DANSLESPOMMES(I-4)>0
                  Dec DANSLESPOMMES(I-4)
               End If 
            Next I
            '
            Gosub CLAVIER
            '
            Synchro 
            If MORT1=0
               Bob 1,X1,Y1,
            Else 
               Wait Vbl 
            End If 
            If MORT2=0
               Bob 2,X2,Y2,
            Else 
               Wait Vbl 
            End If 
            Wait Vbl 
            '
         Until((MORT1=True) and(MORT2=True)) or(PASSE=True)
      Else 
         '
         '*************************************************************** 
         '
         '                  Mode 1 joueur 
         '
         Repeat 
            '
            '- - - - - JOUEUR 1 - - - - - -
            '
            If MORT1=False
               '
               ' Où es-tu ? 
               '
               X1=X Bob(1)
               Y1=Y Bob(1)
               X=X1/16
               Y=Y1/16
               '
               If PARALIZED1>0
                  '
                  ' -------------------- Si on est paralizé
                  '
                  Inc PARALIZED1
                  If PARALIZED1=200
                     PARALIZED1=0
                     Amal Freeze 10
                     J1_ANIM
                     UPDSCORE[0,0]
                  End If 
                  '
                  ' Version allégée des routines KO=False
                  '
                  If STATE1=JUMPING
                     Inc JUMP1
                     If(Y1 mod 16=15) and(Y>=1)
                        If(C(X,Y)=50) or(C((X1+15)/16,Y)=50)
                           JUMP1=17
                        End If 
                     End If 
                     If JUMP1<17
                        Add Y1,-VITY(1)
                     Else 
                        If Y1<10
                           STATE1=FALLING
                           FALL1=0
                           LONGTEMPS1=1
                        Else 
                           If(C(X,Y+1)>0) or(C((X1+15)/16,Y+1)>0)
                              STATE1=FALLING
                              FALL1=0
                              LONGTEMPS1=1
                           Else 
                              If Y=13
                                 STATE1=STANDING
                              Else 
                                 If(C(X,Y+2)>0) or(C((X1+15)/16,Y+2)>0)
                                    STATE1=STANDING
                                 Else 
                                    STATE1=FALLING
                                    FALL1=0
                                    LONGTEMPS1=1
                                 End If 
                              End If 
                           End If 
                        End If 
                     End If 
                  Else 
                     If STATE1=FALLING
                        Inc FALL1
                        If FALL1<9
                           Add Y1,VITY(1)
                        Else 
                           If Y1=223
                              STATE1=STANDING
                              LONGTEMPS1=0
                           Else 
                              If(C(X,Y+2)>0) or(C((X1+15)/16,Y+2)>0)
                                 STATE1=STANDING
                                 LONGTEMPS1=0
                              Else 
                                 FALL1=0
                                 Inc LONGTEMPS1
                              End If 
                           End If 
                        End If 
                     End If 
                  End If 
                  '
               Else 
                  '
                  ' --------------------- Que fais-tu ?
                  '
                  If(STATE1=STANDING) or(STATE1=WALKING)
                     '
                     ' Test Collision : On Tombe ?
                     '
                     If Y1<223
                        If((C(X,Y+2)=0) and(C((X1+15)/16,Y+2)=0)) or(Y1<10)
                           STATE1=FALLING
                           FALL1=0
                           LONGTEMPS1=1
                           J1_ANIM
                        End If 
                     End If 
                     If STATE1<>FALLING
                        '
                        '
                        ' Cà bouge pas...
                        '
                        If(Joy(1)=0) or(Joy(1)=2)
                           If STATE1<>STANDING
                              STATE1=STANDING
                              J1_ANIM
                           End If 
                           EMPJP1=0
                        Else 
                           '
                           ' On saute 
                           '
                           If Jup(1)
                              If EMPJP1=0
                                 STATE1=JUMPING
                                 JUMP1=0
                                 J1_ANIM
                                 EMPJP1=1
                              End If 
                           Else 
                              '
                              EMPJP1=0
                              '
                              ' Gauche 
                              '
                              If(Jleft(1) and INV(1)=False) or(Jright(1) and INV(1)=True)
                                 If(STATE1=STANDING) or(SENS1=DROITE)
                                    STATE1=WALKING
                                    SENS1=GAUCHE
                                    J1_ANIM
                                 End If 
                                 If Y1>=0
                                    If X1>16
                                       If X1 mod 16=0
                                          If C(X-1,Y+1)=0
                                             Add X1,-VITX(1)
                                          End If 
                                       Else 
                                          Add X1,-VITX(1)
                                       End If 
                                    Else 
                                       X1=16
                                    End If 
                                 End If 
                              Else 
                                 '
                                 ' Droite 
                                 '
                                 If(Jright(1) and INV(1)=False) or(Jleft(1) and INV(1)=True)
                                    If(STATE1=STANDING) or(SENS1=GAUCHE)
                                       STATE1=WALKING
                                       SENS1=DROITE
                                       J1_ANIM
                                    End If 
                                    If Y1>=0
                                       If X1<224
                                          If C(X+1,Y+1)=0
                                             Add X1,VITX(1)
                                          End If 
                                       Else 
                                          X1=224
                                       End If 
                                    End If 
                                 End If 
                              End If 
                           End If 
                        End If 
                     End If 
                  End If 
                  '
                  If STATE1=JUMPING
                     '
                     Inc JUMP1
                     If(Y1 mod 16=15) and(Y>=1)
                        If(C(X,Y)=50) or(C((X1+15)/16,Y)=50)
                           JUMP1=17
                        End If 
                     End If 
                     '
                     ' Au cours du saut.
                     '
                     If JUMP1<17
                        Add Y1,-VITY(1)
                        '
                        ' Dep. gauche
                        '
                        If(Jleft(1) and INV(1)=False) or(Jright(1) and INV(1)=True)
                           If SENS1=DROITE
                              SENS1=GAUCHE
                              J1_ANIM
                           End If 
                           If Y1>=0
                              If X1>16
                                 If X1 mod 16=0
                                    If C(X-1,(Y1/16)+1)=0
                                       Add X1,-VITX(1)
                                    End If 
                                 Else 
                                    Add X1,-VITX(1)
                                 End If 
                              Else 
                                 X1=16
                              End If 
                           End If 
                        Else 
                           '
                           ' Dep droite 
                           '
                           If(Jright(1) and INV(1)=False) or(Jleft(1) and INV(1)=True)
                              If SENS1=GAUCHE
                                 SENS1=DROITE
                                 J1_ANIM
                              End If 
                              If Y1>=0
                                 If X1<224
                                    If C(X+1,(Y1/16)+1)=0
                                       Add X1,VITX(1)
                                    End If 
                                 Else 
                                    X1=224
                                 End If 
                              End If 
                           End If 
                        End If 
                     Else 
                        '
                        ' En haut du saut, doit-on retomber ?
                        '
                        If Y1<10
                           STATE1=FALLING
                           FALL1=0
                           LONGTEMPS1=1
                           J1_ANIM
                        Else 
                           If(C(X,Y+1)>0) or(C((X1+15)/16,Y+1)>0)
                              STATE1=FALLING
                              FALL1=0
                              LONGTEMPS1=1
                              J1_ANIM
                           Else 
                              If Y=13
                                 STATE1=STANDING
                                 J1_ANIM
                              Else 
                                 If(C(X,Y+2)>0) or(C((X1+15)/16,Y+2)>0)
                                    STATE1=STANDING
                                    J1_ANIM
                                 Else 
                                    STATE1=FALLING
                                    FALL1=0
                                    LONGTEMPS1=1
                                    J1_ANIM
                                 End If 
                              End If 
                           End If 
                        End If 
                     End If 
                  Else 
                     '
                     If STATE1=FALLING
                        '
                        ' oooooooooooooooo No, I'm Fallin' 
                        '
                        Inc FALL1
                        '
                        ' Au cours de la descente
                        '
                        If FALL1<9
                           If FALL1>1
                              '
                              ' Dep gauche 
                              '
                              If(Jleft(1) and INV(1)=False) or(Jright(1) and INV(1)=True)
                                 If SENS1=DROITE
                                    SENS1=GAUCHE
                                    J1_ANIM
                                 End If 
                                 If Y1>=0
                                    If X1>16
                                       If X1 mod 16=0
                                          If C(X-1,Y+1)=0
                                             Add X1,-VITX(1)
                                          End If 
                                       Else 
                                          Add X1,-VITX(1)
                                       End If 
                                    Else 
                                       X1=16
                                    End If 
                                 End If 
                              Else 
                                 '
                                 ' Droite 
                                 '
                                 If(Jright(1) and INV(1)=False) or(Jleft(1) and INV(1)=True)
                                    If SENS1=GAUCHE
                                       SENS1=DROITE
                                       J1_ANIM
                                    End If 
                                    If Y1>=0
                                       If X1<224
                                          If C(X+1,Y+1)=0
                                             Add X1,VITX(1)
                                          End If 
                                       Else 
                                          X1=224
                                       End If 
                                    End If 
                                 End If 
                              End If 
                           End If 
                           '
                           Add Y1,VITY(1)
                           '
                        Else 
                           '
                           ' Redescendu d'une case: continuer à tomber ?
                           '
                           If Y1=223
                              STATE1=STANDING
                              LONGTEMPS1=0
                              J1_ANIM
                           Else 
                              If(C(X,Y+2)>0) or(C((X1+15)/16,Y+2)>0)
                                 STATE1=STANDING
                                 LONGTEMPS1=0
                                 J1_ANIM
                              Else 
                                 FALL1=0
                                 Inc LONGTEMPS1
                                 J1_ANIM
                              End If 
                           End If 
                        End If 
                     End If 
                  End If 
                  '
                  ' Poum ! 
                  '
                  If Fire(1) and(GLOUGLOU1=False)
                     If Amreg(0)=0
                        Amreg(0)=1
                        Amreg(2)=X1
                        Amreg(3)=Y1
                        Amreg(4)=SENS1
                        If(STATE1<>JUMPING) and(STATE1<>FALLING)
                           If SENS1=DROITE
                              Amal 1,"Let A=188;"
                           Else 
                              Amal 1,"Let A=189;"
                           End If 
                           Amal On 1
                           POUM1=1
                        End If 
                     End If 
                  Else 
                     If POUM1=1
                        POUM1=0
                        J1_ANIM
                     End If 
                  End If 
                  '
                  '++++++++++++++++++++++++++++++++++++
                  '
                  '
                  ' On prend un Bonus ?
                  '
                  If Bob Col(JOUEUR1,BONUS To BONUS)
                     Proc EFFET_BONUS[1]
                     Bob BONUS,,300,
                     BONUSWAIT=150
                  End If 
                  '
                  ' Un trésor ?
                  '
                  If Bob Col(JOUEUR1,TRESOR(1) To TRESOR(NBTRESORS))
                     If CARRY1=0
                        For I=1 To NBTRESORS
                           If Col(TRESOR(I))
                              CARRY1=I
                           End If 
                        Next I
                        TRESOR_TOMBE(CARRY1)=0
                        Bob Off TRESOR(CARRY1)
                        Proc UPDSCORE[1,100]
                        Sam Play VOIX1,1
                     End If 
                  End If 
                  '
                  ' On est en bas à gauche ? 
                  '
                  If X1=16 and Y1=223
                     '
                     ' Ramène un trésor 
                     '
                     If CARRY1>0
                        CARRY1=0
                        Inc RAMENE1
                        Proc UPDSCORE[1,2000]
                        If RAMENE1+RAMENE2=NBTRESORS
                           PASSE=True
                        End If 
                        Sam Play VOIX1*8,2
                     End If 
                     '
                     ' Remonter le temps
                     '
                     If Joy(1)=2
                        If GLOUGLOU1=0
                           Limit Bob 1,0,0 To 256,224
                           Sam Play VOIX1,3
                           GLOUGLOU1=1
                           Amal 1,"Anim 0,(209,2)(210,2)(211,2)(210,2);"
                           Amal On 1
                           SENS1=GAUCHE
                        End If 
                        If HURRY=False
                           Inc TEMPS1
                           TEMPS1=Min(TEMPS1,MXTEMPS1)
                           Ink 3
                           TP=208-TEMPS1
                           Draw 1,TP+1 To 14,TP+1
                           Draw 1,TP To 14,TP
                        End If 
                     Else 
                        If GLOUGLOU1=1
                           J1_ANIM
                        End If 
                     End If 
                  End If 
               End If 
               '
               ' --- Ceci doit toujours être exécuté ---
               '
               '
               ' On tire sur un monstre 
               '
               If Bob Col(TIR1,ENNEMI(1) To ENNEMI(NBMONSTRES))
                  For I=ENNEMI(1) To ENNEMI(5)
                     Exit If Col(I)
                  Next I
                  If DANSLESPOMMES(I-4)=False
                     DANSLESPOMMES(I-4)=200
                     Amal Freeze I
                     Bob TIR1,,320,
                     If SFXON
                        Shoot 
                     End If 
                     Proc UPDSCORE[1,10]
                  End If 
               End If 
               '
               ' Collision avec monstre, aïe !
               '
               If SUPER(1)=False
                  If Bob Col(JOUEUR1,ENNEMI(1) To ENNEMI(NBMONSTRES))
                     MORT1=True
                     Sam Play VOIX1,4
                     J1_ANIM
                     Bob Off TIR1
                     If CARRY1<>0
                        Bob TRESOR(CARRY1),X1-X1 mod 16,Y1,43
                        TRESOR_TOMBE(CARRY1)=True
                     End If 
                     UPDSCORE[0,0]
                  End If 
               End If 
               '
               ' Plus de temps !
               '
               If TEMPS1=0
                  MORT1=True
                  Sam Play VOIX1,4
                  J1_ANIM
                  Bob Off TIR1
                  If CARRY1<>0
                     Bob TRESOR(CARRY1),X1-X1 mod 16,Y1,43
                     TRESOR_TOMBE(CARRY1)=True
                  End If 
                  UPDSCORE[0,0]
               End If 
            End If 
            '
            '================== Diverses gestions ==================== 
            '
            ' Le bonus ! 
            '
            Inc BONUSWAIT
            If BONUSWAIT=200
               BONUSWAIT=0
               BONUS
            End If 
            '
            ' Les trésors tombent
            '
            For I=1 To NBTRESORS
               If TRESOR_TOMBE(I)=True
                  T=TRESOR(I)
                  X=X Bob(T)
                  Y=Y Bob(T)
                  If Y mod 16<15
                     Bob T,,Y+1,
                  Else 
                     If Y=223
                        TRESOR_TOMBE(I)=0
                     Else 
                        If C(X/16,Y/16+2)>0
                           TRESOR_TOMBE(I)=0
                        Else 
                           Bob T,,Y+1,
                        End If 
                     End If 
                  End If 
               End If 
            Next I
            '
            ' Le temps descend (sauf en trainer) 
            '
            If CHEAT=False
               If Timer>12
                  Ink 0
                  If MORT1=False
                     Dec TEMPS1
                     If TEMPS1>0
                        Draw 1,207-TEMPS1 To 14,207-TEMPS1
                        Draw 1,208-TEMPS1 To 14,208-TEMPS1
                     End If 
                  End If 
                  ' Barre rouge aussi
                  Timer=0
               End If 
            End If 
            Add TEMPSTOTAL,2
            If HURRY=False
               If TEMPSTOTAL>=MXTEMPSTOTAL
                  HURRY=True
                  UPDSCORE[0,0]
                  ' Mettre un sample...
               End If 
            End If 
            '
            ' Ennemis paralizés ?
            '
            For I=ENNEMI(1) To ENNEMI(NBMONSTRES)
               If DANSLESPOMMES(I-4)=1
                  Amal On I
               End If 
               If DANSLESPOMMES(I-4)>0
                  Dec DANSLESPOMMES(I-4)
               End If 
            Next I
            '
            Gosub CLAVIER
            '
            Synchro 
            Bob 1,X1,Y1,
            Wait Vbl 
            '
         Until(MORT1=True) or(PASSE=True)
      End If 
      '
      '************************************************************
      '
      Wait Vbl 
      Amal Off 
      MUSIQUE_OFF
      Wait 4
      Fade 2
      Wait 30
      Wait Vbl 
      Bob Off 
      Wait Vbl 
      '
      ' On a gagné ! 
      '
      If PASSE
         If SFXON
            If K$<>"+"
               Sam Play %1111,7
            Else 
               Sam Play %1111,6
            End If 
         End If 
         If RAMENE1>RAMENE2
            Inc VICT1
            Proc _MESSAGE[MSG_WINNER,1]
         Else 
            If RAMENE2>RAMENE1
               Inc VICT2
               Proc _MESSAGE[MSG_WINNER,2]
            Else 
               Proc _MESSAGE[MSG_EGALITE,0]
            End If 
         End If 
         If LEVEL mod 5=0
            _MESSAGE[MSG_BONUS,0]
            Proc JEU[LEVEL/5]
         Else 
            If PUBON
               Proc PUB[LEVEL]
            End If 
         End If 
         Inc LEVEL
         _MESSAGE[MSG_GET_READY,0]
         CHARGER[LEVEL]
         Sam Play 8
         Timer=0
         Repeat 
            Multi Wait 
         Until Fire(1) or Fire(0) or Timer>=400
         Fade 1
         Wait 15
         Cls 0
      Else 
         '
         ' On a perdu ! 
         '
         Dec CREDITS
         If CREDITS>0
            _MESSAGE[MSG_OH_NO,0]
         End If 
      End If 
      Wait Vbl 
   Wend 
   '
   '************************************
   '
   ' Fin de la partie 
   '
   Proc _MESSAGE[MSG_GAME_OVER,0]
   Erase 9
   '
   ' Bon score ?
   '
   For I=1 To 10
      If SCORE1>HISCORE(I)
         For J=10 To I+1 Step -1
            HISCORE(J)=HISCORE(J-1)
            HISCORE$(J)=HISCORE$(J-1)
         Next J
         HISCORE(I)=SCORE1
         HISCORE$(I)=NAME1$
         Exit 
      End If 
   Next I
   For I=1 To 10
      If SCORE2>HISCORE(I)
         For J=10 To I+1 Step -1
            HISCORE(J)=HISCORE(J-1)
            HISCORE$(J)=HISCORE$(J-1)
         Next J
         HISCORE(I)=SCORE2
         HISCORE$(I)=NAME2$
         Exit 
      End If 
   Next I
   '
   ' Affiche les scores 
   '
   Cls 0
   Erase 3
   Erase 5
   HALL_OF_FAME
   Fade 1,0,$FFF,$AAA,$555
   Wait 15
   '
   ' Sauvegarde 
   '
   If HISAVEON
      SAUVE_SCORES
   End If 
   '
   While Fire(1) or Fire(0)
      Multi Wait 
   Wend 
   Repeat 
      Multi Wait 
   Until Fire(1) or Fire(0)
   Fade 1
   Wait 15
   '
   ' Et c'est reparti pour un tour !
   '
Loop 
'
'------------
'
CLAVIER:
'
' Le clavier...
'
K$=Inkey$
If K$<>""
   K$=Upper$(K$)
   ' Esc
   If Asc(K$)=27
      If Key Shift<>0
         Proc FIN
      Else 
         MORT1=True
         MORT2=True
         CREDITS=1
      End If 
   End If 
   ' Pause
   If K$="P"
      Amal Freeze 
      Repeat 
         Multi Wait 
      Until Upper$(Inkey$)="P"
      Amal On 1
      Amal On 3
      If PARTIE=2
         Amal On 2
         Amal On 4
      End If 
      For I=5 To NBMONSTRES+4
         If DANSLESPOMMES(I-4)=0
            Amal On I
         End If 
      Next I
   End If 
   ' Mémoire
   If K$="M"
      Ink 1,0
      Text 10,8,"Fast:"+Str$(Fast Free)+" / Chip:"+Str$(Chip Free)+"  "
   End If 
   ' Cheat On/Off 
   If K$="§"
      CHEAT= Not(CHEAT)
   End If 
   ' Crédits +++
   If K$="C"
      If CHEAT=True
         CREDITS=Min(99,CREDITS+1)
         L$=Str$(CREDITS)-" "
         If Len(L$)=1
            L$="0"+L$
         End If 
         Ink 22,0
         Text 280,218,L$
      End If 
   End If 
   ' Invincibilité
   If K$="I"
      If CHEAT=True
         SUPER(1)= Not(SUPER(1))
      End If 
   End If 
   If K$="J"
      If CHEAT=True
         SUPER(2)= Not(SUPER(2))
      End If 
   End If 
   ' NeXT 
   If K$="+"
      If CHEAT=True
         PASSE=True
      End If 
   End If 
End If 
Return 
'
'***************** PROCEDURES **************** 
'
Procedure INIT
   '
   ' Charge les données principales + affiche imgs + lance zik
   ' pour faire patienter...
   ' Sans oublier ce merveilleux sample:
   ' 'First, there was nothing. Now, PowerMob Productions presents
   ' a T.H.A. Design game: Twin-Baston !' 
   '
   ' Contenu des banques mémoire pendant le programme.
   '
   ' Banques:  1 = sprites
   ' ~~~~~~~~  2 = fonte de caractères ( sous forme sprite )
   '           3 = musique
   '           5 = samples
   '           9 = img intro, prés, options, fond du jeu. 
   '          10 = img intro, prés, options, pubs, copyright. 
   '          11 = img intro, options.  
   '
   Shared ZIKPLAYING,QUELLEZIK,ZIKVOL
   FAST=-1
   '
   ' Met tout propre
   '
   Close Editor 
   Close Workbench 
   While Screen>=0
      Screen Close Screen
   Wend 
   For I=1 To 16
      Erase I
   Next I
   '
   Colour Back 0
   '
   Auto View Off 
   Screen Open 3,320,200,2,0
   Screen Display 3,128,50,320,200
   Palette $0,$FFF
   Curs Off 
   Cls 0
   Hide On 
   If FAST=0
      Proc _COPYRIGHT
   End If 
   '
   CHARGE_PREFS
   Load "Twin:Zik-Intro.Abk",3
   '
   If FAST=0
      Load "Twin:Img-Intro1a.Abk",9
      Load "Twin:Img-Intro1b.Abk",10
      Load "Twin:Img-Intro1c.Abk",11
      '
      For S=0 To 2
         Unpack S+9 To S
         Erase S+9
         Screen S
         Screen Hide S
      Next S
      Screen 2
      For I=0 To 31
         Colour I,0
      Next I
      Screen 3
      View 
      Auto View On 
      '
      Reserve As Chip Work 5,36662
      Reserve As Chip Work 6,53496
      Reserve As Chip Work 7,39876
      Reserve As Chip Work 8,19578
      Bload "Twin:Samples-First1",5
      Bload "Twin:Samples-First2",6
      Bload "Twin:Samples-First3",7
      Bload "Twin:Samples-First4",8
      '
      Volume 0
      Shoot 
      Volume 63
      Led On 
      '
      Plot 160,120,1
      Sam Raw 15,Start(5),Length(5),14000
      Wait 140
      Erase 5
      Sam Raw 15,Start(6),Length(6),14000
      Wait 60
      Plot 160,120,0
      Centre At(,5)+"POWERMOB PRODUCTIONS"
      Wait 130
      Erase 6
      Sam Raw 15,Start(7),Length(7),14000
      Wait 25
      Centre At(,10)+"THEVENOT"
      Wait 15
      Centre At(,11)+"H I L P I P R E"
      Wait 15
      Centre At(,12)+"A   M   I   C   E   L"
      Wait 65
      Fade 1
      Wait 15
   End If 
   Screen Close 3
   Wait 10
   If FAST=0
      Erase 7
      Wait Vbl 
      Sam Raw 15,Start(8),Length(8),7000
      Wait Vbl 
      Screen Offset 0,320,0
      Screen Show 0
      For X=320 To 0 Step -8
         Screen Offset 0,X,0
         Wait Vbl 
      Next X
      Screen Show 1
      For X=0 To 320 Step 8
         Screen Offset 1,X,0
         Wait Vbl 
      Next X
      Wait 20
      Screen Show 2
      Fade 1,$0,$FFF,$1,$2,$3,$4,$5,$6,$7,$8,$9,$A,$B,$C,$D,$E,$F,$1F,$12F,$23F,$34F,$45F,$56F,$67F,$78F,$89F,$9AF,$ABF,$BCF,$CDF,$DEF,$EFF
      Wait 80
      Erase 8
   End If 
   '
   Voice 15
   Mvolume ZIKVOL
   Led Off 
   Music 1
   ZIKPLAYING=True
   QUELLEZIK=1
   '
   Load "Twin:Lettres.Abk"
   Bank Swap 1,2
   Load "Twin:Def-Sprites.Abk"
   CHARGE_SCORES
   Load "Twin:Img-Intro2.Abk",9
   Load "Twin:Img-Intro3.Abk",10
   '
End Proc
Procedure FIN
   '
   ' Serait-ce la fin ? 
   '
   Erase 2
   Music Off 
   Erase 3
   Erase 5
   Erase 9
   Erase 10
   Amal Off 
   Bob Off 
   Erase 1
   End 
End Proc
'
Procedure _COPYRIGHT
   '
   ' © © © © © © © © ® ® ® ® ® ® ® ®
   '
   Load "Twin:Img-Copyright.Abk",10
   YDISPLAY=75
   Unpack 10 To 7
   For Y=2 To Screen Height/2 Step 8
      Screen Display 7,,YDISPLAY+Screen Height/2-Y,,Y*2
      Screen Offset 7,,Screen Height/2-Y
      View 
      Wait Vbl 
   Next Y
   T=Timer
   Repeat 
      Multi Wait 
   Until Len(Inkey$) or Fire(1) or Fire(0) or Timer-T>200
   For Y=Screen Height/2 To 0 Step -8
      Screen Display 7,,YDISPLAY+Screen Height/2-Y,,Y*2
      Screen Offset 7,,Screen Height/2-Y
      View 
      Wait Vbl 
   Next Y
   Screen Close 7
   Erase 10
End Proc
Procedure PRESENTATION
   '
   ' Affiche en boucle des imgs avec zik, attend Fire.
   '
   Shared ZIKVOL,ZIKPLAYING,V$
   '
   '-------- INITS -------- 
   '
   If Length(9)=0
      Load "Twin:Img-Intro2.abk",9
      Load "Twin:Img-Intro3.abk",10
   End If 
   Auto View Off 
   Screen 0
   Cls 0
   Screen Open 3,336,8,2,Lowres
   Screen Display 3,128,292,320,8
   Palette $0,$AAA
   Set Rainbow 0,1,16,"","",""
   Restore CLR
   For I=0 To 9
      Read COUCOU
      Rain(0,I)=COUCOU
   Next I
   Rainbow 0,0,291,11
   CLR:
   Data $444,$777,$AAA,$CCC,$FFF,$FFF,$CCC,$AAA,$777,$444
   Cls 0
   Screen 0
   HALL_OF_FAME
   Unpack 9 To 1
   Unpack 10 To 2
   For I=0 To 2
      Screen I
      Screen Display I,128,40,,
      Palette 0,0,0,0
      Screen Hide I
   Next I
   Screen To Front 3
   Auto View On 
   '
   Restore TXT
   T$=""
   Do 
      Read A$
      Exit If A$="***"
      T$=T$+A$
   Loop 
   '
   '
   If ZIKPLAYING=False
      Proc MUSIQUE[1,ZIKVOL]
   End If 
   Wait Vbl 
   '
   '--------- BOUCLE ---------
   '
   SPEED=2
   Screen 3
   Def Scroll 1,0,0 To 336,8,-SPEED,0
   Ink 1
   Timer=0
   PRES_IMG=1
   Screen Show 1
   Screen 1
   Fade 1,$0,$FFF,$AAA,$555
   Screen 3
   Do 
      For X=1 To Len(T$)
         L$=Mid$(T$,X,1)
         If L$="§"
            Inc X
            L$=Mid$(T$,X,1)
            If L$="1"
               ' Eric sur sa moto 
               SPEED=1
            End If 
            If L$="2"
               ' Slow 
               SPEED=2
            End If 
            If L$="4"
               ' Medium 
               SPEED=4
            End If 
            If L$="8"
               ' Fast 
               SPEED=8
            End If 
            Def Scroll 1,0,0 To 336,8,-SPEED,0
         Else 
            Text 320,6,L$
            For Y=1 To 8/SPEED
               Scroll 1
               Wait Vbl 
               Exit If(Fire(1)) or(Fire(0)),3
            Next Y
         End If 
         If Timer>=500
            If Timer>=520
               Screen Hide PRES_IMG
               Add PRES_IMG,1,0 To 2
               Screen Show PRES_IMG
               Screen PRES_IMG
               Fade 1,0,$FFF,$AAA,$555
               Timer=0
            Else 
               Screen PRES_IMG
               Fade 1
            End If 
            Screen 3
         End If 
      Next X
   Loop 
   '
   '---------- FIN -----------
   '
   While Fire(1) or Fire(0)
      Multi Wait 
   Wend 
   Screen 3
   Rainbow Del 
   Fade 1
   Wait 15
   Screen PRES_IMG
   Fade 1
   Wait 15
   Screen Close 3
   Screen Close 2
   Screen Close 1
   Screen 0
   Screen To Front 0
   Flash Off 
   Cls 0
   Screen Show 0
   '
   ' Scrolltext 
   '
   ' §1 / §2 / §4 / §8 = Vitesse
   '
   TXT:
   Data "§2TWIN-BASTON PREVIEW  -  LISEZ CE SCROLL !!                 "
   Data "1. Twin-Baston   2. Greetings   3. Mr Reed vous parle   "
   Data "4. Eagle vous parle   5. Et enfin Vuzz !"
   Data "        *  *  *        "
   Data "§4Le groupe PowerMob est fier de vous présenter TWIN-BASTON !  "
   Data "Ceci est une pré-version ( v "+V$+" ) ne la distribuez pas...  "
   Data "Il y a des infos sur le cheat-mode à la fin du scroll... Pouf, pouf !   "
   Data "Le manuel du jeu est sous la forme d'un fichier 'Twin.Doc' présent"
   Data "sur ce disque ( passez par le Workbench par exemple ) - Attention ! "
   Data "Ce jeu est en Shareware, il est librement distribuable, mais si vous "
   Data "l'aimez bien et que vous décidez de le garder vous avez l'obligation "
   Data "morale d'envoyer une petite donation aux auteurs pour les encourager. "
   Data "Pour vous motiver, on vous envoie, contre la ridicule somme de "
   Data "40 FF ( ou plus si vous avez les moyens ) la version complète de "
   Data "TWIN BASTON, c'est à dire avec deux fois plus de tableaux, d'images, "
   Data "de bruitages, d'humour... Vous recevrez également un EDITEUR DE TABLEAUX !  "
   Data "Vous pourrez créer vos propres tableaux ultra-facilement !  Et toujours "
   Data "dans le même paquet, une de nos autres disquettes au choix ( voir doc )."
   Data "        *  *  *        "
   Data "Nous saluons nos quelques connaissances:  N.G.C. ( Salut Sébastien ) - "
   Data "Digital Creations ...  Et en dehors de la scène, des 'Greetings' plus "
   Data "personnels: Nico à Aix, Alain, et dans la classe: Tanguy ( Crispin ) - "
   Data "Jérôme - Matthieu ( çà marche, les accords ? ) - et les autres... §8"
   Data "J'ai pas cité les filles, car j'en n'ai rencontré aucune qui perde "
   Data "son temps à lire des scrolltexts à la con...    "
   Data "Merci à §4 M. L'Haridon ( mon prof d'info ) également... "
   Data "        *  *  *        §2"
   Data "C'est maintenant Mr Reed §4 ( moi ) qui va vous remplir quelques Ko de "
   Data "conneries à lire si vous vous emmerdez... Je me présente, d'abord: "
   Data "A l'heure actuelle ( 23.10.1992 ) , j'ai 16 ans, je suis en 1ère S, et "
   Data "je programme pendant mon temps libre ( çà fait pas beaucoup ). Je tape "
   Data "ce texte sur mon petit A500 avec son petit Mo de RAM et ses 2 petits "
   Data "drives... Snif... Si seulement j'avais un 4000... Enfin, bon c'est mieux "
   Data "qu'un ATARY 519 STZ, ou qu'un PC... Si vous voulez me contacter vous "
   Data "pouvez m'écrire à l'addresse suivante: §1 Ronan Amicel / 3 place de la "
   Data " Liberté / 22000 ST BRIEUC / FRANCE . . . . §2 La 1ère fois, mettez un "
   Data "timbre pour la réponse, parce que, vu que je vais recevoir des millions "
   Data "de lettres de tous mes admirateurs, et que je suis très pauvre, parce que "
   Data "vous n'êtes qu'une bande de malhonnêtes qui payez pas vos 40 FF, eh bien je "
   Data "pourrais pas acheter pour 2.412.524 FF de timbres, non mais ! Au fait vous "
   Data "n'avez pas à hésiter à écrire, je répondrais à toutes les lettres, sauf "
   Data "celles du type 'Votre jeu ne veut pas marcher sur mon Amscrad' ou 'Echange "
   Data "hot news'  §4. Ok... Maintenant je vais dire plein de conneries sur Vuzz : "
   Data "Il a des cheveux longs et blonds, il s'habille tout de noir, et ses fringues "
   Data "sont couvertes des poils de son clebs. Son Amiga 500 et actuellement en réparation "
   Data "depuis 2 semaines ( il DEVRAIT l'avoir dans 1 semaine normalement ) ce qui "
   Data "fait qu'il n'a pas encore fini la musique. En parlant de musique, il s'est "
   Data "acheté une basse que j'ai vue tout à l'heure avec Matthieu, qui lui s'est offert "
   Data "une guitare. On attend le concert du duo de choc ! Il a aussi une très moche "
   Data "'moto' bleue de marque MBK ( TM, (c), (R), etc... ) qui fait plein de bruit "
   Data "sans aller vite pour autant... Il passe donc son temps avec François, un autre "
   Data "possesseur de mob ( pas une POWER-Mob non plus ) , qui ressemble sur son engin "
   Data "à un crapaud sur une boîte d'allumettes... En fait, avec son casque, il "
   Data "ressemble beaucoup à Zool ( TM ) si vous voyez...    - Bon, j'en ai marre"
   Data " d'écrire, je vais regarder Nulle Part Ailleurs... - "
   Data "                         - A SUIVRE -                            "
   Data "***"
   '
End Proc
Procedure OPTIONS2
   '
   ' Toutes nouvelles options (10.10.92)
   '
   Shared ZIKON,ZIKVOL,SFXON,SFXVOL,PUBON,NAME1$,NAME2$,HISAVEON,MSG_OPTIONS
   Shared PARTIE,DIFF,CASE,J
   '
   DIFF=2
   '
   Load "Twin:Img-Optionsa.abk",9
   Load "Twin:Img-Optionsb.abk",10
   Load "Twin:Img-Optionsc.abk",11
   Bank Swap 1,4
   Load "Twin:Options.Abk"
   Make Mask 
   '
   Screen Close 0
   Auto View Off 
   Unpack 9 To 0
   Erase 9
   Unpack 10 To 1
   Erase 10
   Unpack 11 To 2
   Erase 11
   Ink 0
   Bar 163,7 To 202,46
   Paste Bob 163,7,9+PUBON
   Bar 218,7 To 257,46
   Paste Bob 218,7,9+DIFF
   Screen 1
   Ink 4,2
   Text 12,29,Left$(NAME1$,12)
   Ink 4,11
   Text 208,29,Left$(NAME2$,12)
   Screen 0
   If PARTIE=1
      Paste Bob 16,2,31
   Else 
      Paste Bob 240,2,32
   End If 
   Wait Vbl 
   Auto View On 
   View 
   '--------------
   CASE=PARTIE
   FINISHED=False
   Repeat 
      J=Joy(1)
      If J>0
         If J=16
            If CASE=1
               PARTIE=1
               FINISHED=True
            End If 
            If CASE=2
               PARTIE=2
               FINISHED=True
            End If 
            If CASE=3
               ' Entrer nom J1
            End If 
            If CASE=4
               ' Entrer nom J2
            End If 
            If CASE=5
               ' Zikvol 
            End If 
            If CASE=6
               ' Sfxvol 
            End If 
            If CASE=7
               ' Jeuxvol
            End If 
            If CASE=8
               PUBON= Not(PUBON)
               Screen 2
               Ink 0
               Wait Vbl 
               Bob Clear 
               Bar 163,7 To 202,46
               Paste Bob 163,7,9+PUBON
               Bob Draw 
            End If 
            If CASE=9
               Add DIFF,1,1 To 3
               Screen 2
               Ink 0
               Wait Vbl 
               Bob Clear 
               Bar 218,7 To 257,46
               Paste Bob 218,7,9+DIFF
               Bob Draw 
            End If 
            If CASE=10
               SAUVE_PREFS
            End If 
            If CASE=11
               ' Code 
            End If 
         Else 
            Proc NOUVELLE_CASE
         End If 
         '
         Repeat 
            Wait Vbl 
         Until Joy(1)=0
      End If 
   Until FINISHED
   '--------------
   Auto View Off 
   Bob Off 
   Bank Swap 1,4
   Erase 4
   Screen Close 2
   Screen Close 1
   Screen Close 0
   Screen Open 0,320,256,32,0
   Screen Display 0,128,40,320,256
   Flash Off 
   Curs Off 
   Hide On 
   Get Sprite Palette 
   Cls 0
   Autoback 1
   Wait Vbl 
   Auto View On 
   View 
   '
   MUSIQUE_OFF
   Erase 3
   '
End Proc
Procedure NOUVELLE_CASE
   Shared CASE,J
   OLD=CASE
   L=4
   R=8
   U=1
   D=2
   If CASE=1
      If J=D
         CASE=3
      Else 
         If J=R
            CASE=2
         End If 
      End If 
   Else 
      If CASE=2
         If J=L
            CASE=1
         Else 
            If J=D
               CASE=4
            End If 
         End If 
      Else 
         If CASE=3
            If J=U
               CASE=1
            Else 
               If J=R
                  CASE=4
               Else 
                  If J=D
                     CASE=5
                  End If 
               End If 
            End If 
         Else 
            If CASE=4
               If J=L
                  CASE=3
               Else 
                  If J=U
                     CASE=2
                  Else 
                     If J=D
                        CASE=10
                     End If 
                  End If 
               End If 
            Else 
               If CASE=5
                  If J=U
                     CASE=3
                  Else 
                     If J=D
                        CASE=6
                     Else 
                        If J=R
                           CASE=8
                        End If 
                     End If 
                  End If 
               Else 
                  If CASE=6
                     If J=U
                        CASE=5
                     Else 
                        If J=D
                           CASE=7
                        Else 
                           If J=R
                              CASE=8
                           End If 
                        End If 
                     End If 
                  Else 
                     If CASE=7
                        If J=U
                           CASE=6
                        Else 
                           If J=R
                              CASE=11
                           End If 
                        End If 
                     Else 
                        If CASE=8
                           If J=U
                              CASE=4
                           Else 
                              If J=D
                                 CASE=11
                              Else 
                                 If J=R
                                    CASE=9
                                 Else 
                                    If J=L
                                       CASE=5
                                    End If 
                                 End If 
                              End If 
                           End If 
                        Else 
                           If CASE=9
                              If J=U
                                 CASE=4
                              Else 
                                 If J=D
                                    CASE=11
                                 Else 
                                    If J=R
                                       CASE=10
                                    Else 
                                       If J=L
                                          CASE=8
                                       End If 
                                    End If 
                                 End If 
                              End If 
                           Else 
                              If CASE=10
                                 If J=U
                                    CASE=4
                                 Else 
                                    If J=D
                                       CASE=11
                                    Else 
                                       If J=L
                                          CASE=9
                                       End If 
                                    End If 
                                 End If 
                              Else 
                                 If CASE=11
                                    If J=U
                                       CASE=8
                                    Else 
                                       If J=L
                                          CASE=7
                                       End If 
                                    End If 
                                 End If 
                              End If 
                           End If 
                        End If 
                     End If 
                  End If 
               End If 
            End If 
         End If 
      End If 
   End If 
   '
   If OLD=1
      Screen 0
      Ink 0
      Bar 16,2 To 80,66
      Paste Bob 16,2,33
   Else 
      If OLD=2
         Screen 0
         Ink 0
         Bar 240,2 To 304,66
         Paste Bob 240,2,34
      Else 
         If OLD=3
            Screen 1
            Colour 2,$7D3
         Else 
            If OLD=4
               Screen 1
               Colour 11,$7D3
            Else 
               If OLD=5
                  Screen 2
                  Paste Bob 10,2,1
               Else 
                  If OLD=6
                     Screen 2
                     Paste Bob 10,38,2
                  Else 
                     If OLD=7
                        Screen 2
                        Paste Bob 10,74,3
                     Else 
                        If OLD=8
                           Screen 2
                           Bob Off 
                           Wait Vbl 
                        Else 
                           If OLD=9
                              Screen 2
                              Bob Off 
                              Wait Vbl 
                           Else 
                              If OLD=10
                                 Screen 2
                                 Bob Off 
                                 Wait Vbl 
                              Else 
                                 If OLD=11
                                    Screen 2
                                    Bob Off 
                                    Wait Vbl 
                                 End If 
                              End If 
                           End If 
                        End If 
                     End If 
                  End If 
               End If 
            End If 
         End If 
      End If 
   End If 
   '
   If CASE=1
      Screen 0
      Paste Bob 16,2,31
   Else 
      If CASE=2
         Screen 0
         Paste Bob 240,2,32
      Else 
         If CASE=3
            Screen 1
            Colour 2,$9F7
         Else 
            If CASE=4
               Screen 1
               Colour 11,$9F7
            Else 
               If CASE=5
                  Screen 2
                  Paste Bob 10,2,4
               Else 
                  If CASE=6
                     Screen 2
                     Paste Bob 10,38,5
                  Else 
                     If CASE=7
                        Screen 2
                        Paste Bob 10,74,6
                     Else 
                        If CASE=8
                           Screen 2
                           Bob 1,159,3,14
                        Else 
                           If CASE=9
                              Screen 2
                              Bob 1,214,3,14
                           Else 
                              If CASE=10
                                 Screen 2
                                 Bob 1,268,3,14
                              Else 
                                 If CASE=11
                                    Screen 2
                                    Bob 1,175,63,15
                                 End If 
                              End If 
                           End If 
                        End If 
                     End If 
                  End If 
               End If 
            End If 
         End If 
      End If 
   End If 
End Proc
Procedure HALL_OF_FAME
   '
   ' Affiche les 'Ten Top Scores' 
   '
   Shared HISCORE(),HISCORE$(),MSG_SCORES
   Autoback 0
   Flash Off 
   Palette 0,0,0,0
   Cls 0
   _MESSAGE[MSG_SCORES,0]
   Ink 1,0
   For I=1 To 10
      H$=Str$(HISCORE(I))-" "
      S$=Right$(Str$(I),2)
      If I=1
         S$=S$+"ST"
      Else 
         If I=2
            S$=S$+"ND"
         Else 
            If I=3
               S$=S$+"RD"
            Else 
               S$=S$+"TH"
            End If 
         End If 
      End If 
      S$=S$+"  "+String$("0",6-Len(H$))+H$+"  "+HISCORE$(I)
      Text 16,12*I+80,S$
   Next I
   Screen Copy Logic(0) To Physic(0)
   Autoback 2
End Proc
'
Procedure INIT_TABLEAU
   '
   ' Prépare tout pour le tableau à venir 
   '
   Shared LEVEL,VICT1,VICT2,CREDITS,ZIKON,ZIKVOL,SFXVOL
   Shared JOUEUR1,JOUEUR2,TIR1,TIR2,MORT1,MORT2,VITX(),VITY(),INV(),SUPER()
   Shared XD1,YD1,XD2,YD2,PARTIE
   Shared TEMPS1,TEMPS2,MXTEMPS1,MXTEMPS2,MEMETEMPS,MXTEMPSTOTAL
   Shared ENNEMI(),DANSLESPOMMES(),NBMONSTRES
   Shared TRESOR(),NBTRESORS,TRESOR_TOMBE()
   '
   ' *** Ecran
   '
   Auto View Off 
   Screen Open 0,320,256,32,Lowres
   Screen Hide 0
   Curs Off 
   Flash Off 
   Hide On 
   Wait Vbl 
   '
   Cls 0
   Unpack 9 To 1
   Screen 0
   Get Palette 1
   Wait Vbl 
   Screen Copy 1 To 0
   Wait Vbl 
   Screen Close 1
   Screen Display 0,128,40,320,256
   Ink 24,0
   Text 268,185,"LEVEL"
   Text 260,208,"CREDITS"
   Text 260,231,"OVERALL"
   Ink 22,0
   L$=Str$(LEVEL)-" "
   If Len(L$)=1
      L$="0"+L$
   End If 
   Text 280,195,L$
   L$=Str$(CREDITS)-" "
   If Len(L$)=1
      L$="0"+L$
   End If 
   Text 280,218,L$
   L$=(Str$(VICT1)-" ")+" /"+Str$(VICT2)
   If Len(Str$(VICT1))+Len(Str$(VICT2))=4
      L$=" "+L$
   End If 
   Text 260,241,L$
   '
   ' *** Temps
   '
   TEMPS1=MXTEMPS1
   If PARTIE=2
      If MEMETEMPS
         MXTEMPS2=MXTEMPS1
      End If 
      TEMPS2=MXTEMPS2
      Ink 4
      Bar 241,208-TEMPS2 To 254,207
   Else 
      Ink 8
      T=Min(MXTEMPSTOTAL*2,192)
      Bar 241,208-T To 254,207
      Ink 3
   End If 
   Bar 1,208-TEMPS1 To 14,207
   '
   Proc UPDSCORE[0,0]
   '
   Double Buffer 
   Autoback 1
   Priority Reverse On 
   '
   '
   ' *** Briques et trésors 
   '
   '
   NBTRESORS=0
   For X=1 To 14
      For Y=1 To 14
         C(X,Y)=C2(X,Y)
         If C(X,Y)>0
            If C(X,Y)=72
               C(X,Y)=0
               If NBTRESORS<4
                  Inc NBTRESORS
                  Bob TRESOR(NBTRESORS),X*16,Y*16-1,43
               End If 
            Else 
               Paste Bob X*16,Y*16-16,C(X,Y)
            End If 
         End If 
      Next Y
   Next X
   '
   For I=1 To NBTRESORS
      TRESOR_TOMBE(I)=0
   Next I
   '
   '
   ' ***  Joueurs 
   '
   '
   X=XD1*16
   Y=YD1*16-1
   Bob JOUEUR1,X,Y,178
   Channel 1 To Bob JOUEUR1
   Channel 10 To Bob JOUEUR1
   Amal 10,"Anim 0,(251,8)(252,8)(253,8);"
   '
   If PARTIE=2
      X=XD2*16
      Y=YD2*16-1
      Bob JOUEUR2,X,Y,177
      Channel 2 To Bob JOUEUR2
      Channel 11 To Bob JOUEUR2
      Amal 11,"Anim 0,(248,8)(249,8)(250,8);"
   End If 
   '
   Limit Bob 16,0 To 240,224
   '
   '
   ' *** Tir 1
   '
   A$="      Let RA=0;"
   A$=A$+"Loop:"
   A$=A$+"   If RA=0 then Jump Loop;"
   A$=A$+"   Let X=RC; Let Y=RD;"
   A$=A$+"   If RE=1 then Jump M;"
   A$=A$+"   Let A=45;"
   A$=A$+"   Move -48,0,12;"
   A$=A$+"   Jump N;"
   A$=A$+"M: Let A=44"
   A$=A$+"   Move 48,0,12;"
   A$=A$+"N: Let Y=320;"
   A$=A$+"   Let RA=0"
   A$=A$+"   Jump Loop;"
   '
   Bob TIR1,10,320,44
   Limit Bob TIR1,16,0 To 240,224
   Channel 3 To Bob TIR1
   Amal 3,A$
   '
   If PARTIE=2
      '
      ' *** Tir 2
      '
      A$="      Let RF=0;"
      A$=A$+"Loop:"
      A$=A$+"   If RF=0 then Jump Loop;"
      A$=A$+"   Let X=RH; Let Y=RI;"
      A$=A$+"   If RJ=1 then Jump M;"
      A$=A$+"   Let A=45;"
      A$=A$+"   Move -48,0,12;"
      A$=A$+"   Jump N;"
      A$=A$+"M: Let A=44;"
      A$=A$+"   Move 48,0,12;"
      A$=A$+"N: Let Y=320;"
      A$=A$+"   Let RF=0"
      A$=A$+"   Jump Loop;"
      '
      Bob TIR2,30,320,45
      Limit Bob TIR2,16,0 To 240,224
      Channel 4 To Bob TIR2
      Amal 4,A$
   End If 
   '
   '
   ' *** Ennemis
   '
   '
   FOUTRE_MONSTRES
   '
   For I=1 To NBMONSTRES
      DANSLESPOMMES(I)=0
   Next I
   '
   ' *** Divers 
   '
   Clear Key 
   '
   Auto View On 
   Screen Show 0
   '
   Volume SFXVOL
   Mvolume 0
   If ZIKON
      MUSIQUE[2,ZIKVOL]
   End If 
   '
   '
   Wait Vbl 
   '
End Proc
Procedure BONUS
   '
   '  Afficher un nouveau bonus 
   '
   Shared BONUS,LEVEL,CURBONUS
   Do 
      X=Rnd(13)+1
      Y=Rnd(13)+1
      If C(X,Y)=0
         If Y=14
            Exit 
         Else 
            If C(X,Y+1)>0
               Exit 
            End If 
         End If 
      End If 
   Loop 
   Repeat 
      B=Rnd(31)+48
   Until(B<>58) and(B<>59)
   If B=79
      A$="A0,(79,20)(80,20);"
   Else 
      If B=57
         A$="A0,(57,2)(58,2)(59,2);"
      Else 
         If B=60
            A$="A0,(60,2)(61,2)(62,2)(63,2)(64,2)(65,2);"
         Else 
         End If 
      End If 
   End If 
   Bob BONUS,X*16,Y*16-16,B
   Amal BONUS,A$
   CURBONUS=B
End Proc
Procedure EFFET_BONUS[N]
   '
   '  S'occupe des bonus ( j1 & j2 )
   '
   Shared CURBONUS,SFXON,CREDITS,JOUEUR1,JOUEUR2,TEMPS1,TEMPS2
   Shared PARALIZED1,PARALIZED2,MXTEMPS1,MXTEMPS2,MEMETEMPS
   Shared VITX(),INV(),SUPER(),SCORE1,SCORE2,PARTIE
   '
   A=CURBONUS-74
   If N=1
      VOIX=%1
   Else 
      VOIX=%10
   End If 
   If SFXON=False
      VOIX=%0
   End If 
   '
   '---------------- ?
   If A=13
      Repeat 
         A=Rnd(16)+1
      Until A<>13
   End If 
   ' ------------------- Autre
   L$=("B"+Str$(A))-" "
   Gosub L$
   Pop Proc
   '
   '
   ' ------------------- Epée 
   B1:
   Sam Play VOIX,1
   UPDSCORE[N,200]
   Return 
   ' ------------------- Anneau 'Sonic' 
   B2:
   Sam Play VOIX,1
   UPDSCORE[N,500]
   Return 
   ' ------------------- Citron 
   B3:
   Sam Play VOIX,1
   UPDSCORE[N,500]
   Return 
   ' ------------------- Fraise 
   B4:
   Sam Play VOIX,1
   UPDSCORE[N,500]
   Return 
   ' ------------------- Fleur bleue
   B5:
   Sam Play VOIX,1
   UPDSCORE[N,-100]
   Return 
   ' ------------------- Fleur rouge
   B6:
   Sam Play VOIX,1
   UPDSCORE[N,-100]
   Return 
   ' ------------------- Diamant brillant 
   B7:
   Sam Play VOIX,1
   UPDSCORE[N,5000]
   Return 
   ' ------------------- Diamant carré
   B8:
   Sam Play VOIX,1
   UPDSCORE[N,2500]
   Return 
   ' ------------------- "Soleil" 
   B9:
   Sam Play VOIX,1
   UPDSCORE[N,200]
   Return 
   ' ------------------- Coeur
   B10:
   Sam Play VOIX,1
   CREDITS=Min(99,CREDITS+1)
   L$=Str$(CREDITS)-" "
   If Len(L$)=1
      L$="0"+L$
   End If 
   Ink 22,0
   Text 280,218,L$
   Return 
   ' ------------------- Skate board
   B11:
   Sam Play VOIX,1
   VITX(N)=4
   Return 
   ' ------------------- Patin à roulettes
   B12:
   Sam Play VOIX,1
   UPDSCORE[N,250]
   Return 
   ' ------------------- Gant de boxe 
   B14:
   If PARTIE=2
      B=Rnd(2)
      If B>0
         Add N,1,1 To 2
      End If 
   End If 
   If N=1
      If PARALIZED1=0
         Sam Play VOIX,4
         PARALIZED1=1
         Amal Freeze 1
         Amal On 10
         UPDSCORE[2,750]
      End If 
   Else 
      If PARALIZED2=0
         Sam Play VOIX,5
         PARALIZED2=1
         Amal Freeze 2
         Amal On 11
         UPDSCORE[1,750]
      End If 
   End If 
   Return 
   ' ------------------- Shaker 
   B15:
   Sam Play VOIX,1
   If PARTIE=2
      Swap SCORE1,SCORE2
   Else 
      SCORE1=(SCORE1/2)+Rnd(SCORE1/2)
   End If 
   UPDSCORE[0,0]
   Return 
   ' ------------------- Verre de coca
   B16:
   Sam Play VOIX,3
   Ink 2+PARTIE
   If(N=2) and(MEMETEMPS=False)
      TEMPS2=MXTEMPS2
      Bar 241,208-TEMPS2 To 254,207
   Else 
      TEMPS1=MXTEMPS1
      Bar 1,208-TEMPS1 To 14,207
      If MEMETEMPS
         Bar 241,208-TEMPS1 To 254,207
      End If 
   End If 
   Return 
   ' ------------------- Joystick 
   B17:
   Sam Play VOIX,1
   UPDSCORE[N,500]
   If(PARTIE=1) or(N=2)
      INV(1)=True
   Else 
      INV(2)=True
   End If 
   Return 
   ' ------------------- Invincibilité
   B99:
   Sam Play VOIX,12345
   UPDSCORE[N,100]
   SUPER(N)=True
   Return 
   '
End Proc
'
Procedure J1_ANIM
   '
   ' Met en place l'anim qu'il faut 
   '
   Shared STATE1,SENS1,STANDING,WALKING,JUMPING,FALLING,DROITE,GAUCHE
   Shared GLOUGLOU1,MORT1,LONGTEMPS1
   If MORT1=True
      A$="Let A=238; Move 0,300,75;"
   Else 
      If STATE1=STANDING
         If SENS1=DROITE
            A$="Let A=178;"
         Else 
            A$="Let A=199;"
         End If 
      End If 
      If STATE1=WALKING
         If SENS1=DROITE
            A$="Anim 0,(179,2)(180,2)(181,2)(182,2)(181,2)(180,2);"
         Else 
            A$="Anim 0,(198,2)(197,2)(196,2)(195,2)(196,2)(197,2);"
         End If 
      End If 
      If STATE1=JUMPING
         If SENS1=DROITE
            A$="Let A=232;"
         Else 
            A$="Let A=241;"
         End If 
      End If 
      If STATE1=FALLING
         Amreg(1,1)=SENS1
         A$="      For R0=0 To 7;"
         A$=A$+"   If R1=-1 the Jump A;"
         A$=A$+"   Let A=233; Jump B;"
         A$=A$+"A: Let A=242;"
         A$=A$+"B: Next R0;"
         A$=A$+"   For R0=0 To 7;"
         A$=A$+"   If R1=-1 the Jump C;"
         A$=A$+"   Anim 0,(234,2)(235,2); Jump D;"
         A$=A$+"C: Anim 0,(243,2)(244,2);"
         A$=A$+"D: Next R0;"
         A$=A$+"   If R1=-1 then Jump E;"
         A$=A$+"   Anim 1,(236,1); Jump F;"
         A$=A$+"E: Anim 1,(245,1);"
         A$=A$+"F: Pause;"
         A$=A$+"G: Pause;"
         A$=A$+"   If R1=-1 the Jump H;"
         A$=A$+"   Anim 1,(237,2); Jump G;"
         A$=A$+"H: Anim 1,(246,2); Jump G;"
      End If 
   End If 
   Limit Bob 1,16,0 To 240,224
   Amal 1,A$
   Amal On 1
   GLOUGLOU1=0
End Proc
Procedure J2_ANIM
   '
   ' Met en place l'anim qu'il faut 
   '
   Shared STATE2,SENS2,STANDING,WALKING,JUMPING,FALLING,DROITE,GAUCHE
   Shared GLOUGLOU2,MORT2,LONGTEMPS2
   If MORT2=True
      A$="Let A=220; Move 0,300,75;"
   Else 
      If STATE2=STANDING
         If SENS2=DROITE
            A$="Let A=156;"
         Else 
            A$="Let A=177;"
         End If 
      End If 
      If STATE2=WALKING
         If SENS2=DROITE
            A$="Anim 0,(157,2)(158,2)(159,2)(160,2)(159,2)(158,2);"
         Else 
            A$="Anim 0,(176,2)(175,2)(174,2)(173,2)(174,2)(175,2);"
         End If 
      End If 
      If STATE2=JUMPING
         If SENS2=DROITE
            A$="Let A=214;"
         Else 
            A$="Let A=223;"
         End If 
      End If 
      If STATE2=FALLING
         A$="      For R0=0 To 7;"
         A$=A$+"   If R1=-1 the Jump A;"
         A$=A$+"   Let A=215; Jump B;"
         A$=A$+"A: Let A=224;"
         A$=A$+"B: Next R0;"
         A$=A$+"   For R0=0 To 7;"
         A$=A$+"   If R1=-1 the Jump C;"
         A$=A$+"   Anim 0,(216,2)(217,2); Jump D;"
         A$=A$+"C: Anim 0,(225,2)(226,2);"
         A$=A$+"D: Next R0;"
         A$=A$+"   If R1=-1 then Jump E;"
         A$=A$+"   Anim 1,(218,1); Jump F;"
         A$=A$+"E: Anim 1,(227,1);"
         A$=A$+"F: Pause;"
         A$=A$+"G: Pause;"
         A$=A$+"   If R1=-1 the Jump H;"
         A$=A$+"   Anim 1,(219,2); Jump G;"
         A$=A$+"H: Anim 1,(228,2); Jump G;"
      End If 
   End If 
   Limit Bob 2,16,0 To 240,224
   Amal 2,A$
   Amreg(2,1)=SENS2
   Amal On 2
   GLOUGLOU2=0
End Proc
Procedure _MESSAGE[N,W]
   '
   ' Affiche les messages 
   '
   ' 1 = OH NO !
   ' 2 = GAME OVER
   ' 3 = GET READY ...
   ' 4 = THE WINNER IS ...
   ' 5 = EGALITE
   ' 6 = BONUS STAGE
   ' 7 = OPTIONS
   ' 8 = SCORES 
   '
   Shared LEVEL
   Bank Swap 1,2
   L=Len(Str$(LEVEL)-" ")
   If L=1
      L1=28
      L2=LEVEL+28
   Else 
      L1=(LEVEL/10)+28
      L2=(LEVEL mod 10)+28
   End If 
   Cls 0
   Palette 0,0,0,0
   Restore "M"+Str$(N)-" "
   Read NB
   For I=1 To NB
      Read X,Y,A
      If Y=0
         Y=80
      End If 
      If N=3
         If I=6 and A=29
            Add X,10
         End If 
      End If 
      Paste Bob X,Y,A
   Next I
   If N<7
      Fade 1,0,$FFF,$AAA,$555
      Wait 15
      If N<>3
         While Fire(1) or Fire(0)
            Multi Wait 
         Wend 
         Clear Key 
         Repeat 
            Multi Wait 
         Until Fire(1) or Fire(0) or Len(Inkey$)
         Fade 1
         Wait 15
         Cls 0
      End If 
   End If 
   Bank Swap 1,2
   '
   M1: Data 5,66,,15,103,,8,152,,14,186,,15,232,,38
   M2: Data 8,30,,7,62,,1,98,,13,130,,5,170,,15,202,,22,244,,5,270,,18
   M3: Data 15,45,50,12,73,50,5,97,50,22,139,50,5,163,50,12,205,50,L1,235-(L2=29)*4,50,L2
   Data 35,100,7,69,100,5,93,100,20,137,100,18,165,100,5,187,100,1,225,100,4,250,100,25
   M4: Data 18,20,70,20,50,70,8,80,70,5,124,70,23,158,70,9,180,70,14,214,70,14,247,70,5,271,70,18
   Data 20,110,9,42,110,19,87,110,16,116,110,12,144,110,1,176,110,25,206,110,5
   Data 232,110,18,272-(W=1)*8,110,W+28
   M5: Data 7,54,77,27,80,,7,114,,1,154,,12,184,,9,206,,20,236,77,27
   M6: Data 10,78,60,2,108,60,15,145,60,14,180,60,21,211,60,19
   Data 82,110,19,112,110,20,136,110,1,174,110,7,210,110,5
   M7: Data 7,57,20,15,91,20,16,120,20,20,149,20,9,170,20,15,204,20,14,237,20,19
   M8: Data 6,70,26,19,98,26,3,131,26,15,164,26,18,192,26,5,216,26,19
End Proc
Procedure MUSIQUE[N,V]
   '
   ' Lance une musique
   '
   Shared ZIKPLAYING,QUELLEZIK
   Mvolume 0
   If(QUELLEZIK<>N) or(Length(3)=0)
      Erase 3
      Erase 5
      If N=1
         ' My First 
         Load "Twin:Zik-Intro.Abk",3
      Else 
         ' Magic Forest 
         Load "Twin:Zik-Jeu.Abk",3
      End If 
   End If 
   QUELLEZIK=N
   Led Off 
   Music 1
   Mvolume V
   Wait Vbl 
   ZIKPLAYING=True
End Proc
Procedure MUSIQUE_OFF
   '
   ' Arrête la musique en Fading
   '
   Shared ZIKVOL,ZIKPLAYING
   For I=ZIKVOL To 0 Step -1
      Mvolume I
      Wait Vbl 
   Next I
   Music Off 
   Led On 
   ZIKPLAYING=False
End Proc
Procedure UPDSCORE[JOUEUR,PLUS]
   '
   ' Met à jour les scores, s'occupe des affichages en bas de l'écran 
   '
   Shared SCORE1,SCORE2,MORT1,MORT2,PARALIZED1,PARALIZED2,NAME1$,NAME2$,HURRY,PARTIE
   '
   If JOUEUR=1
      Add SCORE1,PLUS
      SCORE1=Max(0,SCORE1)
   Else 
      Add SCORE2,PLUS
      SCORE2=Max(0,SCORE2)
   End If 
   '
   If HURRY=True
      Ink 8,0
      Text 4,242,"  HURRY UP !!  "
      If PARTIE=2
         Text 132,242,"  HURRY UP !!  "
      End If 
   Else 
      Ink 11,0
      If MORT1=False
         If PARALIZED1=False
            Text 8,242,"SCORE"+Space$(8-Len(Str$(SCORE1)))+Str$(SCORE1)
         Else 
            Text 8,242," Oh my head ! "
         End If 
      Else 
         Text 8,242,"    OH NO !   "
      End If 
      If PARTIE=2
         If MORT2=False
            If PARALIZED2=False
               Text 136,242,"SCORE"+Space$(8-Len(Str$(SCORE2)))+Str$(SCORE2)
            Else 
               Text 136,242," Oh my head ! "
            End If 
         Else 
            Text 136,242,"    OH NO !   "
         End If 
      End If 
   End If 
End Proc
Procedure CTR[Y,A$]
   '
   ' Utilisé par 'Options'
   '
   Text(320-Len(A$)*8)/2,Y,A$
End Proc
'
Procedure CHARGER[LEVEL]
   '
   Shared NBMONSTRES,MX(),MY(),NBSQ(),MA(),MH(),MV(),MT()
   Shared MEMETEMPS,MXTEMPS1,MXTEMPS2,MXTEMPSTOTAL,COM$
   Shared XD1,YD1,XD2,YD2
   '
   Paper 0
   Pen 1
   Locate 0,25
   L$=("Twin:Levels/L"+Str$(LEVEL))-" "
   If Exist(L$)
      Open In 1,L$
      B$=Input$(1,196)
      I=0
      For X=1 To 14
         For Y=1 To 14
            Inc I
            C2(X,Y)=Asc(Mid$(B$,I,1))
         Next Y
      Next X
      Input #1,B$
      Input #1,MXTEMPS1
      Input #1,MXTEMPS2
      Input #1,MEMETEMPS
      Input #1,MXTEMPSTOTAL
      Input #1,NBMONSTRES
      Input #1,COM$
      If NBMONSTRES>0
         For I=1 To NBMONSTRES
            Input #1,MX(I)
            Input #1,MY(I)
            Input #1,NBSQ(I)
            If NBSQ(I)>0
               For J=1 To NBSQ(I)
                  Input #1,MA(I,J)
                  Input #1,MH(I,J)
                  Input #1,MV(I,J)
                  Input #1,MT(I,J)
               Next J
            End If 
         Next I
      End If 
      If Not(Eof(1))
         Input #1,XD1
         Input #1,YD1
         Input #1,XD2
         Input #1,YD2
      Else 
         XD1=1
         YD1=14
         XD2=14
         YD2=14
      End If 
      Close 1
      Centre COM$
   Else 
      Centre "Plus de tableaux !"
      FIN
   End If 
End Proc
Procedure CHARGE_SCORES
   '
   ' Charge la table des scores 
   '
   Shared HISCORE$(),HISCORE()
   Open In 1,"Twin:High-Scores"
   For I=1 To 10
      Input #1,HISCORE$(I)
      Input #1,HISCORE(I)
   Next I
   Close 1
End Proc
Procedure SAUVE_SCORES
   '
   ' Sauve les scores 
   '
   Shared HISCORE$(),HISCORE()
   Open Out 1,"Twin:High-Scores"
   For I=1 To 10
      Print #1,HISCORE$(I)
      Print #1,HISCORE(I)
   Next I
   Close 1
End Proc
Procedure CHARGE_PREFS
   '
   ' Charge les préférences 
   '
   Shared ZIKON,ZIKVOL,SFXON,SFXVOL,PUBON,NAME1$,NAME2$,HISAVEON,PARTIE
   Open In 1,"Twin:Prefs"
   Input #1,ZIKON
   Input #1,ZIKVOL
   Input #1,SFXON
   Input #1,SFXVOL
   Input #1,PUBON
   Input #1,NAME1$
   Input #1,NAME2$
   Input #1,HISAVEON
   Input #1,PARTIE
   Close 1
End Proc
Procedure SAUVE_PREFS
   '
   ' Sauve les prefs
   '
   Shared ZIKON,ZIKVOL,SFXON,SFXVOL,PUBON,NAME1$,NAME2$,HISAVEON,PARTIE
   Open Out 1,"Twin:Prefs"
   Print #1,ZIKON
   Print #1,ZIKVOL
   Print #1,SFXON
   Print #1,SFXVOL
   Print #1,PUBON
   Print #1,NAME1$
   Print #1,NAME2$
   Print #1,HISAVEON
   Print #1,PARTIE
   Close 1
End Proc
Procedure BANK_LOAD[F$,BANK]
   '
   ' PowerMob DecryptFile adapté
   ' cf. AMOS_Save2:Utils/File_Coder.AMOS 
   '
   CLE$="Twin-Baston est le meilleur jeu du monde ! §è!('çà"
   '
   Open In 1,F$
   L=Lof(1)
   Close 1
   L2=Len(CLE$)
   '
   Reserve As Work 13,L
   Reserve As Work BANK,L
   P1=Start(13)
   P2=Start(BANK)
   P3=Varptr(CLE$)
   Bload F$,P1
   '
   For I=1 To L
      Poke P2,Peek(P3)-Peek(P1)
      Inc P1
      Inc P2
      Add P3,1,1 To L2
   Next I
   '
   Erase 13
   '
End Proc
'
Procedure PUB[N]
   '
   ' On se retrouve après ce 'A tout de suite'
   '
   S=Screen
   Screen Hide S
   Load "Twin:Img-Publogo.Abk",10
   Unpack 10 To 4
   Erase 10
   Wait 60
   F$=("Twin:Img-Pub"+Str$(N)+".Abk")-" "
   If Exist(F$)
      Load F$,10
   Else 
      Load "Twin:Img-Pub1.Abk",10
   End If 
   Unpack 10 To 4
   Erase 10
   Repeat 
      Multi Wait 
   Until Fire(1) or Fire(0)
   Fade 1
   Screen Close 4
   Screen S
   Screen Show S
   Wait 15
End Proc
Procedure JEU[N]
   '
   ' Renvoie vers le jeu qu'il faut...
   '
   N=N mod 2
   If N=1
      JEU1
   End If 
   If N=0
      JEU2
   End If 
End Proc
Procedure JEU1
   '
   ' Tron light cycles
   '
   Shared MSG_EGALITE,MSG_WINNER,SCORE1,SCORE2
   '
   Palette 0,$F,$F00,$FFF
   Paper 0
   Pen 3
   Cls 0
   Centre At(,0)+"EVITEZ LES COLLISIONS !"
   Centre At(,2)+"BONUS 10.000 Pts"
   Autoback 1
   Bob Update Off 
   '
   Ink 3,0
   Box 0,32 To 319,255
   '
   X1=32
   Y1=64
   S1=8
   P1=0
   '
   X2=280
   Y2=200
   S2=4
   P2=0
   '
   Repeat 
      '
      '--------- Joueur 1 ---------
      '
      If Jup(1) and S1<>2
         S1=1
      End If 
      If Jdown(1) and S1<>1
         S1=2
      End If 
      If Jleft(1) and S1<>8
         S1=4
      End If 
      If Jright(1) and S1<>4
         S1=8
      End If 
      '
      If S1=1
         Dec Y1
      End If 
      If S1=2
         Inc Y1
      End If 
      If S1=4
         Dec X1
      End If 
      If S1=8
         Inc X1
      End If 
      '
      If Point(X1,Y1)>0
         FIN=True
         P1=True
      End If 
      '
      '-------- Joueur 2 ----------
      '
      If Jup(0) and S2<>2
         S2=1
      End If 
      If Jdown(0) and S2<>1
         S2=2
      End If 
      If Jleft(0) and S2<>8
         S2=4
      End If 
      If Jright(0) and S2<>4
         S2=8
      End If 
      '
      If S2=1
         Dec Y2
      End If 
      If S2=2
         Inc Y2
      End If 
      If S2=4
         Dec X2
      End If 
      If S2=8
         Inc X2
      End If 
      '
      If Point(X2,Y2)>0
         FIN=True
         P2=True
      End If 
      '
      '-------- Affichage ---------
      '
      Plot X1,Y1,1
      Plot X2,Y2,2
      '
      Wait Vbl 
      '
   Until FIN
   '
   Bob Update On 
   Fade 1
   Wait 15
   Cls 0
   '
   If(P1=True) and(P2=True)
      _MESSAGE[MSG_EGALITE,0]
      Add SCORE1,5000
      Add SCORE2,5000
   Else 
      If P2=True
         _MESSAGE[MSG_WINNER,1]
         Add SCORE1,10000
      Else 
         _MESSAGE[MSG_WINNER,2]
         Add SCORE2,10000
      End If 
   End If 
   '
End Proc
Procedure JEU2
   '
   ' Tableau bonus: secouer son joystick
   '
   Shared MSG_EGALITE,MSG_WINNER
   '
   Palette 0,$F,$F00,$FFF
   Paper 0
   Pen 3
   '
   Autoback 1
   Centre At(,3)+"SECOUEZ-MOI DE GAUCHE A DROITE !"
   Centre At(,5)+"BONUS 10.000 Pts"
   Ink 3,0
   Bar 160,96 To 161,130
   '
   T=499
   J1=8
   J2=8
   G=0
   '
   Repeat 
      J=Joy(1)
      If((J=4) and(J1=8)) or((J=8) and(J1=4))
         J1=J
         G=Min(128,G+2)
      End If 
      J=Joy(0)
      If((J=4) and(J2=8)) or((J=8) and(J2=4))
         J2=J
         G=Max(-128,G-2)
      End If 
      Ink 1,0
      Bar 30,100 To 160+G,126
      Ink 2,0
      Bar 160+G+1,100 To 290,126
      Ink 3,0
      Text 120,180,"TEMPS ="+Str$(T/50+1)+" "
      Wait Vbl 
      Dec T
   Until T=0
   '
   Fade 1
   Wait 16
   Cls 0
   '
   If G=0
      _MESSAGE[MSG_EGALITE,0]
      Add SCORE1,5000
      Add SCORE2,5000
   Else 
      If G>0
         _MESSAGE[MSG_WINNER,1]
         Add SCORE1,10000
      Else 
         _MESSAGE[MSG_WINNER,2]
         Add SCORE2,10000
      End If 
   End If 
   '
End Proc
Procedure DESSINER
   S=Screen
   Screen 0
   Cls 0,16,0 To 240,224
   For X=1 To 14
      For Y=1 To 14
         If C(X,Y)>0
            Paste Bob X*16,(Y-1)*16,C(X,Y)
         End If 
      Next Y
   Next X
   Screen S
End Proc
Procedure FOUTRE_MONSTRES
   Shared NBMONSTRES,NBSQ(),MX(),MY(),MA(),MH(),MV(),MT(),SQ$()
   If NBMONSTRES>0
      For M=5 To NBMONSTRES+4
         I=M-4
         Bob M,MX(I),MY(I),84
         Channel M To Bob M
         A$="L:"
         For J=1 To NBSQ(I)
            A$=A$+SQ$(MA(I,J))
            A$=A$+"M"+Str$(MH(I,J))+","+Str$(MV(I,J))+","+Str$(MT(I,J))+";"
         Next J
         A$=A$-" "
         Amal M,A$+"JL;"
      Next M
   End If 
End Proc
'
'
' Conserve 
DT:
Data "A0,(87,4)(86,4)(85,4)(84,4);","A1,(88,2)(89,2);FR0=1T4;NR0;"
Data "A0,(93,4)(92,4)(91,4)(90,4);","A1,(89,2)(88,2);FR0=1T4;NR0;"
'
' Robot
Data "A0,(100,8)(101,8)(102,8);","A1,(103,4)(104,4);FR0=1T8;NR0;"
Data "A0,(105,8)(106,8)(107,8);","A1,(104,4)(103,4);FR0=1T8;NR0;"
'
' PacMan 
Data "A0,(116,8)(117,8);"
Data "A0,(118,8)(119,8);"
'
' Serpent
Data "A0,(126,4)(127,4)(128,4);"
Data "A0,(129,4)(130,4)(131,4);"
'
' Octopus
Data "A0,(120,4)(121,4)(122,4);"
Data "A0,(123,4)(124,4)(125,4);"
'
' Fantôme
Data "A1,(146,1);","A1,(147,2)(148,2)(149,2)(150,2)(151,2)(152,2)(153,2)(154,2);FR0=1T17;NR0;"
Data "A1,(155,1);","A1,(154,2)(153,2)(152,2)(151,2)(150,2)(149,2)(148,2)(147,2);FR0=1T17;NR0;"
'
' Heaume 
Data "A0,(94,8)(95,8)(96,8);"
Data "A0,(97,8)(98,8)(99,8);"
'
' Punk 
Data "A0,(108,4)(109,4)(110,4);","A1,(111,4)(112,4);FR0=1T8;NR0;"
Data "A0,(113,4)(114,4)(115,4);","A1,(112,4)(111,4);FR0=1T8;NR0;"
'
' Koopa
Data "A0,(132,4)(133,4)(134,4);","A1,(135,4)(136,4);FR0=1T8;NR0;"
Data "A0,(137,4)(138,4)(139,4);","A1,(136,4)(135,4);FR0=1T8;NR0;"
'
' Koopa ailé 
Data "A0,(140,8)(141,8)(142,8);"
Data "A0,(143,8)(144,8)(145,8);"
